import Mathlib.NumberTheory.LSeries.DirichletContinuation
import Mathlib.NumberTheory.LSeries.Dirichlet
import Mathlib.NumberTheory.LSeries.Positivity

/-!
# Non-vanishing of `L(Ï‡, 1)` for nontrivial quadratic characters `Ï‡`

The main result of this file is the statement
`DirichletCharacter.LFunction_at_one_ne_zero_of_quadratic`, which says that if `Ï‡` is
a nontrivial (`Ï‡ â‰  1`) quadratic (`Ï‡^2 = 1`) Dirichlet character, then the value of
its L-function at `s = 1` is nonzero.

This is an important step in the proof of
*Dirichlet's Theorem on Primes in Arithmetic Progression*.
-/

/-!
### Auxiliary lemmas
-/

-- Mathlib.Analysis.Calculus.Deriv.Slope or Mathlib.LinearAlgebra.AffineSpace.Slope
lemma HasDerivAt.continuousAt_div {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [DecidableEq ğ•œ]
    {f : ğ•œ â†’ ğ•œ} {c a : ğ•œ} (hf : HasDerivAt f a c) :
    ContinuousAt (Function.update (fun x â†¦ (f x - f c) / (x - c)) c a) c := by
  rw [â† slope_fun_def_field]
  exact continuousAt_update_same.mpr <| hasDerivAt_iff_tendsto_slope.mp hf

-- Mathlib.NumberTheory.MulChar.Basic
/-- A multiplicative character `Ï‡` into an integral domain is quadratic
if and only if `Ï‡^2 = 1`. -/
lemma MulChar.isQuadratic_iff_sq_eq_one {M R : Type*} [CommMonoid M] [CommRing R]
    [NoZeroDivisors R] [Nontrivial R] {Ï‡ : MulChar M R} :
    IsQuadratic Ï‡ â†” Ï‡ ^ 2 = 1:= by
  refine âŸ¨fun h â†¦ ext (fun x â†¦ ?_), fun h x â†¦ ?_âŸ©
  Â· rw [one_apply_coe, Ï‡.pow_apply_coe]
    rcases h x with H | H | H
    Â· exact (not_isUnit_zero <| H â–¸ IsUnit.map Ï‡ <| x.isUnit).elim
    Â· simp only [H, one_pow]
    Â· simp only [H, even_two, Even.neg_pow, one_pow]
  Â· by_cases hx : IsUnit x
    Â· refine .inr <| sq_eq_one_iff.mp ?_
      rw [â† Ï‡.pow_apply' two_ne_zero, h, MulChar.one_apply hx]
    Â· exact .inl <| map_nonunit Ï‡ hx

namespace DirichletCharacter

-- Mathlib.NumberTheory.DirichletCharacter.LSeries
open ArithmeticFunction in
/-- The arihmetic function associated to a Dirichlet character is multiplicative. -/
lemma isMultiplicative_toArithmeticFunction {N : â„•} {R : Type*} [CommMonoidWithZero R]
    (Ï‡ : DirichletCharacter R N) :
    (toArithmeticFunction (Ï‡ .)).IsMultiplicative := by
  refine IsMultiplicative.iff_ne_zero.mpr âŸ¨?_, fun {m} {n} hm hn _ â†¦ ?_âŸ©
  Â· simp only [toArithmeticFunction, coe_mk, one_ne_zero, â†“reduceIte, Nat.cast_one, map_one]
  Â· simp only [toArithmeticFunction, coe_mk, mul_eq_zero, hm, hn, false_or, Nat.cast_mul, map_mul,
      if_false]

lemma apply_eq_toArithmeticFunction_apply {N : â„•} {R : Type*} [CommMonoidWithZero R]
    (Ï‡ : DirichletCharacter R N) {n : â„•} (hn : n â‰  0) :
    Ï‡ n = toArithmeticFunction (Ï‡ Â·) n := by
  simp only [toArithmeticFunction, ArithmeticFunction.coe_mk, hn, â†“reduceIte]

/-!
### Convolution of a Dirichlet character with Î¶

We define `DirichletCharacter.zetaMul Ï‡` to be the arithmetic function obtained by
taking the product (as arithmetic functions = Dirichlet convolution) of the
arithmetic function `Î¶` with `Ï‡`.

We then show that for a quadratic character `Ï‡`, this arithmetic function is multiplicative
and takes nonnegative real values.
-/

open Complex ArithmeticFunction

variable {N : â„•}

/-- The complex-valued arithmetic function that is the convolution of the constant
function `1` with `Ï‡`. -/
def zetaMul (Ï‡ : DirichletCharacter â„‚ N) : ArithmeticFunction â„‚ :=
  .zeta * toArithmeticFunction (Ï‡ Â·)

/-- The arithmetic function `zetaMul Ï‡` is multiplicative. -/
lemma isMultiplicative_zetaMul (Ï‡ : DirichletCharacter â„‚ N) : Ï‡.zetaMul.IsMultiplicative :=
  isMultiplicative_zeta.natCast.mul <| isMultiplicative_toArithmeticFunction Ï‡

lemma LSeriesSummable_zetaMul (Ï‡ : DirichletCharacter â„‚ N) {s : â„‚} (hs : 1 < s.re) :
    LSeriesSummable Ï‡.zetaMul s := by
  refine ArithmeticFunction.LSeriesSummable_mul (LSeriesSummable_zeta_iff.mpr hs) <|
    LSeriesSummable_of_bounded_of_one_lt_re (m := 1) (fun n hn â†¦ ?_) hs
  simpa only [toArithmeticFunction, coe_mk, hn, â†“reduceIte, â† Complex.norm_eq_abs]
  using norm_le_one Ï‡ _

-- We use the ordering on `â„‚` given by comparing real parts for fixed imaginary part
open scoped ComplexOrder

lemma zetaMul_prime_pow_nonneg {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Ï‡ ^ 2 = 1) {p : â„•}
    (hp : p.Prime) (k : â„•) :
    0 â‰¤ zetaMul Ï‡ (p ^ k) := by
  simp only [zetaMul, toArithmeticFunction, coe_zeta_mul_apply, coe_mk,
    Nat.sum_divisors_prime_pow hp, pow_eq_zero_iff', hp.ne_zero, ne_eq, false_and, â†“reduceIte,
    Nat.cast_pow, map_pow]
  rcases MulChar.isQuadratic_iff_sq_eq_one.mpr hÏ‡ p with h | h | h
  Â· refine Finset.sum_nonneg fun i _ â†¦ ?_
    simp only [h, le_refl, pow_nonneg]
  Â· refine Finset.sum_nonneg fun i _ â†¦ ?_
    simp only [h, one_pow, zero_le_one]
  Â· simp only [h, neg_one_geom_sum]
    split_ifs
    exacts [le_rfl, zero_le_one]

/-- `zetaMul Ï‡` takes nonnegative real values when `Ï‡` is a quadratic character. -/
lemma zetaMul_nonneg {Ï‡ : DirichletCharacter â„‚ N} (hÏ‡ : Ï‡ ^ 2 = 1) (n : â„•) :
    0 â‰¤ zetaMul Ï‡ n := by
  rcases eq_or_ne n 0 with rfl | hn
  Â· simp only [ArithmeticFunction.map_zero, le_refl]
  Â· simpa only [Ï‡.isMultiplicative_zetaMul.multiplicative_factorization _ hn] using
      Finset.prod_nonneg
        fun p hp â†¦ zetaMul_prime_pow_nonneg hÏ‡ (Nat.prime_of_mem_primeFactors hp) _


/-!
### "Bad" Dirichlet characters

Our goal is to show that `L Ï‡ 1 â‰  0` when `Ï‡` is a (nontrivial) quadratic Dirichlet character.
To do that, we package the contradictory properties in a structure `DirichletCharacter.BadChar`
and derive further statements eventually leading to a contradiction.
-/

/-- The object we're trying to show doesn't exist: A nontrivial quadratic Dirichlet character
whose L-function vanishes at `s =1`. -/
structure BadChar (N : â„•) [NeZero N] where
  Ï‡ : DirichletCharacter â„‚ N
  Ï‡_ne : Ï‡ â‰  1
  Ï‡_sq : Ï‡ ^ 2 = 1
  hÏ‡ : Ï‡.LFunction 1 = 0

variable {N : â„•} [NeZero N]

open Complex DirichletCharacter

namespace BadChar

/-- The product of the Riemann zeta function with the L-function of `B.Ï‡`.
We will show that `B.F (-2) = 0` but also that `B.F (-2)` must be positive,
giving the desired contradiction. -/
noncomputable
def F (B : BadChar N) : â„‚ â†’ â„‚ :=
  Function.update (fun s : â„‚ â†¦ riemannZeta s * LFunction B.Ï‡ s) 1 (deriv (LFunction B.Ï‡) 1)

lemma F_differentiableAt_of_ne (B : BadChar N) {s : â„‚} (hs : s â‰  1) :
    DifferentiableAt â„‚ B.F s := by
  apply DifferentiableAt.congr_of_eventuallyEq
  Â· exact (differentiableAt_riemannZeta hs).mul <| differentiableAt_LFunction B.Ï‡ s (.inl hs)
  Â· filter_upwards [eventually_ne_nhds hs] with t ht using Function.update_noteq ht ..

open ArithmeticFunction in
/-- `B.F` agrees with the L-series of `zetaMul Ï‡` on `1 < s.re`. -/
lemma F_eq_LSeries (B : BadChar N) {s : â„‚} (hs : 1 < s.re) :
    B.F s = LSeries B.Ï‡.zetaMul s := by
  rw [F, zetaMul, â† coe_mul, LSeries_convolution']
  Â· have hs' : s â‰  1 := fun h â†¦ by simp only [h, one_re, lt_self_iff_false] at hs
    simp only [ne_eq, hs', not_false_eq_true, Function.update_noteq, B.Ï‡.LFunction_eq_LSeries hs]
    congr 1
    Â· rw [â† LSeries_zeta_eq_riemannZeta hs]
      rfl
    Â· exact LSeries_congr s B.Ï‡.apply_eq_toArithmeticFunction_apply
  -- summability side goals from `LSeries_convolution'`
  Â· exact LSeriesSummable_zeta_iff.mpr hs
  Â· exact (LSeriesSummable_congr _ fun h â†¦ (B.Ï‡.apply_eq_toArithmeticFunction_apply h).symm).mpr <|
      ZMod.LSeriesSummable_of_one_lt_re B.Ï‡ hs

/-- If `Ï‡` is a bad character, then `F` is an entire function. -/
lemma F_differentiable (B : BadChar N) : Differentiable â„‚ B.F := by
  intro s
  rcases ne_or_eq s 1 with hs | rfl
  Â· exact B.F_differentiableAt_of_ne hs
  -- now need to deal with `s = 1`
  refine (analyticAt_of_differentiable_on_punctured_nhds_of_continuousAt ?_ ?_).differentiableAt
  Â· filter_upwards [self_mem_nhdsWithin] with t ht
    exact B.F_differentiableAt_of_ne ht
  -- now reduced to showing *continuity* at s = 1
  let G := Function.update (fun s â†¦ (s - 1) * riemannZeta s) 1 1
  let H := Function.update (fun s â†¦ (B.Ï‡.LFunction s - B.Ï‡.LFunction 1) / (s - 1)) 1
    (deriv B.Ï‡.LFunction 1)
  have : B.F = G * H := by
    ext1 t
    rcases eq_or_ne t 1 with rfl | ht
    Â· simp only [F, G, H, Pi.mul_apply, one_mul, Function.update_same]
    Â· simp only [F, G, H, Function.update_noteq ht, mul_comm _ (riemannZeta _), B.hÏ‡, sub_zero,
      Pi.mul_apply, mul_assoc, mul_div_cancelâ‚€ _ (sub_ne_zero.mpr ht)]
  rw [this]
  apply ContinuousAt.mul
  Â· simpa only [G, continuousAt_update_same] using riemannZeta_residue_one
  Â· exact (B.Ï‡.differentiableAt_LFunction 1 (.inr B.Ï‡_ne)).hasDerivAt.continuousAt_div

/-- The trivial zero at `s = -2` of the zeta function gives that `F (-2) = 0`.
This is used later to obtain a contradction. -/
lemma F_neg_two (B : BadChar N) : B.F (-2 : â„) = 0 := by
  have := riemannZeta_neg_two_mul_nat_add_one 0
  rw [Nat.cast_zero, zero_add, mul_one] at this
  rw [F, ofReal_neg, ofReal_ofNat, Function.update_noteq (mod_cast (by omega : (-2 : â„¤) â‰  1)),
    this, zero_mul]

end BadChar

/-!
### The main result
-/

open scoped ComplexOrder in
/-- If `Ï‡` is a nontrivial quadratic Dirichlet character, then `L(Ï‡, 1) â‰  0`. -/
theorem LFunction_at_one_ne_zero_of_quadratic {N : â„•} [NeZero N] {Ï‡ : DirichletCharacter â„‚ N}
    (hÏ‡ : Ï‡ ^ 2 = 1) (Ï‡_ne : Ï‡ â‰  1) :
    Ï‡.LFunction 1 â‰  0 := by
  intro hL
  -- construct a "bad character" and put together a contradiction.
  let B : BadChar N := {Ï‡ := Ï‡, Ï‡_sq := hÏ‡, hÏ‡ := hL, Ï‡_ne := Ï‡_ne}
  refine (B.F_neg_two â–¸ (?_ : 0 < B.F (-2 : â„))).false
  refine ArithmeticFunction.LSeries_positive_of_differentiable_of_eqOn (zetaMul_nonneg hÏ‡)
    (Ï‡.isMultiplicative_zetaMul.map_one â–¸ zero_lt_one) B.F_differentiable ?_
    (fun _ â†¦ B.F_eq_LSeries) _
  exact LSeries.abscissaOfAbsConv_le_of_forall_lt_LSeriesSummable
    fun _ a â†¦ Ï‡.LSeriesSummable_zetaMul a

end DirichletCharacter
