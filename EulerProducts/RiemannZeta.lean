import EulerProducts.EulerProduct
import Mathlib.NumberTheory.ZetaFunction
import Mathlib.NumberTheory.DirichletCharacter.Basic
import Mathlib.FieldTheory.Finite.Basic

/-!
## The Euler Product for the Riemann Zeta Function
-/

local macro_rules | `($x ^ $y)   => `(HPow.hPow $x $y)

lemma DirichletCharacter.norm_le_one {n : â„•} (Ï‡ : DirichletCharacter â„‚ n) (m : ZMod n) :
    â€–Ï‡ mâ€– â‰¤ 1 := by
  by_cases h : IsUnit m
  Â· match n with
    | 0 =>
        rcases Int.isUnit_iff.mp h with rfl | rfl
        Â· rw [map_one, norm_one]
        Â· refine (sq_le_one_iff <| norm_nonneg _).mp ?_
          rw [â† norm_pow, â† map_pow, neg_one_sq, map_one, norm_one]
    | n + 1 =>
      Â· have H := congr_arg ((â†‘) : _ â†’ ZMod n.succ) <| ZMod.pow_totient h.unit
        push_cast at H
        rw [h.unit_spec.symm]
        refine (pow_le_one_iff_of_nonneg (norm_nonneg _) (Nat.totient_pos n.succ_pos).ne').mp ?_
        rw [â† norm_pow, â† map_pow, H, map_one, norm_one]
  Â· rw [Ï‡.map_nonunit h, norm_zero]
    exact zero_le_one

open Complex

-- should perhaps go to `Mathlib.Analysis.SpecialFunctions.Pow.Real`
lemma Nat.complex_norm_cpow_eq_rpow_re (n : â„•) {s : â„‚} (h : s.re â‰  0) :
    â€–(n : â„‚) ^ sâ€– = (n : â„) ^ s.re := by
  rcases n.eq_zero_or_pos with rfl | H
  Â· have hs : s â‰  0 := fun H â†¦ H â–¸ h <| rfl
    simp [zero_cpow hs, Real.zero_rpow h]
  Â· exact abs_cpow_eq_rpow_re_of_pos (Nat.cast_pos.mpr H) s

/-- The `n`th summand in the Dirichlet series giving the Riemann Î¶ function at `s`. -/
noncomputable
def riemannZetaSummand (s : â„‚) (n : â„•) : â„‚ := (n : â„‚) ^ (-s)

lemma riemannZetaSummand.mul (s : â„‚) (m n : â„•) :
    riemannZetaSummand s (m * n) = riemannZetaSummand s m * riemannZetaSummand s n := by
  simpa [riemannZetaSummand] using mul_cpow_ofReal_nonneg m.cast_nonneg n.cast_nonneg _

variable {s : â„‚}

/-- When `s â‰  0`, the map `n â†¦ n^(-s)` is completely multiplicative and vanishes at zero. -/
noncomputable
def riemannZetaSummandHom (hs : s â‰  0) : â„• â†’*â‚€ â„‚ where
  toFun := riemannZetaSummand s
  map_zero' := by simpa [riemannZetaSummand]
  map_one' := by simp [riemannZetaSummand]
  map_mul' := riemannZetaSummand.mul s

/-- When `Ï‡` is a Dirichlet character and `s â‰  0`, the map `n â†¦ n^(-s)` is completely
multiplicative and vanishes at zero. -/
noncomputable
def dirichletSummandHom {n : â„•} (Ï‡ : DirichletCharacter â„‚ n) (hs : s â‰  0) : â„• â†’*â‚€ â„‚ where
  toFun n := Ï‡ n * riemannZetaSummand s n
  map_zero' := by simp [riemannZetaSummand, hs]
  map_one' := by simp [riemannZetaSummand]
  map_mul' m n := by
    simpa only [Nat.cast_mul, IsUnit.mul_iff, not_and, map_mul, riemannZetaSummand.mul]
      using mul_mul_mul_comm ..

lemma Complex.ne_zero_of_one_lt_re (hs : 1 < s.re) : s â‰  0 :=
  fun h â†¦ ((zero_re â–¸ h â–¸ hs).trans zero_lt_one).false

lemma Complex.re_neg_ne_zero_of_one_lt_re (hs : 1 < s.re) : (-s).re â‰  0 := by
  intro h
  rw [neg_re, neg_eq_zero] at h
  exact lt_irrefl (1 : â„) <| (h â–¸ hs).trans zero_lt_one

/-- When `s.re > 1`, the map `n â†¦ n^(-s)` is norm-summable. -/
lemma summable_riemannZetaSummand (hs : 1 < s.re) :
  Summable (fun n â†¦ â€–riemannZetaSummandHom (Complex.ne_zero_of_one_lt_re hs) nâ€–) := by
  simp only [riemannZetaSummandHom, riemannZetaSummand, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk]
  convert Real.summable_nat_rpow_inv.mpr hs with n
  rw [n.complex_norm_cpow_eq_rpow_re <| Complex.re_neg_ne_zero_of_one_lt_re hs, neg_re,
    Real.rpow_neg <| Nat.cast_nonneg _]

/-- When `s.re > 1`, the map `n â†¦ Ï‡(n) * n^(-s)` is norm-summable. -/
lemma summable_dirichletSummand {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) (hs : 1 < s.re) :
  Summable (fun n â†¦ â€–dirichletSummandHom Ï‡ (Complex.ne_zero_of_one_lt_re hs) nâ€–) := by
  simp only [dirichletSummandHom, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk, norm_mul]
  refine summable_of_nonneg_of_le (fun n â†¦ ?_) (fun n â†¦ ?_) <| summable_riemannZetaSummand hs
  Â· positivity
  Â· exact mul_le_of_le_one_left (norm_nonneg _) <| Ï‡.norm_le_one n

open Filter Nat Topology BigOperators EulerProduct in
/-- The Euler product for the Riemann Î¶ function, valid for `s.re > 1`. -/
theorem riemannZeta_eulerProduct (hs : 1 < s.re) :
    Tendsto (fun n : â„• â†¦ âˆ p in primesBelow n, (1 - (p : â„‚) ^ (-s))â»Â¹) atTop (ð“ (riemannZeta s))
    := by
  have hsum := summable_riemannZetaSummand hs
  convert euler_product_multiplicative hsum
  rw [zeta_eq_tsum_one_div_nat_add_one_cpow hs, tsum_eq_zero_add <| summable_of_summable_norm hsum,
    map_zero, zero_add]
  simp [riemannZetaSummandHom, riemannZetaSummand, cpow_neg]

open Filter Nat Topology BigOperators EulerProduct in
/-- The Euler product for Dirichlet L-series, valid for `s.re > 1`. -/
theorem dirichletLSeries_eulerProduct {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) (hs : 1 < s.re) :
    Tendsto (fun n : â„• â†¦ âˆ p in primesBelow n, (1 - Ï‡ p * (p : â„‚) ^ (-s))â»Â¹) atTop
      (ð“ (âˆ‘' n : â„•, dirichletSummandHom Ï‡ (Complex.ne_zero_of_one_lt_re hs) n)) := by
  convert euler_product_multiplicative <| summable_dirichletSummand Ï‡ hs
