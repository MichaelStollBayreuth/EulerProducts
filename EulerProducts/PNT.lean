import Mathlib.Analysis.SpecialFunctions.Complex.LogBounds
import Mathlib.NumberTheory.DirichletCharacter.Orthogonality
import Mathlib.NumberTheory.LSeries.Linearity
import Mathlib.NumberTheory.LSeries.QuadraticNonvanishing
import Mathlib.RingTheory.RootsOfUnity.AlgebraicallyClosed

/-!
# Logarithms of Euler Products

Here we consider `f : ‚Ñï ‚Üí*‚ÇÄ ‚ÑÇ` and the goal is to prove that
`exp (‚àë p in Primes, log (1 - f p)‚Åª¬π) = ‚àë n : ‚Ñï, f n`
under suitable conditions on `f`.
-/
-- Mathlib.NumberTheory.EulerProduct.ExpLog

open Complex

attribute [fun_prop] DifferentiableAt.clog

open Topology in
/-- If `f : Œ± ‚Üí ‚ÑÇ` is summable, then so is `n ‚Ü¶ -log (1 - f n)`. -/
lemma Summable.neg_clog_one_sub {Œ±  : Type*} {f : Œ± ‚Üí ‚ÑÇ} (hsum : Summable f) :
    Summable (fun n ‚Ü¶ -log (1 - f n)) := by
  have hg : DifferentiableAt ‚ÑÇ (fun z ‚Ü¶ -log (1 - z)) 0 := by
    have : 1 - 0 ‚àà slitPlane := (sub_zero (1 : ‚ÑÇ)).symm ‚ñ∏ one_mem_slitPlane
    fun_prop (disch := assumption)
  have : (fun z ‚Ü¶ -log (1 - z)) =O[ùìù 0] id := by
    simpa only [sub_zero, log_one, neg_zero] using hg.isBigO_sub
  exact this.comp_summable hsum

namespace EulerProduct

/-- A variant of the Euler Product formula in terms of the exponential of a sum of logarithms. -/
theorem exp_tsum_primes_log_eq_tsum {f : ‚Ñï ‚Üí*‚ÇÄ ‚ÑÇ} (hsum : Summable (‚Äñf ¬∑‚Äñ)) :
    exp (‚àë' p : Nat.Primes, -log (1 - f p)) = ‚àë' n : ‚Ñï, f n := by
  have hs {p : ‚Ñï} (hp : 1 < p) : ‚Äñf p‚Äñ < 1 := hsum.of_norm.norm_lt_one (f := f.toMonoidHom) hp
  have hp (p : Nat.Primes) : 1 - f p ‚â† 0 :=
    fun h ‚Ü¶ norm_one (Œ± := ‚ÑÇ) ‚ñ∏ (sub_eq_zero.mp h) ‚ñ∏ hs p.prop.one_lt |>.false
  have H := hsum.of_norm.neg_clog_one_sub.subtype {p | p.Prime} |>.hasSum.cexp.tprod_eq
  simp only [Set.coe_setOf, Set.mem_setOf_eq, Function.comp_apply] at H
  conv at H => enter [1, 1, p]; rw [exp_neg, exp_log (hp p)]
  exact H.symm.trans <| eulerProduct_completely_multiplicative_tprod hsum

/-- A variant of the Euler Product formula in terms of the exponential of a sum of logarithms. -/
theorem exp_tsum_primes_log_eq_tsum' {f : ‚Ñï ‚Üí ‚ÑÇ} (h‚ÇÄ : f 0 = 0) (h‚ÇÅ : f 1 = 1)
    (hf : ‚àÄ m n, f (m * n) = f m * f n) (hsum : Summable (‚Äñf ¬∑‚Äñ)) :
    exp (‚àë' p : Nat.Primes, -log (1 - f p)) = ‚àë' n : ‚Ñï, f n :=
  exp_tsum_primes_log_eq_tsum (f := {toFun := f, map_zero' := h‚ÇÄ, map_one' := h‚ÇÅ, map_mul' := hf})
    hsum

end EulerProduct

section BigO

open Topology Asymptotics Filter

-- [Mathlib.Analysis.InnerProductSpace.Basic, Mathlib.Analysis.Complex.Asymptotics,
--  Mathlib.Analysis.Normed.Algebra.Exponential]
lemma Complex.isBigO_comp_ofReal {f g : ‚ÑÇ ‚Üí ‚ÑÇ} {x : ‚Ñù} (h : f =O[ùìù (x : ‚ÑÇ)] g) :
    (fun y : ‚Ñù ‚Ü¶ f y) =O[ùìù x] (fun y : ‚Ñù ‚Ü¶ g y) :=
  h.comp_tendsto <| Continuous.tendsto continuous_ofReal x

-- [Mathlib.Analysis.Complex.RealDeriv]
lemma Complex.isBigO_comp_ofReal_nhds_ne {f g : ‚ÑÇ ‚Üí ‚ÑÇ} {x : ‚Ñù} (h : f =O[ùìù[‚â†] (x : ‚ÑÇ)] g) :
    (fun y : ‚Ñù ‚Ü¶ f y) =O[ùìù[‚â†] x] (fun y : ‚Ñù ‚Ü¶ g y) :=
  h.comp_tendsto <| ((hasDerivAt_id (x : ‚ÑÇ)).comp_ofReal).tendsto_punctured_nhds one_ne_zero

-- [Mathlib.Analysis.Asymptotics.Theta, Mathlib.Analysis.SpecificLimits.Normed,
--  Mathlib.Analysis.Asymptotics.SpecificAsymptotics,
--  Mathlib.Analysis.NormedSpace.OperatorNorm.Asymptotics]
lemma ContinuousAt.isBigO {ùïú ùïú' : Type*} [NormedRing ùïú] [NormedRing ùïú'] [NormOneClass ùïú']
    {f : ùïú ‚Üí ùïú'} {z : ùïú} (hf : ContinuousAt f z) :
    (fun w ‚Ü¶ f (w + z)) =O[ùìù 0] (fun _ ‚Ü¶ (1 : ùïú')) := by
  rw [isBigO_iff']
  replace hf : ContinuousAt (fun w ‚Ü¶ f (w + z)) 0 := by
    convert (Homeomorph.comp_continuousAt_iff' (Homeomorph.addLeft (-z)) _ z).mp ?_
    ¬∑ simp only [Homeomorph.coe_addLeft, neg_add_cancel]
    ¬∑ simp only [Homeomorph.coe_addLeft, Function.comp_def, neg_add_cancel_comm, hf]
  simp_rw [Metric.continuousAt_iff', dist_eq_norm_sub, zero_add] at hf
  specialize hf 1 zero_lt_one
  refine ‚ü®‚Äñf z‚Äñ + 1, by positivity, ?_‚ü©
  refine Eventually.mp hf <| Eventually.of_forall fun w hw ‚Ü¶ le_of_lt ?_
  calc ‚Äñf (w + z)‚Äñ
    _ ‚â§ ‚Äñf z‚Äñ + ‚Äñf (w + z) - f z‚Äñ := norm_le_insert' ..
    _ < ‚Äñf z‚Äñ + 1 := add_lt_add_left hw _
    _ = _ := by simp only [norm_one, mul_one]

-- [Mathlib.Analysis.Calculus.Deriv.Shift]
lemma DifferentiableAt.isBigO_of_eq_zero {ùïú ùïú' : Type*} [NontriviallyNormedField ùïú]
    [NormedAddCommGroup ùïú'] [NormedSpace ùïú ùïú']
    {f : ùïú ‚Üí ùïú'} {z : ùïú} (hf : DifferentiableAt ùïú f z) (hz : f z = 0) :
    (fun w ‚Ü¶ f (w + z)) =O[ùìù 0] id := by
  rw [‚Üê zero_add z] at hf
  simpa only [zero_add, hz, sub_zero]
    using (hf.hasDerivAt.comp_add_const 0 z).differentiableAt.isBigO_sub

end BigO

section LSeries

-- This should go to `Mathlib.NumberTheory.LSeries.Linearity`

open LSeries

variable {Œπ : Type*} [DecidableEq Œπ] (f : Œπ ‚Üí ‚Ñï ‚Üí ‚ÑÇ) (S : Finset Œπ) (s : ‚ÑÇ)

lemma LSeries.term_sum_apply (n : ‚Ñï) :
    term (‚àë i ‚àà S, f i) s n  = ‚àë i ‚àà S, term (f i) s n := by
  induction S using Finset.induction_on with
  | empty =>
    simp only [Finset.sum_empty, term, Pi.zero_apply, zero_div, ite_self]
  | insert hi IH  =>
    simp only [Finset.sum_insert hi, term_add_apply, IH]

lemma LSeries.term_sum : term (‚àë i ‚àà S, f i) s  = ‚àë i ‚àà S, term (f i) s :=
  funext fun _ ‚Ü¶ by rw [Finset.sum_apply]; exact term_sum_apply f S s _

variable {f S s}

lemma LSeriesHasSum.sum {a : Œπ ‚Üí ‚ÑÇ} (hf : ‚àÄ i ‚àà S, LSeriesHasSum (f i) s (a i)) :
    LSeriesHasSum (‚àë i ‚àà S, f i) s (‚àë i ‚àà S, a i) := by
  simpa only [LSeriesHasSum, term_sum, Finset.sum_fn S fun i ‚Ü¶ term (f i) s] using hasSum_sum hf

lemma LSeriesSummable.sum (hf : ‚àÄ i ‚àà S, LSeriesSummable (f i) s) :
    LSeriesSummable (‚àë i ‚àà S, f i) s := by
  simpa only [LSeriesSummable, ‚Üê term_sum_apply] using summable_sum hf

@[simp]
lemma LSeries_sum (hf : ‚àÄ i ‚àà S, LSeriesSummable (f i) s) :
    LSeries (‚àë i ‚àà S, f i) s = ‚àë i ‚àà S, LSeries (f i) s := by
  simpa only [LSeries, term_sum, Finset.sum_apply] using tsum_sum hf

variable [Fintype Œπ]

/-- The version of `LSeriesHasSum.sum` for `Fintype.sum`. -/
lemma LSeriesHasSum.sum' {a : Œπ ‚Üí ‚ÑÇ} (hf : ‚àÄ i, LSeriesHasSum (f i) s (a i)) :
    LSeriesHasSum (‚àë i : Œπ, f i) s (‚àë i : Œπ, a i) :=
  sum fun i _ ‚Ü¶ hf i

/-- The version of `LSeriesSummable.sum` for `Fintype.sum`. -/
lemma LSeriesSummable.sum' (hf : ‚àÄ i, LSeriesSummable (f i) s) :
    LSeriesSummable (‚àë i : Œπ, f i) s :=
  sum fun i _ ‚Ü¶ hf i

/-- The version of `LSeries_sum` for `Fintype.sum`. -/
@[simp]
lemma LSeries_sum' (hf : ‚àÄ i, LSeriesSummable (f i) s) :
    LSeries (‚àë i : Œπ, f i) s = ‚àë i : Œπ, LSeries (f i) s :=
  LSeries_sum fun i _ ‚Ü¶ hf i

end LSeries

/-!
### Statement of a version of the Wiener-Ikehara Theorem
-/

open scoped LSeries.notation

open Filter Topology Nat ArithmeticFunction in
/-- A version of the *Wiener-Ikehara Tauberian Theorem*: If `f` is a nonnegative arithmetic
function whose L-series has a simple pole at `s = 1` with residue `A` and otherwise extends
continuously to the closed half-plane `re s ‚â• 1`, then `‚àë n < N, f n` is asymptotic to `A*N`. -/
def WienerIkeharaTheorem : Prop :=
  ‚àÄ {f : ‚Ñï ‚Üí ‚Ñù} {A : ‚Ñù} {F : ‚ÑÇ ‚Üí ‚ÑÇ}, (‚àÄ n, 0 ‚â§ f n) ‚Üí
    Set.EqOn F (fun s ‚Ü¶ L ‚Üóf s - A / (s - 1)) {s | 1 < s.re} ‚Üí
    ContinuousOn F {s | 1 ‚â§ s.re} ‚Üí
    Tendsto (fun N : ‚Ñï ‚Ü¶ ((Finset.range N).sum f) / N) atTop (ùìù A)

/-!
### The L-function of a Dirichlet character does not vanish on Re(s) = 1
-/

open Complex

section EulerProduct

-- This gets moved to `NumberTheory.LSeries.EulerProduct`

open LSeries Nat EulerProduct

/-- A variant of the Euler product for Dirichlet L-series. -/
theorem DirichletCharacter.LSeries_eulerProduct' {N : ‚Ñï} (œá : DirichletCharacter ‚ÑÇ N) {s : ‚ÑÇ}
    (hs : 1 < s.re) :
    exp (‚àë' p : Nat.Primes, -log (1 - œá p * p ^ (-s))) = L ‚Üóœá s := by
  rw [LSeries]
  let f := dirichletSummandHom œá <| ne_zero_of_one_lt_re hs
  have h n : term ‚Üóœá s n = f n := by
    rcases eq_or_ne n 0 with rfl | hn
    ¬∑ simp only [term_zero, map_zero]
    ¬∑ simp only [ne_eq, hn, not_false_eq_true, term_of_ne_zero, div_eq_mul_inv,
        dirichletSummandHom, cpow_neg, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk, f]
  simpa only [h] using exp_tsum_primes_log_eq_tsum (f := f) <| summable_dirichletSummand œá hs

open DirichletCharacter

/-- A variant of the Euler product for the L-series of `Œ∂`. -/
theorem ArithmeticFunction.LSeries_zeta_eulerProduct' {s : ‚ÑÇ} (hs : 1 < s.re) :
    exp (‚àë' p : Nat.Primes, -Complex.log (1 - p ^ (-s))) = L 1 s := by
  convert modOne_eq_one (R := ‚ÑÇ) ‚ñ∏ LSeries_eulerProduct' (1 : DirichletCharacter ‚ÑÇ 1) hs using 7
  rw [MulChar.one_apply <| isUnit_of_subsingleton _, one_mul]

/-- A variant of the Euler product for the Riemann zeta function. -/
theorem riemannZeta_eulerProduct'  {s : ‚ÑÇ} (hs : 1 < s.re) :
    exp (‚àë' p : Nat.Primes, -Complex.log (1 - p ^ (-s))) = riemannZeta s :=
  LSeries_one_eq_riemannZeta hs ‚ñ∏ ArithmeticFunction.LSeries_zeta_eulerProduct' hs

end EulerProduct


lemma summable_neg_log_one_sub_character_mul_prime_cpow {N : ‚Ñï} (œá : DirichletCharacter ‚ÑÇ N)
    {s : ‚ÑÇ} (hs : 1 < s.re) :
    Summable fun p : Nat.Primes ‚Ü¶ -log (1 - œá p * (p : ‚ÑÇ) ^ (-s)) := by
  have (p : Nat.Primes) : ‚Äñœá p * (p : ‚ÑÇ) ^ (-s)‚Äñ ‚â§ (p : ‚Ñù) ^ (-s).re := by
    rw [norm_mul, norm_natCast_cpow_of_re_ne_zero _ <| re_neg_ne_zero_of_one_lt_re hs]
    calc ‚Äñœá p‚Äñ * (p : ‚Ñù) ^ (-s).re
      _ ‚â§ 1 * (p : ‚Ñù) ^ (-s.re) := by gcongr; exact DirichletCharacter.norm_le_one œá _
      _ = _ := one_mul _
  refine (Nat.Primes.summable_rpow.mpr ?_).of_nonneg_of_le (fun _ ‚Ü¶ norm_nonneg _) this
    |>.of_norm.neg_clog_one_sub
  simp only [neg_re, neg_lt_neg_iff, hs]

/-- A technical lemma showing that a certain linear combination of real parts of logarithms
is nonnegative. This is used to show non-vanishing of the Riemann zeta function and of
Dirichlet L-series on the line `re s = 1`. -/
lemma re_log_comb_nonneg {a : ‚Ñù} (ha‚ÇÄ : 0 ‚â§ a) (ha‚ÇÅ : a < 1) {z : ‚ÑÇ} (hz : ‚Äñz‚Äñ = 1) :
      0 ‚â§ 3 * (-log (1 - a)).re + 4 * (-log (1 - a * z)).re + (-log (1 - a * z ^ 2)).re := by
  have hac‚ÇÄ : ‚Äñ(a : ‚ÑÇ)‚Äñ < 1 := by
    simp only [norm_eq_abs, abs_ofReal, _root_.abs_of_nonneg ha‚ÇÄ, ha‚ÇÅ]
  have hac‚ÇÅ : ‚Äña * z‚Äñ < 1 := by rwa [norm_mul, hz, mul_one]
  have hac‚ÇÇ : ‚Äña * z ^ 2‚Äñ < 1 := by rwa [norm_mul, norm_pow, hz, one_pow, mul_one]
  have H‚ÇÄ := (hasSum_re <| hasSum_taylorSeries_neg_log hac‚ÇÄ).mul_left 3
  have H‚ÇÅ := (hasSum_re <| hasSum_taylorSeries_neg_log hac‚ÇÅ).mul_left 4
  have H‚ÇÇ := hasSum_re <| hasSum_taylorSeries_neg_log hac‚ÇÇ
  rw [‚Üê ((H‚ÇÄ.add H‚ÇÅ).add H‚ÇÇ).tsum_eq]; clear H‚ÇÄ H‚ÇÅ H‚ÇÇ
  refine tsum_nonneg fun n ‚Ü¶ ?_
  simp only [‚Üê ofReal_pow, div_natCast_re, ofReal_re, mul_pow, mul_re, ofReal_im, zero_mul,
    sub_zero]
  rcases n.eq_zero_or_pos with rfl | hn
  ¬∑ simp only [pow_zero, CharP.cast_eq_zero, div_zero, mul_zero, one_re, mul_one, add_zero,
     le_refl]
  ¬∑ simp only [‚Üê mul_div_assoc, ‚Üê add_div]
    refine div_nonneg ?_ n.cast_nonneg
    rw [‚Üê pow_mul, pow_mul', sq, mul_re, ‚Üê sq, ‚Üê sq, ‚Üê sq_abs_sub_sq_re, ‚Üê norm_eq_abs, norm_pow, hz]
    calc
     0 ‚â§ 2 * a ^ n * ((z ^ n).re + 1) ^ 2 := by positivity
      _ = _  := by ring

namespace DirichletCharacter

lemma deriv_LFunction_eq_deriv_LSeries {n : ‚Ñï} [NeZero n] (œá : DirichletCharacter ‚ÑÇ n) {s : ‚ÑÇ}
    (hs : 1 < s.re) :
    deriv (LFunction œá) s = deriv (LSeries ‚Üóœá) s :=  by
  refine Filter.EventuallyEq.deriv_eq ?_
  have h : {z | 1 < z.re} ‚àà nhds s :=
    (isOpen_lt continuous_const continuous_re).mem_nhds hs
  filter_upwards [h] with z hz
  exact LFunction_eq_LSeries œá hz

/-- The logarithm of an Euler factor of the product `L(œá^0, x)^3 * L(œá, x+I*y)^4 * L(œá^2, x+2*I*y)`
has nonnegative real part when `s = x + I*y` has real part `x > 1`. -/
lemma re_log_comb_nonneg {N : ‚Ñï} (œá : DirichletCharacter ‚ÑÇ N) {n : ‚Ñï} (hn : 2 ‚â§ n) {x y : ‚Ñù}
    (hx : 1 < x) :
    0 ‚â§ 3 * (-log (1 - (1 : DirichletCharacter ‚ÑÇ N) n * n ^ (-x : ‚ÑÇ))).re +
          4 * (-log (1 - œá n * n ^ (-(x + I * y)))).re +
          (-log (1 - (œá n ^ 2) * n ^ (-(x + 2 * I * y)))).re := by
  by_cases hn' : IsUnit (n : ZMod N)
  ¬∑ have ha‚ÇÄ : 0 ‚â§ (n : ‚Ñù) ^ (-x) := Real.rpow_nonneg n.cast_nonneg _
    have ha‚ÇÅ : (n : ‚Ñù) ^ (-x) < 1 := by
      simpa only [Real.rpow_lt_one_iff n.cast_nonneg, Nat.cast_eq_zero, Nat.one_lt_cast,
        Left.neg_neg_iff, Nat.cast_lt_one, Left.neg_pos_iff]
        using .inr <| .inl ‚ü®hn, zero_lt_one.trans hx‚ü©
    have hz : ‚Äñœá n * (n : ‚ÑÇ) ^ (-(I * y))‚Äñ = 1 := by
      rw [norm_mul, ‚Üê hn'.unit_spec, DirichletCharacter.unit_norm_eq_one œá hn'.unit, one_mul,
        norm_eq_abs, abs_cpow_of_imp fun h ‚Ü¶ False.elim <| by linarith [Nat.cast_eq_zero.mp h, hn]]
      simp only [abs_natCast, neg_re, mul_re, I_re, ofReal_re, zero_mul, I_im, ofReal_im, mul_zero,
        sub_self, neg_zero, Real.rpow_zero, natCast_arg, neg_im, mul_im, one_mul, zero_add, mul_neg,
        Real.exp_zero, ne_eq, one_ne_zero, not_false_eq_true, div_self]
    rw [MulChar.one_apply hn', one_mul]
    convert _root_.re_log_comb_nonneg ha‚ÇÄ ha‚ÇÅ hz using 6
    ¬∑ congr 2
      exact_mod_cast (ofReal_cpow n.cast_nonneg (-x)).symm
    ¬∑ congr 2
      rw [neg_add, cpow_add _ _ <| by norm_cast; linarith, ‚Üê ofReal_neg,
        ofReal_cpow n.cast_nonneg (-x), ofReal_natCast, mul_left_comm]
    ¬∑ rw [neg_add, cpow_add _ _ <| by norm_cast; linarith, ‚Üê ofReal_neg,
        ofReal_cpow n.cast_nonneg (-x), ofReal_natCast,
        show -(2 * I * y) = (2 : ‚Ñï) * -(I * y) by ring, cpow_nat_mul, mul_pow, mul_left_comm]
  ¬∑ simp only [MulChar.map_nonunit _ hn', zero_mul, sub_zero, log_one, neg_zero, zero_re, mul_zero,
      neg_add_rev, add_zero, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow, le_refl]

variable {N : ‚Ñï} [NeZero N] {œá : DirichletCharacter ‚ÑÇ N}

open Complex BigOperators Filter Topology Homeomorph Asymptotics

private lemma one_lt_re_one_add {x : ‚Ñù} (hx : 0 < x) (y : ‚Ñù) :
    1 < (1 + x : ‚ÑÇ).re ‚àß 1 < (1 + x + I * y).re ‚àß 1 < (1 + x + 2 * I * y).re := by
  simp only [add_re, one_re, ofReal_re, lt_add_iff_pos_right, hx, mul_re, I_re, zero_mul, I_im,
    ofReal_im, mul_zero, sub_self, add_zero, re_ofNat, im_ofNat, mul_one, mul_im, and_self]

open scoped LSeries.notation

open Nat ArithmeticFunction

/-- For positive `x` and nonzero `y` we have that
$|L(\chi^0, x)^3 \cdot L(\chi, x+iy)^4 \cdot L(\chi^2, x+2iy)| \ge 1$. -/
lemma norm_LSeries_product_ge_one {N : ‚Ñï} (œá : DirichletCharacter ‚ÑÇ N) {x : ‚Ñù} (hx : 0 < x)
    (y : ‚Ñù) :
    ‚ÄñL ‚Üó(1 : DirichletCharacter ‚ÑÇ N) (1 + x) ^ 3 * L ‚Üóœá (1 + x + I * y) ^ 4 *
      L ‚Üó(œá ^ 2 :) (1 + x + 2 * I * y)‚Äñ ‚â• 1 := by
  let œá‚ÇÄ := (1 : DirichletCharacter ‚ÑÇ N)
  have ‚ü®h‚ÇÄ, h‚ÇÅ, h‚ÇÇ‚ü© := one_lt_re_one_add hx y
  have hx‚ÇÅ : 1 + (x : ‚ÑÇ) = (1 + x : ‚ÑÇ).re := by -- kills three goals of the `convert` below
    simp only [add_re, one_re, ofReal_re, ofReal_add, ofReal_one]
  have hsum‚ÇÄ :=
    (hasSum_re (summable_neg_log_one_sub_character_mul_prime_cpow œá‚ÇÄ h‚ÇÄ).hasSum).summable
    |>.mul_left 3
  have hsum‚ÇÅ :=
    (hasSum_re (summable_neg_log_one_sub_character_mul_prime_cpow œá h‚ÇÅ).hasSum).summable.mul_left 4
  have hsum‚ÇÇ :=
    (hasSum_re (summable_neg_log_one_sub_character_mul_prime_cpow (œá ^ 2) h‚ÇÇ).hasSum).summable
  rw [‚Üê LSeries_eulerProduct' _ h‚ÇÄ, ‚Üê LSeries_eulerProduct' œá h‚ÇÅ,
    ‚Üê LSeries_eulerProduct' (œá ^ 2) h‚ÇÇ, ‚Üê exp_nat_mul, ‚Üê exp_nat_mul, ‚Üê exp_add, ‚Üê exp_add,
    norm_eq_abs, abs_exp]
  simp only [cast_ofNat, add_re, mul_re, re_ofNat, im_ofNat, zero_mul, sub_zero,
    Real.one_le_exp_iff]
  rw [re_tsum <| summable_neg_log_one_sub_character_mul_prime_cpow _ h‚ÇÄ,
    re_tsum <| summable_neg_log_one_sub_character_mul_prime_cpow _ h‚ÇÅ,
    re_tsum <| summable_neg_log_one_sub_character_mul_prime_cpow _ h‚ÇÇ, ‚Üê tsum_mul_left,
    ‚Üê tsum_mul_left, ‚Üê tsum_add hsum‚ÇÄ hsum‚ÇÅ, ‚Üê tsum_add (hsum‚ÇÄ.add hsum‚ÇÅ) hsum‚ÇÇ]
  simp only [œá.pow_apply' two_ne_zero]
  -- `exact` times out here
  convert tsum_nonneg fun p : Nat.Primes ‚Ü¶ œá.re_log_comb_nonneg p.prop.two_le h‚ÇÄ

variable {N : ‚Ñï} [NeZero N] {œá : DirichletCharacter ‚ÑÇ N}

/-- A variant of `DirichletCharacter.norm_LSeries_product_ge_one` in terms of the L-functions. -/
lemma norm_LFunction_product_ge_one {x : ‚Ñù} (hx : 0 < x) (y : ‚Ñù) :
    ‚ÄñLFunctionTrivChar N (1 + x) ^ 3 * LFunction œá (1 + x + I * y) ^ 4 *
      LFunction (œá ^ 2) (1 + x + 2 * I * y)‚Äñ ‚â• 1 := by
  have ‚ü®h‚ÇÄ, h‚ÇÅ, h‚ÇÇ‚ü© := one_lt_re_one_add hx y
  rw [LFunctionTrivChar, DirichletCharacter.LFunction_eq_LSeries 1 h‚ÇÄ,
    œá.LFunction_eq_LSeries h‚ÇÅ, (œá ^ 2).LFunction_eq_LSeries h‚ÇÇ]
  exact norm_LSeries_product_ge_one œá hx y

lemma LFunctionTrivChar_isBigO_near_one_horizontal :
    (fun x : ‚Ñù ‚Ü¶ LFunctionTrivChar N (1 + x)) =O[ùìù[>] 0] (fun x ‚Ü¶ (1 : ‚ÑÇ) / x) := by
  have : (fun w : ‚ÑÇ ‚Ü¶ LFunctionTrivChar N (1 + w)) =O[ùìù[‚â†] 0] (1 / ¬∑) := by
    have H : Tendsto (fun w ‚Ü¶ w * LFunctionTrivChar N (1 + w)) (ùìù[‚â†] 0)
               (ùìù <| ‚àè p ‚àà N.primeFactors, (1 - (p : ‚ÑÇ)‚Åª¬π)) := by
      convert (LFunctionTrivChar_residue_one (N := N)).comp (f := fun w ‚Ü¶ 1 + w) ?_ using 1
      ¬∑ ext1 w
        simp only [Function.comp_apply, add_sub_cancel_left]
      ¬∑ refine tendsto_iff_comap.mpr <| map_le_iff_le_comap.mp <| Eq.le ?_
        convert map_punctured_nhds_eq (Homeomorph.addLeft (1 : ‚ÑÇ)) 0 using 2 <;>
          simp only [coe_addLeft, add_zero]
    exact ((isBigO_mul_iff_isBigO_div eventually_mem_nhdsWithin).mp <|
      Tendsto.isBigO_one ‚ÑÇ H).trans <| isBigO_refl ..
  exact (isBigO_comp_ofReal_nhds_ne this).mono <| nhds_right'_le_nhds_ne 0

omit [NeZero N] in
private lemma one_add_I_mul_ne_one_or {y : ‚Ñù} (hy : y ‚â† 0 ‚à® œá ‚â† 1) :
    1 + I * y ‚â† 1 ‚à® œá ‚â† 1:= by
  simpa only [ne_eq, add_right_eq_self, _root_.mul_eq_zero, I_ne_zero, ofReal_eq_zero, false_or]
    using hy

lemma LFunction_isBigO_horizontal {y : ‚Ñù} (hy : y ‚â† 0 ‚à® œá ‚â† 1) :
    (fun x : ‚Ñù ‚Ü¶ œá.LFunction (1 + x + I * y)) =O[ùìù[>] 0] (fun _ ‚Ü¶ (1 : ‚ÑÇ)) := by
  refine IsBigO.mono ?_ nhdsWithin_le_nhds
  convert isBigO_comp_ofReal
    (œá.differentiableAt_LFunction _ <| one_add_I_mul_ne_one_or hy).continuousAt.isBigO using 3
  abel

lemma LFunction_isBigO_horizontal_of_eq_zero {y : ‚Ñù} (hy : y ‚â† 0 ‚à® œá ‚â† 1)
    (h : LFunction œá (1 + I * y) = 0) :
    (fun x : ‚Ñù ‚Ü¶ LFunction œá (1 + x + I * y)) =O[ùìù[>] 0] fun x : ‚Ñù ‚Ü¶ (x : ‚ÑÇ) := by
  conv => enter [2, x]; rw [add_comm 1, add_assoc]
  refine (isBigO_comp_ofReal <| DifferentiableAt.isBigO_of_eq_zero ?_ h).mono nhdsWithin_le_nhds
  exact œá.differentiableAt_LFunction (1 + I * ‚Üëy) <| one_add_I_mul_ne_one_or hy

/-- The L-function of a Dirichlet character `œá` does not vanish at `1 + I*t` if `t ‚â† 0`
or `œá^2 ‚â† 1`. -/
lemma LFunction_ne_zero_of_ne_one_or_not_quadratic {t : ‚Ñù} (h : œá ^ 2 ‚â† 1 ‚à® t ‚â† 0) :
    LFunction œá (1 + I * t) ‚â† 0 := by
  intro Hz
  have H‚ÇÄ : (fun _ : ‚Ñù ‚Ü¶ (1 : ‚Ñù)) =O[ùìù[>] 0]
      (fun x ‚Ü¶ LFunctionTrivChar N (1 + x) ^ 3 * LFunction œá (1 + x + I * t) ^ 4 *
                   LFunction (œá ^ 2) (1 + x + 2 * I * t)) :=
    IsBigO.of_bound' <| eventually_nhdsWithin_of_forall
      fun _ hx ‚Ü¶ (norm_one (Œ± := ‚Ñù)).symm ‚ñ∏ (norm_LFunction_product_ge_one hx t).le
  have hz‚ÇÅ : t ‚â† 0 ‚à® œá ‚â† 1 := by
    rcases h with h | h
    ¬∑ exact .inr <| by rintro rfl; simp only [one_pow, ne_eq, not_true_eq_false] at h
    ¬∑ exact .inl h
  have hz‚ÇÇ : 2 * t ‚â† 0 ‚à® œá ^ 2 ‚â† 1 := by
    rcases h with h | h
    ¬∑ exact .inr h
    ¬∑ exact .inl <| mul_ne_zero two_ne_zero h
  have H := ((LFunctionTrivChar_isBigO_near_one_horizontal (N := N)).pow 3).mul
    ((LFunction_isBigO_horizontal_of_eq_zero hz‚ÇÅ Hz).pow 4) |>.mul <|
    LFunction_isBigO_horizontal hz‚ÇÇ
  have help (x : ‚Ñù) : ((1 / x) ^ 3 * x ^ 4 * 1 : ‚ÑÇ) = x := by
    rcases eq_or_ne x 0 with rfl | h
    ¬∑ rw [ofReal_zero, zero_pow (by norm_num), mul_zero, mul_one]
    ¬∑ field_simp [h]
      exact _root_.pow_succ' ..
  simp only [ofReal_mul, ofReal_ofNat, mul_left_comm I, ‚Üê mul_assoc, help] at H
  replace H := (H‚ÇÄ.trans H).norm_right
  simp only [norm_eq_abs, abs_ofReal] at H
  refine isLittleO_irrefl ?_ <| H.of_abs_right.trans_isLittleO <|
    isLittleO_id_one.mono nhdsWithin_le_nhds
  simp only [ne_eq, one_ne_zero, not_false_eq_true, frequently_true_iff_neBot]
  exact mem_closure_iff_nhdsWithin_neBot.mp <| closure_Ioi (0 : ‚Ñù) ‚ñ∏ Set.left_mem_Ici

/-- If `œá` is a Dirichlet character, then `L(œá, 1 + I*t)` does not vanish for `t ‚àà ‚Ñù`
except when `œá` is trivial and `t = 0` (then `L(œá, s)` has a simple pole at `s = 1`). -/
theorem Lfunction_ne_zero_of_re_eq_one (œá : DirichletCharacter ‚ÑÇ N) (t : ‚Ñù) (hœát : œá ‚â† 1 ‚à® t ‚â† 0) :
    LFunction œá (1 + I * t) ‚â† 0 := by
  by_cases h : œá ^ 2 = 1 ‚àß t = 0
  ¬∑ simp only [ne_eq, h.2, not_true_eq_false, or_false] at hœát
    simpa only [h.2, ofReal_zero, mul_zero, add_zero]
      using LFunction_at_one_ne_zero_of_quadratic h.1 hœát
  ¬∑ exact LFunction_ne_zero_of_ne_one_or_not_quadratic <| not_and_or.mp h

/-- If `œá` is a Dirichlet character, then `L(œá, s)` does not vanish for `s.re ‚â• 1`
except when `œá` is trivial and `s = 1` (then `L(œá, s)` has a simple pole at `s = 1`). -/
theorem Lfunction_ne_zero_of_one_le_re (œá : DirichletCharacter ‚ÑÇ N) ‚¶És : ‚ÑÇ‚¶Ñ (hœás : œá ‚â† 1 ‚à® s ‚â† 1)
    (hs : 1 ‚â§ s.re) :
    LFunction œá s ‚â† 0 := by
  rcases hs.eq_or_lt with hs | hs
  ¬∑ have hseq : s = 1 + I * s.im := by
      rw [mul_comm I, ‚Üê re_add_im s, ‚Üê hs]
      push_cast
      simp only [add_im, one_im, mul_im, ofReal_re, I_im, mul_one, ofReal_im, I_re, mul_zero,
        add_zero, zero_add]
    rw [hseq]
    apply Lfunction_ne_zero_of_re_eq_one
    rcases hœás with h | h
    ¬∑ exact .inl h
    ¬∑ refine .inr ?_
      rw [‚Üê re_add_im s, ‚Üê hs, ofReal_one] at h
      simpa only [ne_eq, add_right_eq_self, _root_.mul_eq_zero, ofReal_eq_zero, I_ne_zero,
        or_false] using h
  ¬∑ exact LFunction_eq_LSeries œá hs ‚ñ∏ LSeries_ne_zero_of_one_lt_re œá hs

end DirichletCharacter

open DirichletCharacter in
/-- The Riemann Zeta Function does not vanish on the closed half-plane `re z ‚â• 1`. -/
lemma riemannZeta_ne_zero_of_one_le_re ‚¶Éz : ‚ÑÇ‚¶Ñ (hz : z ‚â† 1) (hz' : 1 ‚â§ z.re) :
    riemannZeta z ‚â† 0 := by
  rw [‚Üê LFunction_modOne_eq (œá := 1)]
  exact Lfunction_ne_zero_of_one_le_re (1 : DirichletCharacter ‚ÑÇ 1) (.inr hz) hz'

/-!
### The logarithmic derivative of the L-function of a trivial character has a simple pole at s = 1 with residue -1

We show that `s ‚Ü¶ L'(œá) s / L(œá) s + 1 / (s - 1)` (or rather, its negative, which is the function
we need for the Wiener-Ikehara Theorem) is continuous outside the zeros of `L(œá)` when `œá`
is a trivial Dirichlet character.
-/

namespace DirichletCharacter

section trivial

variable (n : ‚Ñï) [NeZero n]

/-- The function obtained by "multiplying away" the pole of `L(œá)` for a trivial Dirichlet
character `œá`. Its (negative) logarithmic derivative is used in the Wiener-Ikehara Theorem
to prove the Prime Number Theorem version of Dirichlet's Theorem on primes in arithmetic
progressions. -/
noncomputable def LFunctionTrivChar‚ÇÅ : ‚ÑÇ ‚Üí ‚ÑÇ :=
  Function.update (fun z ‚Ü¶ LFunctionTrivChar n z * (z - 1)) 1
    (‚àè p ‚àà n.primeFactors, (1 - (p : ‚ÑÇ)‚Åª¬π))

lemma LFunctionTrivChar‚ÇÅ_apply_of_ne_one {z : ‚ÑÇ} (hz : z ‚â† 1) :
    LFunctionTrivChar‚ÇÅ n z = LFunctionTrivChar n z * (z - 1) := by
  simp [LFunctionTrivChar‚ÇÅ, hz]

lemma LFunction_triv_char‚ÇÅ_differentiable : Differentiable ‚ÑÇ (LFunctionTrivChar‚ÇÅ n) := by
  rw [‚Üê differentiableOn_univ,
    ‚Üê differentiableOn_compl_singleton_and_continuousAt_iff (c := 1) Filter.univ_mem,
    LFunctionTrivChar‚ÇÅ]
  refine ‚ü®DifferentiableOn.congr (f := fun z ‚Ü¶ LFunctionTrivChar n z * (z - 1))
    (fun z hz ‚Ü¶ DifferentiableAt.differentiableWithinAt ?_) fun _ hz ‚Ü¶ ?_,
    continuousWithinAt_compl_self.mp ?_‚ü©
  ¬∑ simp only [Set.mem_diff, Set.mem_univ, Set.mem_singleton_iff, true_and] at hz
    exact (differentiableAt_LFunction _ z (.inl hz)).mul <| (differentiableAt_id').sub <|
      differentiableAt_const _
  ¬∑ simp only [Set.mem_diff, Set.mem_univ, Set.mem_singleton_iff, true_and] at hz
    simp only [ne_eq, hz, not_false_eq_true, Function.update_noteq]
  ¬∑ conv in (_ * _) => rw [mul_comm]
    simp only [continuousWithinAt_compl_self, continuousAt_update_same]
    exact LFunctionTrivChar_residue_one

lemma deriv_LFunctionTrivChar‚ÇÅ_apply_of_ne_one  {z : ‚ÑÇ} (hz : z ‚â† 1) :
    deriv (LFunctionTrivChar‚ÇÅ n) z =
      deriv (LFunctionTrivChar n) z * (z - 1) + LFunctionTrivChar n z := by
  have H : deriv (LFunctionTrivChar‚ÇÅ n) z =
      deriv (fun w ‚Ü¶ LFunctionTrivChar n w * (w - 1)) z := by
    refine Filter.EventuallyEq.deriv_eq <| Filter.eventuallyEq_iff_exists_mem.mpr ?_
    refine ‚ü®{w | w ‚â† 1}, IsOpen.mem_nhds isOpen_ne hz, fun w hw ‚Ü¶ ?_‚ü©
    simp only [LFunctionTrivChar‚ÇÅ, ne_eq, Set.mem_setOf.mp hw, not_false_eq_true,
      Function.update_noteq]
  rw [H, deriv_mul (differentiableAt_LFunction _ z (.inl hz)) <| differentiableAt_id'.sub <|
    differentiableAt_const 1, deriv_sub_const, deriv_id'', mul_one]

lemma neg_logDeriv_LFunctionTrivChar‚ÇÅ_eq {z : ‚ÑÇ} (hz‚ÇÅ : z ‚â† 1)
    (hz‚ÇÇ : LFunctionTrivChar n z ‚â† 0) :
    -deriv (LFunctionTrivChar‚ÇÅ n) z / LFunctionTrivChar‚ÇÅ n z =
      -deriv (LFunctionTrivChar n) z / LFunctionTrivChar n z - 1 / (z - 1) := by
  rw [deriv_LFunctionTrivChar‚ÇÅ_apply_of_ne_one n hz‚ÇÅ, LFunctionTrivChar‚ÇÅ_apply_of_ne_one n hz‚ÇÅ]
  field_simp [sub_ne_zero.mpr hz‚ÇÅ]
  ring

lemma continuousOn_neg_logDeriv_LFunctionTrivChar‚ÇÅ :
    ContinuousOn (fun z ‚Ü¶ -deriv (LFunctionTrivChar‚ÇÅ n) z / LFunctionTrivChar‚ÇÅ n z)
      {z | z = 1 ‚à® LFunctionTrivChar n z ‚â† 0} := by
  simp_rw [neg_div]
  refine (((LFunction_triv_char‚ÇÅ_differentiable n).contDiff.continuous_deriv le_rfl).continuousOn.div
    (LFunction_triv_char‚ÇÅ_differentiable n).continuous.continuousOn fun w hw ‚Ü¶ ?_).neg
  rcases eq_or_ne w 1 with rfl | hw'
  ¬∑ simp only [LFunctionTrivChar‚ÇÅ, Function.update_same]
    refine Finset.prod_ne_zero_iff.mpr fun p hp ‚Ü¶ ?_
    rw [sub_ne_zero, ne_eq, one_eq_inv]
    exact_mod_cast (Nat.prime_of_mem_primeFactors hp).ne_one
  ¬∑ simp only [ne_eq, Set.mem_setOf_eq, hw', false_or] at hw
    simp only [LFunctionTrivChar‚ÇÅ, ne_eq, hw', not_false_eq_true, Function.update_noteq, _root_.mul_eq_zero, hw,
      false_or]
    exact sub_ne_zero.mpr hw'

lemma eq_one_or_LFunctionTrivChar_ne_zero_of_one_le_re :
    {s : ‚ÑÇ | 1 ‚â§ s.re} ‚äÜ {s | s = 1 ‚à® LFunction (1 : DirichletCharacter ‚ÑÇ n) s ‚â† 0} := by
  intro s hs
  simp only [Set.mem_setOf_eq, ne_eq] at hs ‚ä¢
  have := Lfunction_ne_zero_of_one_le_re (1 : DirichletCharacter ‚ÑÇ n) (s := s)
  tauto

end trivial

section nontrivial

variable {n : ‚Ñï} [NeZero n] {œá : DirichletCharacter ‚ÑÇ n}

/-- The negative logarithmic derivative of a Dirichlet L-function is continuous away from
the zeros of the L-function. -/
lemma continuousOn_neg_logDeriv_LFunction_nontriv_char (hœá : œá ‚â† 1) :
    ContinuousOn (fun z ‚Ü¶ -deriv (LFunction œá) z / LFunction œá z)
      {z | LFunction œá z ‚â† 0} := by
  simp_rw [neg_div]
  have h‚ÇÅ := differentiable_LFunction hœá
  exact ((h‚ÇÅ.contDiff.continuous_deriv le_rfl).continuousOn.div
    h‚ÇÅ.continuous.continuousOn fun w hw ‚Ü¶ hw).neg

lemma LFunction_nontriv_char_ne_zero_of_one_le_re (hœá : œá ‚â† 1) :
    {s : ‚ÑÇ | 1 ‚â§ s.re} ‚äÜ {s | LFunction œá s ‚â† 0} := by
  intro s hs
  simp only [Set.mem_setOf_eq, ne_eq] at hs ‚ä¢
  have := Lfunction_ne_zero_of_one_le_re œá (s := s)
  tauto

end nontrivial

end DirichletCharacter

section zeta

/-!
### The logarithmic derivative of Œ∂ has a simple pole at s = 1 with residue -1

We show that `s ‚Ü¶ Œ∂' s / Œ∂ s + 1 / (s - 1)` (or rather, its negative, which is the function
we need for the Wiener-Ikehara Theorem) is continuous outside the zeros of `Œ∂`.
-/

/-- We use `Œ∂` to denote the Riemann zeta function. -/
local notation (name := rzeta) "Œ∂" => riemannZeta

/-- The function obtained by "multiplying away" the pole of `Œ∂`. Its (negative) logarithmic
derivative is the function used in the Wiener-Ikehara Theorem to prove the Prime Number
Theorem. -/
noncomputable def Œ∂‚ÇÅ : ‚ÑÇ ‚Üí ‚ÑÇ := Function.update (fun z ‚Ü¶ Œ∂ z * (z - 1)) 1 1

open DirichletCharacter

lemma riemannZeta_eq_LFunctionTrivChar_one : Œ∂ = LFunctionTrivChar 1 :=
  LFunction_modOne_eq.symm

lemma Œ∂‚ÇÅ_eq_LFunctionTrivChar‚ÇÅ_one : Œ∂‚ÇÅ = LFunctionTrivChar‚ÇÅ 1 := by
  ext1
  simp only [Œ∂‚ÇÅ, LFunctionTrivChar‚ÇÅ, LFunction_modOne_eq, Nat.primeFactors_one,
    Finset.prod_empty]

lemma neg_logDeriv_Œ∂‚ÇÅ_eq {z : ‚ÑÇ} (hz‚ÇÅ : z ‚â† 1) (hz‚ÇÇ : Œ∂ z ‚â† 0) :
    -deriv Œ∂‚ÇÅ z / Œ∂‚ÇÅ z = -deriv Œ∂ z / Œ∂ z - 1 / (z - 1) := by
  simp only [Œ∂‚ÇÅ_eq_LFunctionTrivChar‚ÇÅ_one, riemannZeta_eq_LFunctionTrivChar_one] at hz‚ÇÇ ‚ä¢
  exact neg_logDeriv_LFunctionTrivChar‚ÇÅ_eq 1 hz‚ÇÅ hz‚ÇÇ

lemma continuousOn_neg_logDeriv_Œ∂‚ÇÅ :
    ContinuousOn (fun z ‚Ü¶ -deriv Œ∂‚ÇÅ z / Œ∂‚ÇÅ z) {z | z = 1 ‚à® Œ∂ z ‚â† 0} := by
  simp only [Œ∂‚ÇÅ_eq_LFunctionTrivChar‚ÇÅ_one, riemannZeta_eq_LFunctionTrivChar_one]
  exact continuousOn_neg_logDeriv_LFunctionTrivChar‚ÇÅ 1

end zeta

/-!
### Proof of Lemma 9

We prove Lemma 9 of
[Section 2 in the PNT+ Project](https://alexkontorovich.github.io/PrimeNumberTheoremAnd/web/sect0002.html).
-/

section arith_prog

open scoped ArithmeticFunction.vonMangoldt
open DirichletCharacter

variable {q : ‚Ñï} [NeZero q] {a : ZMod q}

/-- Lemma 9 of Section 2 of PNT+: The L-series of the von Mangoldt function restricted to the
prime residue class `a` mod `q` as a linear combination of logarithmic derivatives of
L functions of the Dirichlet characters mod `q`. -/
lemma WeakPNT_character (ha : IsUnit a) {s : ‚ÑÇ} (hs : 1 < s.re) :
     LSeries ({n : ‚Ñï | (n : ZMod q) = a}.indicator ‚ÜóŒõ) s =
      -(q.totient : ‚ÑÇ)‚Åª¬π * ‚àë œá : DirichletCharacter ‚ÑÇ q, œá a‚Åª¬π *
        (deriv (LFunction œá) s / LFunction œá s) := by
  simp only [deriv_LFunction_eq_deriv_LSeries _ hs, LFunction_eq_LSeries _ hs, neg_mul, ‚Üê mul_neg,
    ‚Üê Finset.sum_neg_distrib, ‚Üê neg_div, ‚Üê LSeries_twist_vonMangoldt_eq _ hs]
  rw [eq_inv_mul_iff_mul_eq‚ÇÄ <| mod_cast (Nat.totient_pos.mpr q.pos_of_neZero).ne']
  simp only [‚Üê LSeries_smul]
  classical
  rw [‚Üê LSeries_sum' <| fun œá ‚Ü¶ (LSeriesSummable_twist_vonMangoldt œá hs).smul _]
  refine LSeries_congr s fun {n} _ ‚Ü¶ ?_
  simp only [Pi.smul_apply, smul_eq_mul, Finset.sum_apply, Pi.mul_apply, Set.indicator_apply]
  conv_lhs => rw [‚Üê one_mul (Œõ n : ‚ÑÇ), ‚Üê zero_mul (Œõ n : ‚ÑÇ), ‚Üê ite_mul]
  simp only [‚Üê mul_assoc, ‚Üê Finset.sum_mul, mul_ite, mul_one, mul_zero, Set.mem_setOf_eq]
  congrm (?_ * (Œõ n : ‚ÑÇ))
  simpa only [Nat.cast_ite, Nat.cast_zero, eq_comm (a := a)]
    using (sum_char_inv_mul_char_eq ‚ÑÇ ha n).symm

variable (q a) in
open Classical in
/-- The function `F` used in the Wiener-Ikehara Theorem to prove Dirichlet's Theorem. -/
noncomputable
def weakDirichlet_auxFun (s : ‚ÑÇ) : ‚ÑÇ :=
  (q.totient : ‚ÑÇ)‚Åª¬π * (-deriv (LFunctionTrivChar‚ÇÅ q) s / LFunctionTrivChar‚ÇÅ q s -
    ‚àë œá ‚àà ({1}·∂ú : Finset (DirichletCharacter ‚ÑÇ q)), œá a‚Åª¬π * deriv (LFunction œá) s / LFunction œá s)

lemma weakDirichlet_auxFun_prop (ha : IsUnit a) :
    Set.EqOn (weakDirichlet_auxFun q a)
      (fun s ‚Ü¶ LSeries ({n : ‚Ñï | (n : ZMod q) = a}.indicator ‚ÜóŒõ) s - (q.totient : ‚ÑÇ)‚Åª¬π / (s - 1))
      {s | 1 < s.re} := by
  classical
  intro s hs
  simp only [Set.mem_setOf_eq] at hs
  simp only [WeakPNT_character ha hs]
  rw [weakDirichlet_auxFun, neg_div, ‚Üê neg_add', mul_neg, ‚Üê neg_mul,
    div_eq_mul_one_div (q.totient : ‚ÑÇ)‚Åª¬π, sub_eq_add_neg, ‚Üê neg_mul, ‚Üê mul_add]
  congrm (_ * ?_)
  -- this should be easier, but `IsUnit.inv ha` does not work here
  have ha' : IsUnit a‚Åª¬π := isUnit_of_dvd_one ‚ü®a, (ZMod.inv_mul_of_unit a ha).symm‚ü©
  rw [Fintype.sum_eq_add_sum_compl 1, MulChar.one_apply ha', one_mul, add_right_comm]
  simp only [mul_div_assoc]
  congrm (?_ + _)
  have hs‚ÇÅ : s ‚â† 1 := by
    rintro rfl
    simp only [one_re, lt_self_iff_false] at hs
  rw [deriv_LFunctionTrivChar‚ÇÅ_apply_of_ne_one _ hs‚ÇÅ, LFunctionTrivChar‚ÇÅ_apply_of_ne_one _ hs‚ÇÅ]
  simp only [LFunctionTrivChar]
  rw [add_div, mul_div_mul_right _ _ (sub_ne_zero_of_ne hs‚ÇÅ)]
  conv_lhs => enter [2, 1]; rw [‚Üê mul_one (LFunction ..)]
  rw [mul_div_mul_left _ _ <| Lfunction_ne_zero_of_one_le_re 1 (.inr hs‚ÇÅ) hs.le]

/-- (A version of) Proposition 2 of Section 2 of PNT+: the L-series of the von Mangoldt function
restricted to the prime residue class `a` mod `q` is continuous on `s.re ‚â• 1` except
for a single pole at `s = 1` with residue `(q.totient)‚Åª¬π`.-/
lemma continuousOn_weakDirichlet_auxFun :
    ContinuousOn (weakDirichlet_auxFun q a) {s | 1 ‚â§ s.re} := by
  rw [show weakDirichlet_auxFun q a = fun s ‚Ü¶ _ from rfl]
  simp only [weakDirichlet_auxFun, sub_eq_add_neg]
  refine continuousOn_const.mul <| ContinuousOn.add ?_ ?_
  ¬∑ exact ContinuousOn.mono (continuousOn_neg_logDeriv_LFunctionTrivChar‚ÇÅ q)
      (eq_one_or_LFunctionTrivChar_ne_zero_of_one_le_re q)
  ¬∑ simp only [‚Üê Finset.sum_neg_distrib, mul_div_assoc, ‚Üê mul_neg, ‚Üê neg_div]
    refine continuousOn_finset_sum _ fun œá hœá ‚Ü¶ continuousOn_const.mul ?_
    replace hœá : œá ‚â† 1 := by simpa only [ne_eq, Finset.mem_compl, Finset.mem_singleton] using hœá
    exact ContinuousOn.mono (continuousOn_neg_logDeriv_LFunction_nontriv_char hœá)
      (LFunction_nontriv_char_ne_zero_of_one_le_re hœá)

end arith_prog

/-!
### Derivation of the Prime Number Theorem and Dirichlet's Theorem from the Wiener-Ikehara Theorem
-/

open Filter ArithmeticFunction Topology

/- /-- The *Wiener-Ikehara Theorem* implies the *Prime Number Theorem* in the form that
`œà x ‚àº x`, where `œà x = ‚àë n < x, Œõ n` and `Œõ` is the von Mangoldt function. -/
theorem PNT_vonMangoldt' (WIT : WienerIkeharaTheorem) :
    Tendsto (fun N : ‚Ñï ‚Ü¶ ((Finset.range N).sum Œõ) / N) atTop (ùìù 1) := by
  have hnv := riemannZeta_ne_zero_of_one_le_re
  refine WIT (F := fun z ‚Ü¶ -deriv Œ∂‚ÇÅ z / Œ∂‚ÇÅ z) (fun _ ‚Ü¶ vonMangoldt_nonneg) (fun s hs ‚Ü¶ ?_) ?_
  ¬∑ have hs‚ÇÅ : s ‚â† 1 := by
      rintro rfl
      simp at hs
    simp only [ne_eq, hs‚ÇÅ, not_false_eq_true, LSeries_vonMangoldt_eq_deriv_riemannZeta_div hs,
      ofReal_one]
    exact neg_logDeriv_Œ∂‚ÇÅ_eq hs‚ÇÅ <| hnv hs‚ÇÅ (Set.mem_setOf.mp hs).le
  ¬∑ refine continuousOn_neg_logDeriv_Œ∂‚ÇÅ.mono fun s _ ‚Ü¶ ?_
    specialize @hnv s
    simp at *
    tauto -/

/--  The *Wiener-Ikehara Theorem* implies *Dirichlet's Theorem* in the form that
`œà x ‚àº q.totient‚Åª¬π * x`, where `œà x = ‚àë n < x ‚àß n ‚â° a mod q, Œõ n`
and `Œõ` is the von Mangoldt function.

This is Theorem 2 in Section 2 of PNT+ (but using the `WIT` stub defined here). -/
theorem Dirichlet_vonMangoldt (WIT : WienerIkeharaTheorem) {q : ‚Ñï} [NeZero q] {a : ZMod q}
    (ha : IsUnit a) :
    Tendsto (fun N : ‚Ñï ‚Ü¶ (((Finset.range N).filter (fun n : ‚Ñï ‚Ü¶ (n : ZMod q) = a)).sum Œõ) / N)
      atTop (ùìù <| (q.totient : ‚Ñù)‚Åª¬π) := by
  classical
  have H N : ((Finset.range N).filter (fun n : ‚Ñï ‚Ü¶ (n : ZMod q) = a)).sum Œõ =
      (Finset.range N).sum ({n : ‚Ñï | (n : ZMod q) = a}.indicator Œõ) :=
    (Finset.sum_indicator_eq_sum_filter _ _ (fun _ ‚Ü¶ {n : ‚Ñï | n = a}) _).symm
  simp only [H]
  refine WIT (F := weakDirichlet_auxFun q a) (fun n ‚Ü¶ ?_) ?_ ?_
  ¬∑ exact Set.indicator_apply_nonneg fun _ ‚Ü¶ vonMangoldt_nonneg
  ¬∑ convert weakDirichlet_auxFun_prop ha with s n
    ¬∑ by_cases hn : n = a
      ¬∑ simp only [Set.mem_setOf_eq, hn, Set.indicator_of_mem]
      ¬∑ simp only [Set.mem_setOf_eq, hn, not_false_eq_true, Set.indicator_of_not_mem, ofReal_zero]
    ¬∑ rw [ofReal_inv, ofReal_natCast]
  ¬∑ exact continuousOn_weakDirichlet_auxFun

/-- The *Wiener-Ikehara Theorem* implies the *Prime Number Theorem* in the form that
`œà x ‚àº x`, where `œà x = ‚àë n < x, Œõ n` and `Œõ` is the von Mangoldt function. -/
theorem PNT_vonMangoldt (WIT : WienerIkeharaTheorem) :
    Tendsto (fun N : ‚Ñï ‚Ü¶ ((Finset.range N).sum Œõ) / N) atTop (ùìù 1) := by
  convert Dirichlet_vonMangoldt WIT (q := 1) (a := 1) isUnit_one with n
  ¬∑ exact (Finset.filter_true_of_mem fun _ _ ‚Ü¶ Subsingleton.eq_one _).symm
  ¬∑ simp only [Nat.totient_one, Nat.cast_one, inv_one]
