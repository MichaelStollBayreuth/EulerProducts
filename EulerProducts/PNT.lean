import Mathlib.NumberTheory.DirichletCharacter.Orthogonality
import Mathlib.NumberTheory.LSeries.Linearity
import Mathlib.NumberTheory.LSeries.Nonvanishing
import Mathlib.NumberTheory.LSeries.PrimesInAP
import Mathlib.RingTheory.RootsOfUnity.AlgebraicallyClosed


open Complex

/-!
### The L-series of Œõ restricted to a residue class
-/

open LSeries DirichletCharacter

namespace ArithmeticFunction.vonMangoldt

variable {q : ‚Ñï} (a : ZMod q) [NeZero q]

open Classical in
/-- The auxiliary function used, e.g., with the Wiener-Ikehara Theorem to prove
Dirichlet's Theorem. On `re s > 1`, it agrees with the L-series of the von Mangoldt
function restricted to the residue class `a : ZMod q` minus the principal part
`(q.totient)‚Åª¬π/(s-1)` of the pole at `s = 1`; see `DirichletsThm.auxFun_prop`. -/
noncomputable
abbrev auxFun (s : ‚ÑÇ) : ‚ÑÇ :=
  (q.totient : ‚ÑÇ)‚Åª¬π * (-deriv (LFunctionTrivChar‚ÇÅ q) s / LFunctionTrivChar‚ÇÅ q s -
    ‚àë œá ‚àà ({1}·∂ú : Finset (DirichletCharacter ‚ÑÇ q)), œá a‚Åª¬π * deriv (LFunction œá) s / LFunction œá s)

/-- The auxiliary function is continuous away from the zeros of the L-functions of the Dirichlet
characters mod `q` (including at `s = 1`). -/
lemma continuousOn_auxFun' :
    ContinuousOn (auxFun a) {s | s = 1 ‚à® ‚àÄ œá : DirichletCharacter ‚ÑÇ q, LFunction œá s ‚â† 0} := by
  rw [show auxFun a = fun s ‚Ü¶ _ from rfl]
  simp only [auxFun, sub_eq_add_neg]
  refine continuousOn_const.mul <| ContinuousOn.add ?_ ?_
  ¬∑ refine (continuousOn_neg_logDeriv_LFunctionTrivChar‚ÇÅ q).mono fun s hs ‚Ü¶ ?_
    have := LFunction_ne_zero_of_one_le_re (1 : DirichletCharacter ‚ÑÇ q) (s := s)
    simp only [ne_eq, Set.mem_setOf_eq] at hs
    tauto
  ¬∑ simp only [‚Üê Finset.sum_neg_distrib, mul_div_assoc, ‚Üê mul_neg, ‚Üê neg_div]
    refine continuousOn_finset_sum _ fun œá hœá ‚Ü¶ continuousOn_const.mul ?_
    replace hœá : œá ‚â† 1 := by simpa only [ne_eq, Finset.mem_compl, Finset.mem_singleton] using hœá
    refine (continuousOn_neg_logDeriv_LFunction_of_nontriv hœá).mono fun s hs ‚Ü¶ ?_
    simp only [ne_eq, Set.mem_setOf_eq] at hs
    rcases hs with rfl | hs
    ¬∑ simp only [ne_eq, Set.mem_setOf_eq, one_re, le_refl,
        LFunction_ne_zero_of_one_le_re œá (.inl hœá), not_false_eq_true]
    ¬∑ exact hs œá

/-- The L-series of the von Mangoldt function restricted to the prime residue class `a` mod `q`
is continuous on `re s ‚â• 1` except for a single pole at `s = 1` with residue `(q.totient)‚Åª¬π`.
The statement as given here in terms auf `DirichletsThm.auxFun` is equivalent. -/
lemma continuousOn_auxFun : ContinuousOn (auxFun a) {s | 1 ‚â§ s.re} := by
  refine (continuousOn_auxFun' a).mono fun s hs ‚Ü¶ ?_
  rcases eq_or_ne s 1 with rfl | hs‚ÇÅ
  ¬∑ simp only [ne_eq, Set.mem_setOf_eq, true_or]
  ¬∑ simp only [ne_eq, Set.mem_setOf_eq, hs‚ÇÅ, false_or]
    exact fun œá ‚Ü¶ LFunction_ne_zero_of_one_le_re œá (.inr hs‚ÇÅ) <| Set.mem_setOf.mp hs

variable {a}

open scoped LSeries.notation

/-- The auxiliary function agrees on `re s > 1` with the L-series of the von Mangoldt function
restricted to the residue class `a : ZMod q` minus the principal part `(q.totient)‚Åª¬π/(s-1)`
of its pole at `s = 1`. -/
lemma eqOn_auxFun (ha : IsUnit a) :
    Set.EqOn (auxFun a)
      (fun s ‚Ü¶ L ‚Üó(residueClass a) s - (q.totient : ‚ÑÇ)‚Åª¬π / (s - 1))
      {s | 1 < s.re} := by
  intro s hs
  replace hs := Set.mem_setOf.mp hs
  simp only [LSeries_residueClass_eq ha hs, auxFun]
  rw [neg_div, ‚Üê neg_add', mul_neg, ‚Üê neg_mul, div_eq_mul_one_div (q.totient : ‚ÑÇ)‚Åª¬π,
    sub_eq_add_neg, ‚Üê neg_mul, ‚Üê mul_add]
  congrm (_ * ?_)
  -- this should be easier, but `IsUnit.inv ha` does not work here
  have ha' : IsUnit a‚Åª¬π := isUnit_of_dvd_one ‚ü®a, (ZMod.inv_mul_of_unit a ha).symm‚ü©
  classical -- for `Fintype.sum_eq_add_sum_compl`
  rw [Fintype.sum_eq_add_sum_compl 1, MulChar.one_apply ha', one_mul, add_right_comm]
  simp only [mul_div_assoc]
  congrm (?_ + _)
  have hs‚ÇÅ : s ‚â† 1 := fun h ‚Ü¶ ((h ‚ñ∏ hs).trans_eq one_re).false
  rw [deriv_LFunctionTrivChar‚ÇÅ_apply_of_ne_one _ hs‚ÇÅ, LFunctionTrivChar‚ÇÅ,
    Function.update_noteq hs‚ÇÅ, LFunctionTrivChar, add_div,
    mul_div_mul_left _ _ (sub_ne_zero_of_ne hs‚ÇÅ)]
  conv_lhs => enter [2, 1]; rw [‚Üê mul_one (LFunction ..)]
  rw [mul_comm _ 1, mul_div_mul_right _ _ <| LFunction_ne_zero_of_one_le_re 1 (.inr hs‚ÇÅ) hs.le]

/-- The auxiliary function takes real values for real arguments `x > 1`. -/
lemma auxFun_real (ha : IsUnit a) {x : ‚Ñù} (hx : 1 < x) : auxFun a x = (auxFun a x).re := by
  rw [eqOn_auxFun ha hx]
  simp only [sub_re, ofReal_sub]
  congr 1
  ¬∑ rw [LSeries, re_tsum <| LSeriesSummable_of_abscissaOfAbsConv_lt_re <|
      (abscissaOfAbsConv_residueClass_le_one a).trans_lt <| by norm_cast]
    push_cast
    refine tsum_congr fun n ‚Ü¶ ?_
    rcases eq_or_ne n 0 with rfl | hn
    ¬∑ simp only [term_zero, zero_re, ofReal_zero]
    ¬∑ simp only [term_of_ne_zero hn, ‚Üê ofReal_natCast n, ‚Üê ofReal_cpow n.cast_nonneg, ‚Üê ofReal_div,
        ofReal_re]
  ¬∑ rw [show (q.totient : ‚ÑÇ) = (q.totient : ‚Ñù) from rfl, ‚Üê ofReal_one, ‚Üê ofReal_sub, ‚Üê ofReal_inv,
      ‚Üê ofReal_div, ofReal_re]

variable {q : ‚Ñï} [NeZero q] {a : ZMod q}

/-- As `x` approaches `1` from the right along the real axis, the L-series of
`ArithmeticFunction.vonMangoldt.residueClass` is bounded below by `(q.totient)‚Åª¬π/(x-1) - C`. -/
lemma LSeries_residueClass_lower_bound (ha : IsUnit a) :
    ‚àÉ C : ‚Ñù, ‚àÄ {x : ‚Ñù} (_ : x ‚àà Set.Ioc 1 2),
      (q.totient : ‚Ñù)‚Åª¬π / (x - 1) - C ‚â§ ‚àë' n, residueClass a n / (n : ‚Ñù) ^ x := by
  have H {x : ‚Ñù} (hx : 1 < x) :
      ‚àë' n, residueClass a n / (n : ‚Ñù) ^ x = (auxFun a x).re + (q.totient : ‚Ñù)‚Åª¬π / (x - 1) := by
    refine ofReal_injective ?_
    simp only [ofReal_tsum, ofReal_div, ofReal_cpow (Nat.cast_nonneg _), ofReal_natCast,
      ofReal_add, ofReal_inv, ofReal_sub, ofReal_one]
    simp_rw [‚Üê auxFun_real ha hx, eqOn_auxFun ha <| Set.mem_setOf.mpr (ofReal_re x ‚ñ∏ hx),
      sub_add_cancel, LSeries, term]
    refine tsum_congr fun n ‚Ü¶ ?_
    split_ifs with hn
    ¬∑ simp only [hn, residueClass_apply_zero, ofReal_zero, zero_div]
    ¬∑ rfl
  have : ContinuousOn (fun x : ‚Ñù ‚Ü¶ (auxFun a x).re) (Set.Icc 1 2) :=
    continuous_re.continuousOn.comp (t := Set.univ) (continuousOn_auxFun a)
      (fun ‚¶Éx‚¶Ñ a ‚Ü¶ trivial) |>.comp continuous_ofReal.continuousOn fun x hx ‚Ü¶ by
        simpa only [Set.mem_setOf_eq, ofReal_re] using hx.1
  obtain ‚ü®C, hC‚ü© := bddBelow_def.mp <| IsCompact.bddBelow_image isCompact_Icc this
  replace hC {x : ‚Ñù} (hx : x ‚àà Set.Icc 1 2) : C ‚â§ (auxFun a x).re :=
    hC (auxFun a x).re <| Set.mem_image_of_mem (fun x : ‚Ñù ‚Ü¶ (auxFun a x).re) hx
  refine ‚ü®-C, fun {x} hx ‚Ü¶ ?_‚ü©
  rw [H hx.1, add_comm, sub_neg_eq_add, add_le_add_iff_left]
  exact hC <| Set.mem_Icc_of_Ioc hx

open vonMangoldt Filter Topology in
/-- The function `n ‚Ü¶ Œõ n / n` restricted to primes in an invertible residue class
is not summable. This then implies that there must be infinitely many such primes. -/
lemma not_summable_residueClass_prime_div (ha : IsUnit a) :
    ¬¨ Summable fun n : ‚Ñï ‚Ü¶ (if n.Prime then residueClass a n else 0) / n := by
  intro H
  have key : Summable fun n : ‚Ñï ‚Ü¶ residueClass a n / n := by
    convert (summable_residueClass_non_primes_div a).add H using 2 with n
    simp only [‚Üê add_div, ite_add_ite, zero_add, add_zero, ite_self]
  let C := ‚àë' n, residueClass a n / n
  have H‚ÇÅ {x : ‚Ñù} (hx : 1 < x) : ‚àë' n, residueClass a n / (n : ‚Ñù) ^ x ‚â§ C := by
    refine tsum_le_tsum (fun n ‚Ü¶ ?_) ?_ key
    ¬∑ rcases n.eq_zero_or_pos with rfl | hn
      ¬∑ simp only [Nat.cast_zero, Real.zero_rpow (zero_lt_one.trans hx).ne', div_zero, le_refl]
      ¬∑ refine div_le_div_of_nonneg_left (residueClass_nonneg a _) (mod_cast hn) ?_
        conv_lhs => rw [‚Üê Real.rpow_one n]
        exact Real.rpow_le_rpow_of_exponent_le (by norm_cast) hx.le
    ¬∑ exact summable_real_of_abscissaOfAbsConv_lt <|
        (abscissaOfAbsConv_residueClass_le_one a).trans_lt <| mod_cast hx
  obtain ‚ü®C', hC'‚ü© := LSeries_residueClass_lower_bound ha
  have H‚ÇÅ {x} (hx : x ‚àà Set.Ioc 1 2) : (q.totient : ‚Ñù)‚Åª¬π ‚â§ (C + C') * (x - 1) :=
    (div_le_iff‚ÇÄ <| sub_pos.mpr hx.1).mp <|
      sub_le_iff_le_add.mp <| (hC' hx).trans (H‚ÇÅ hx.1)
  have hq : 0 < (q.totient : ‚Ñù)‚Åª¬π := inv_pos.mpr (mod_cast q.totient.pos_of_neZero)
  rcases le_or_lt (C + C') 0 with h‚ÇÄ | h‚ÇÄ
  ¬∑ have := hq.trans_le (H‚ÇÅ (Set.right_mem_Ioc.mpr one_lt_two))
    rw [show (2 : ‚Ñù) - 1 = 1 by norm_num, mul_one] at this
    exact (this.trans_le h‚ÇÄ).false
  ¬∑ obtain ‚ü®Œæ, hŒæ‚ÇÅ, hŒæ‚ÇÇ‚ü© : ‚àÉ Œæ ‚àà Set.Ioc 1 2, (C + C') * (Œæ - 1) < (q.totient : ‚Ñù)‚Åª¬π := by
      refine ‚ü®min (1 + (q.totient : ‚Ñù)‚Åª¬π / (C + C') / 2) 2, ‚ü®?_, min_le_right ..‚ü©, ?_‚ü©
      ¬∑ simpa only [lt_inf_iff, lt_add_iff_pos_right, Nat.ofNat_pos, div_pos_iff_of_pos_right,
          Nat.one_lt_ofNat, and_true] using div_pos hq h‚ÇÄ
      ¬∑ rw [‚Üê min_sub_sub_right, add_sub_cancel_left, ‚Üê lt_div_iff‚ÇÄ' h‚ÇÄ]
        exact (min_le_left ..).trans_lt <| div_lt_self (div_pos hq h‚ÇÄ) one_lt_two
    exact ((H‚ÇÅ hŒæ‚ÇÅ).trans_lt hŒæ‚ÇÇ).false

end ArithmeticFunction.vonMangoldt

/-!
### Dirichlet's Theorem
-/

section DirichletsTheorem

namespace Nat

open ArithmeticFunction vonMangoldt

variable {q : ‚Ñï} [NeZero q] {a : ZMod q}
/-- **Dirichlet's Theorem** on primes in arithmetic progression: if `q` is a positive
integer and `a : ZMod q` is a unit, then there are infintely many prime numbers `p`
such that `(p : ZMod q) = a`. -/
theorem setOf_prime_and_eq_mod_infinite (ha : IsUnit a) :
    {p : ‚Ñï | p.Prime ‚àß (p : ZMod q) = a}.Infinite := by
  by_contra H
  rw [Set.not_infinite] at H
  exact not_summable_residueClass_prime_div ha <|
    summable_of_finite_support <| support_residueClass_prime_div a ‚ñ∏ H

/-- **Dirichlet's Theorem** on primes in arithmetic progression: if `q` is a positive
integer and `a : ZMod q` is a unit, then there are infintely many prime numbers `p`
such that `(p : ZMod q) = a`. -/
theorem forall_exists_prime_gt_and_eq_mod (ha : IsUnit a) (n : ‚Ñï) :
    ‚àÉ p > n, p.Prime ‚àß (p : ZMod q) = a := by
  obtain ‚ü®p, hp‚ÇÅ, hp‚ÇÇ‚ü© := Set.infinite_iff_exists_gt.mp (setOf_prime_and_eq_mod_infinite ha) n
  exact ‚ü®p, hp‚ÇÇ.gt, Set.mem_setOf.mp hp‚ÇÅ‚ü©

end Nat

end DirichletsTheorem

/-!
### Statement of a version of the Wiener-Ikehara Theorem
-/

open scoped LSeries.notation

open Filter Topology Nat ArithmeticFunction in
/-- A version of the *Wiener-Ikehara Tauberian Theorem*: If `f` is a nonnegative arithmetic
function whose L-series has a simple pole at `s = 1` with residue `A` and otherwise extends
continuously to the closed half-plane `re s ‚â• 1`, then `‚àë n < N, f n` is asymptotic to `A*N`. -/
def WienerIkeharaTheorem : Prop :=
  ‚àÄ {f : ‚Ñï ‚Üí ‚Ñù} {A : ‚Ñù} {F : ‚ÑÇ ‚Üí ‚ÑÇ}, (‚àÄ n, 0 ‚â§ f n) ‚Üí
    Set.EqOn F (fun s ‚Ü¶ L ‚Üóf s - A / (s - 1)) {s | 1 < s.re} ‚Üí
    ContinuousOn F {s | 1 ‚â§ s.re} ‚Üí
    Tendsto (fun N : ‚Ñï ‚Ü¶ ((Finset.range N).sum f) / N) atTop (ùìù A)

/-!
### Derivation of the Prime Number Theorem and Dirichlet's Theorem from the Wiener-Ikehara Theorem
-/

open Filter ArithmeticFunction Topology

/--  The *Wiener-Ikehara Theorem* implies *Dirichlet's Theorem* in the form that
`œà x ‚àº q.totient‚Åª¬π * x`, where `œà x = ‚àë n < x ‚àß n ‚â° a mod q, Œõ n`
and `Œõ` is the von Mangoldt function.

This is Theorem 2 in Section 2 of PNT+ (but using the `WIT` stub defined here). -/
theorem Dirichlet_vonMangoldt (WIT : WienerIkeharaTheorem) {q : ‚Ñï} [NeZero q] {a : ZMod q}
    (ha : IsUnit a) :
    Tendsto (fun N : ‚Ñï ‚Ü¶ (((Finset.range N).filter (fun n : ‚Ñï ‚Ü¶ (n : ZMod q) = a)).sum Œõ) / N)
      atTop (ùìù <| (q.totient : ‚Ñù)‚Åª¬π) := by
  classical
  have H N : ((Finset.range N).filter (fun n : ‚Ñï ‚Ü¶ (n : ZMod q) = a)).sum Œõ =
      (Finset.range N).sum ({n : ‚Ñï | (n : ZMod q) = a}.indicator Œõ) :=
    (Finset.sum_indicator_eq_sum_filter _ _ (fun _ ‚Ü¶ {n : ‚Ñï | n = a}) _).symm
  simp only [H]
  refine WIT (F := vonMangoldt.auxFun a) (fun n ‚Ü¶ ?_) ?_ ?_
  ¬∑ exact Set.indicator_apply_nonneg fun _ ‚Ü¶ vonMangoldt_nonneg
  ¬∑ convert vonMangoldt.eqOn_auxFun ha with s
    push_cast
    rfl
  ¬∑ exact vonMangoldt.continuousOn_auxFun a

/-- The *Wiener-Ikehara Theorem* implies the *Prime Number Theorem* in the form that
`œà x ‚àº x`, where `œà x = ‚àë n < x, Œõ n` and `Œõ` is the von Mangoldt function. -/
theorem PNT_vonMangoldt (WIT : WienerIkeharaTheorem) :
    Tendsto (fun N : ‚Ñï ‚Ü¶ ((Finset.range N).sum Œõ) / N) atTop (ùìù 1) := by
  convert Dirichlet_vonMangoldt WIT (q := 1) (a := 1) isUnit_one with n
  ¬∑ exact (Finset.filter_true_of_mem fun _ _ ‚Ü¶ Subsingleton.eq_one _).symm
  ¬∑ simp only [Nat.totient_one, Nat.cast_one, inv_one]
