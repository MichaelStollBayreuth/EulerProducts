import EulerProducts.LSeries
import EulerProducts.Logarithm
import EulerProducts.DirichletLSeries
import Mathlib.Tactic.RewriteSearch

/-!
### Statement of a version of the Wiener-Ikehara Theorem
-/

open Filter Topology Nat ArithmeticFunction in
/-- A version of the *Wiener-Ikehara Tauberian Theorem*: If `f` is a nonnegative arithmetic
function whose L-series has a simple pole at `s = 1` with residue `A` and otherwise extends
continuously to the closed half-plane `re s â‰¥ 1`, then `âˆ‘ n < N, f n` is asymptotic to `A*N`. -/
def WienerIkeharaTheorem : Prop :=
  âˆ€ {f : ArithmeticFunction â„} {A : â„} {F : â„‚ â†’ â„‚}, (âˆ€ n, 0 â‰¤ f n) â†’
    Set.EqOn F (fun s â†¦ L f s - A / (s - 1)) {s | 1 < s.re} â†’
    ContinuousOn F {s | 1 â‰¤ s.re} â†’
    Tendsto (fun N : â„• â†¦ ((Finset.range N).sum f) / N) atTop (ğ“ A)

/-!
### The Riemann Zeta Function does not vanish on Re(s) = 1
-/

open Complex

local notation (name := rzeta) "Î¶" => riemannZeta

local notation (name := Dchar_one) "Ï‡â‚" => (1 : DirichletCharacter â„‚ 1)

lemma summable_neg_log_one_sub_char_mul_prime_cpow {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) {s : â„‚}
    (hs : 1 < s.re) :
    Summable fun p : Nat.Primes â†¦ -log (1 - Ï‡ p * (p : â„‚) ^ (-s)) := by
  have (p : Nat.Primes) : â€–Ï‡ p * (p : â„‚) ^ (-s)â€– â‰¤ (p : â„) ^ (-s).re
  Â· rw [norm_mul, norm_ofNat_cpow_of_re_ne_zero _ <| re_neg_ne_zero_of_one_lt_re hs]
    calc â€–Ï‡ pâ€– * (p : â„) ^ (-s).re
      _ â‰¤ 1 * (p : â„) ^ (-s.re) := by gcongr; exact DirichletCharacter.norm_le_one Ï‡ _
      _ = _ := one_mul _
  refine (Nat.Primes.summable_rpow.mpr ?_).of_nonneg_of_le (fun _ â†¦ norm_nonneg _) this
    |>.of_norm.neg_log_one_sub
  simp only [neg_re, neg_lt_neg_iff, hs]

/-- A technical lemma showing that a certain linear combination of real parts of logarithms
is nonnegative. This is used to show non-vanishing of the Riemann zeta function and of
Dirichlet L-series on the line `re s = 1`. -/
lemma re_log_comb_nonneg' {a : â„} (haâ‚€ : 0 â‰¤ a) (haâ‚ : a < 1) {z : â„‚} (hz : â€–zâ€– = 1) :
      0 â‰¤ 3 * (-log (1 - a)).re + 4 * (-log (1 - a * z)).re + (-log (1 - a * z ^ 2)).re := by
  have hacâ‚€ : â€–(a : â„‚)â€– < 1
  Â· simp only [norm_eq_abs, abs_ofReal, _root_.abs_of_nonneg haâ‚€, haâ‚]
  have hacâ‚ : â€–a * zâ€– < 1
  Â· rwa [norm_mul, hz, mul_one]
  have hacâ‚‚ : â€–a * z ^ 2â€– < 1
  Â· rwa [norm_mul, norm_pow, hz, one_pow, mul_one]
  have Hâ‚€ := (Complex.hasSum_re <| hasSum_taylorSeries_neg_log hacâ‚€).mul_left 3
  have Hâ‚ := (Complex.hasSum_re <| hasSum_taylorSeries_neg_log hacâ‚).mul_left 4
  have Hâ‚‚ := Complex.hasSum_re <| hasSum_taylorSeries_neg_log hacâ‚‚
  rw [â† ((Hâ‚€.add Hâ‚).add Hâ‚‚).tsum_eq]; clear Hâ‚€ Hâ‚ Hâ‚‚
  refine tsum_nonneg fun n â†¦ ?_
  simp only [mul_pow, â† ofReal_pow, div_nat_cast_re, ofReal_re, mul_re, ofReal_im, zero_mul,
    sub_zero]
  rcases n.eq_zero_or_pos with rfl | hn
  Â· simp
  field_simp
  refine div_nonneg ?_ n.cast_nonneg
  rw [â† pow_mul, pow_mul', sq, mul_re, â† sq, â† sq, â† sq_abs_sub_sq_re, â† norm_eq_abs, norm_pow, hz]
  calc
    0 â‰¤ 2 * a ^ n * ((z ^ n).re + 1) ^ 2 := by positivity
    _ = _  := by ring

/-- The logarithm of an Euler factor of the product `L(Ï‡^0, x)^3 * L(Ï‡, x+I*y)^4 * L(Ï‡^2, x+2*I*y)`
has nonnegative real part when `s = x + I*y` has real part `x > 1`. -/
lemma re_log_comb_nonneg_dirichlet {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) {n : â„•} (hn : 2 â‰¤ n)
    {x y : â„} (hx : 1 < x) (hy : y â‰  0) :
    0 â‰¤ 3 * (-log (1 - (1 : DirichletCharacter â„‚ N) n * n ^ (-x : â„‚))).re +
          4 * (-log (1 - Ï‡ n * n ^ (-(x + I * y)))).re +
          (-log (1 - (Ï‡ n ^ 2) * n ^ (-(x + 2 * I * y)))).re := by
  by_cases hn' : IsUnit (n : ZMod N)
  Â· have haâ‚€ : 0 â‰¤ (n : â„) ^ (-x) := Real.rpow_nonneg n.cast_nonneg _
    have haâ‚ : (n : â„) ^ (-x) < 1
    Â· simpa only [Real.rpow_lt_one_iff n.cast_nonneg, Nat.cast_eq_zero, Nat.one_lt_cast,
        Left.neg_neg_iff, Nat.cast_lt_one, Left.neg_pos_iff]
        using Or.inr <| Or.inl âŸ¨hn, zero_lt_one.trans hxâŸ©
    have hz : â€–Ï‡ n * (n : â„‚) ^ (-(I * y))â€– = 1
    Â· rw [norm_mul, â† hn'.unit_spec, DirichletCharacter.unit_norm_eq_one Ï‡ hn'.unit, one_mul,
        norm_eq_abs, abs_cpow_of_imp fun h â†¦ False.elim <| by linarith [Nat.cast_eq_zero.mp h, hn]]
      simp [hy]
    rw [MulChar.one_apply _ hn', one_mul]
    convert re_log_comb_nonneg' haâ‚€ haâ‚ hz using 6
    Â· congr 2
      exact_mod_cast (ofReal_cpow n.cast_nonneg (-x)).symm
    Â· congr 2
      rw [neg_add, cpow_add _ _ <| by norm_cast; linarith, â† ofReal_neg,
        ofReal_cpow n.cast_nonneg (-x), ofReal_nat_cast]
      ring
    Â· rw [neg_add, cpow_add _ _ <| by norm_cast; linarith, â† ofReal_neg,
        ofReal_cpow n.cast_nonneg (-x), ofReal_nat_cast,
        show -(2 * I * y) = (2 : â„•) * (-I * y) by ring, cpow_nat_mul]
      ring_nf
  Â· simp [MulChar.map_nonunit _ hn']

-- A helper lemma used in the next two proofs
lemma one_lt_re_of_pos {x : â„} (y : â„) (hx : 0 < x) :
    1 < (1 + x : â„‚).re âˆ§ 1 < (1 + x + I * y).re âˆ§ 1 < (1 + x + 2 * I * y).re := by
  simp only [add_re, one_re, ofReal_re, lt_add_iff_pos_right, hx, mul_re, I_re, zero_mul, I_im,
    ofReal_im, mul_zero, sub_self, add_zero, re_ofNat, im_ofNat, mul_one, mul_im, and_self]

section

open Nat ArithmeticFunction

/-- For positive `x` and nonzero `y` we have that
$|L(\chi^0, x)^3 \cdot L(\chi, x+iy)^4 \cdot L(\chi^2, x+2iy)| \ge 1$. -/
lemma norm_dirichlet_product_ge_one {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) {x y : â„} (hx : 0 < x)
    (hy : y â‰  0) :
    â€–L (1 : DirichletCharacter â„‚ N) (1 + x) ^ 3 * L Ï‡ (1 + x + I * y) ^ 4 *
      L (Ï‡ ^ 2 :) (1 + x + 2 * I * y)â€– â‰¥ 1 := by
  have âŸ¨hâ‚€, hâ‚, hâ‚‚âŸ© := one_lt_re_of_pos y hx
  have hxâ‚ : 1 + (x : â„‚) = (1 + x : â„‚).re -- kills three goals of the `convert` below
  Â· simp only [add_re, one_re, ofReal_re, ofReal_add, ofReal_one]
  have hsumâ‚€ :=
    (summable_re <|
      summable_neg_log_one_sub_char_mul_prime_cpow (1 : DirichletCharacter â„‚ N) hâ‚€).mul_left 3
  have hsumâ‚ := (summable_re <| summable_neg_log_one_sub_char_mul_prime_cpow Ï‡ hâ‚).mul_left 4
  have hsumâ‚‚ := summable_re <| summable_neg_log_one_sub_char_mul_prime_cpow (Ï‡ ^ 2) hâ‚‚
  rw [â† LSeries_dirichlet_eulerProduct' _ hâ‚€, â† LSeries_dirichlet_eulerProduct' Ï‡ hâ‚,
    â† LSeries_dirichlet_eulerProduct' (Ï‡ ^ 2) hâ‚‚, â† exp_nat_mul, â† exp_nat_mul, â† exp_add,
    â† exp_add, norm_eq_abs, abs_exp]
  simp only [Nat.cast_ofNat, add_re, mul_re, re_ofNat, im_ofNat, zero_mul, sub_zero,
    Real.one_le_exp_iff]
  rw [re_tsum <| summable_neg_log_one_sub_char_mul_prime_cpow _ hâ‚€,
    re_tsum <| summable_neg_log_one_sub_char_mul_prime_cpow _ hâ‚,
    re_tsum <| summable_neg_log_one_sub_char_mul_prime_cpow _ hâ‚‚, â† tsum_mul_left, â† tsum_mul_left,
    â† tsum_add hsumâ‚€ hsumâ‚, â† tsum_add (hsumâ‚€.add hsumâ‚) hsumâ‚‚]
  convert tsum_nonneg fun p : Nat.Primes â†¦ re_log_comb_nonneg_dirichlet Ï‡ p.prop.two_le hâ‚€ hy
  rw [sq, sq, MulChar.mul_apply]

/-- For positive `x` and nonzero `y` we have that
$|\zeta(x)^3 \cdot \zeta(x+iy)^4 \cdot \zeta(x+2iy)| \ge 1$. -/
lemma norm_zeta_product_ge_one {x y : â„} (hx : 0 < x) (hy : y â‰  0) :
    â€–Î¶ (1 + x) ^ 3 * Î¶ (1 + x + I * y) ^ 4 * Î¶ (1 + x + 2 * I * y)â€– â‰¥ 1 := by
  have âŸ¨hâ‚€, hâ‚, hâ‚‚âŸ© := one_lt_re_of_pos y hx
  simpa only [one_pow, dirichletCharModOne_eq_zeta, LSeries.zeta_eq_riemannZeta, hâ‚€, hâ‚, hâ‚‚]
    using norm_dirichlet_product_ge_one Ï‡â‚ hx hy

open BigOperators Finset ZMod in
lemma prod_primesBelow_mul_eq_prod_primesBelow {N : â„•} (hN : N â‰  0) {s : â„‚} (hs : 1 < s.re)
    {n : â„•} (hn : N < n) :
    (âˆ p in primesBelow n, (1 - (p : â„‚) ^ (-s))â»Â¹) * (âˆ p in N.primeFactors, (1 - (p : â„‚) ^ (-s))) =
        âˆ p in primesBelow n, (1 - (1 : DirichletCharacter â„‚ N) p * (p : â„‚) ^ (-s))â»Â¹ := by
  letI Îµ : DirichletCharacter â„‚ N := 1
  rw [mul_comm]
  have hd : Disjoint N.primeFactors (n.primesBelow.filter (Â· âˆ‰ N.primeFactors))
  Â· convert disjoint_filter_filter_neg N.primeFactors n.primesBelow (Â· âˆˆ N.primeFactors)
    rw [filter_mem_eq_inter, inter_self]
  have hdeq : disjUnion _ _ hd = primesBelow n
  Â· simp only [disjUnion_eq_union]
    ext p
    simp only [mem_union, mem_filter]
    refine âŸ¨fun H' â†¦ H'.elim (fun H â†¦ ?_) fun H â†¦ H.1, fun _ â†¦ by tautoâŸ©
    exact mem_primesBelow.mpr âŸ¨(le_of_mem_primeFactors H).trans_lt hn, prime_of_mem_primeFactors HâŸ©
  have Hâ‚ := hdeq â–¸ prod_disjUnion (f := fun p : â„• â†¦ (1 - Îµ p * (p : â„‚) ^ (-s))â»Â¹) hd
  have Hâ‚‚ := hdeq â–¸ prod_disjUnion (f := fun p : â„• â†¦ (1 - (p : â„‚) ^ (-s))â»Â¹) hd
  have Hâ‚ƒ : âˆ p in N.primeFactors, (1 - Îµ p * (p : â„‚) ^ (-s))â»Â¹ = 1
  Â· refine prod_eq_one fun p hp â†¦ ?_
    rw [MulChar.map_nonunit _ <| not_isUnit_of_mem_primeFactors hp, zero_mul, sub_zero, inv_one]
  rw [Hâ‚, Hâ‚‚, Hâ‚ƒ, one_mul, â† mul_assoc, â† prod_mul_distrib]; clear Hâ‚ Hâ‚‚ Hâ‚ƒ
  conv => enter [2]; rw [â† one_mul (âˆ p in (n.primesBelow.filter _), _)]
  congr 1
  Â· exact prod_eq_one fun p hp â†¦
      mul_inv_cancel <| one_sub_prime_cpow_ne_zero (prime_of_mem_primeFactors hp) hs
  Â· refine prod_congr rfl fun p hp â†¦ ?_
    simp only [mem_primeFactors, ne_eq, hN, not_false_eq_true, and_true, not_and, mem_filter] at hp
    have hpâ‚ := (mem_primesBelow.mp hp.1).2
    rw [MulChar.one_apply _ <| isUnit_prime_of_not_dvd hpâ‚ <| hp.2 hpâ‚, one_mul]

open BigOperators in
lemma LSeries.exists_extension_of_trivial {N : â„•} (hN : N â‰  0) {s : â„‚} (hs : 1 < s.re) :
    L (1 : DirichletCharacter â„‚ N) s = Î¶ s * âˆ p in N.primeFactors, (1 - (p : â„‚) ^ (-s)) := by
  have HÎ¶ := (riemannZeta_eulerProduct hs).mul_const (âˆ p in N.primeFactors, (1 - (p : â„‚) ^ (-s)))
  have HL := dirichletLSeries_eulerProduct (1 : DirichletCharacter â„‚ N) hs
  have Hev : (fun n : â„• â†¦ (âˆ p in primesBelow n, (1 - (p : â„‚) ^ (-s))â»Â¹) *
    (âˆ p in N.primeFactors, (1 - (p : â„‚) ^ (-s)))) =á¶ [Filter.atTop]
    (fun n : â„• â†¦ âˆ p in primesBelow n, (1 - (1 : DirichletCharacter â„‚ N) p * (p : â„‚) ^ (-s))â»Â¹)
  Â· refine Filter.eventuallyEq_of_mem (s := {n | N < n}) ?_
      fun _ â†¦ prod_primesBelow_mul_eq_prod_primesBelow hN hs
    simp only [Filter.mem_atTop_sets, Set.mem_setOf_eq]
    exact âŸ¨N + 1, fun _ hm â†¦ hmâŸ©
  convert (tendsto_nhds_unique (Filter.Tendsto.congr' Hev HÎ¶) HL).symm using 1
  rw [LSeries]
  congr
  funext n
  simp only [dirichletSummandHom, MonoidWithZeroHom.coe_mk, ZeroHom.coe_mk]
  rcases eq_or_ne n 0 with rfl | hn
  Â· simp only [ArithmeticFunction.map_zero, CharP.cast_eq_zero, ne_eq, ne_zero_of_one_lt_re hs,
      not_false_eq_true, zero_cpow, div_zero, cast_zero, neg_eq_zero, mul_zero]
  rw [DirichletCharacter.toArithmeticFunction_apply_of_ne_zero _ hn, div_eq_mul_inv, cpow_neg]

end

section

open Filter Topology Homeomorph Asymptotics

lemma riemannZeta_isBigO_near_one_horizontal :
    (fun x : â„ â†¦ Î¶ (1 + x)) =O[ğ“[>] 0] (fun x â†¦ (1 : â„‚) / x) := by
  have : (fun w : â„‚ â†¦ Î¶ (1 + w)) =O[ğ“[â‰ ] 0] (1 / Â·)
  Â· have H : Tendsto (fun w â†¦ w * Î¶ (1 + w)) (ğ“[â‰ ] 0) (ğ“ 1)
    Â· convert Tendsto.comp (f := fun w â†¦ 1 + w) riemannZeta_residue_one ?_ using 1
      Â· ext w
        simp only [Function.comp_apply, add_sub_cancel']
      Â· refine tendsto_iff_comap.mpr <| map_le_iff_le_comap.mp <| Eq.le ?_
        convert map_punctured_nhds_eq (Homeomorph.addLeft (1 : â„‚)) 0 using 2 <;> simp
    exact ((isBigO_mul_iff_isBigO_div eventually_mem_nhdsWithin).mp <|
      Tendsto.isBigO_one â„‚ H).trans <| isBigO_refl ..
  exact (isBigO_comp_ofReal_nhds_ne this).mono <| nhds_right'_le_nhds_ne 0

lemma riemannZeta_isBigO_of_ne_one_horizontal {y : â„} (hy : y â‰  0) :
    (fun x : â„ â†¦ Î¶ (1 + x + I * y)) =O[ğ“[>] 0] (fun _ â†¦ (1 : â„‚)) := by
  refine Asymptotics.IsBigO.mono ?_ nhdsWithin_le_nhds
  have hy' : 1 + I * y â‰  1 := by simp [hy]
  convert isBigO_comp_ofReal
    (differentiableAt_riemannZeta hy').continuousAt.isBigO using 3 with x
  ring

lemma riemannZeta_isBigO_near_root_horizontal {y : â„} (hy : y â‰  0) (h : Î¶ (1 + I * y) = 0) :
    (fun x : â„ â†¦ Î¶ (1 + x + I * y)) =O[ğ“[>] 0] fun x : â„ â†¦ (x : â„‚) := by
  have hy' : 1 + I * y â‰  1 := by simp [hy]
  conv => enter [2, x]; rw [add_comm 1, add_assoc]
  exact (isBigO_comp_ofReal <| (differentiableAt_riemannZeta hy').isBigO_of_eq_zero h).mono
    nhdsWithin_le_nhds

/-- The Riemann Zeta Function does not vanish on the closed half-plane `re z â‰¥ 1`. -/
lemma riemannZeta_ne_zero_of_one_le_re â¦ƒz : â„‚â¦„ (hz : z â‰  1) (hz' : 1 â‰¤ z.re) : Î¶ z â‰  0 := by
  refine hz'.eq_or_lt.elim (fun h Hz â†¦ ?_) riemannZeta_ne_zero_of_one_lt_re
  -- We assume that `Î¶ z = 0` and `z.re = 1` and derive a contradiction.
  have hzâ‚€ : z.im â‰  0
  Â· rw [â† re_add_im z, â† h, ofReal_one] at hz
    simpa only [ne_eq, add_right_eq_self, mul_eq_zero, ofReal_eq_zero, I_ne_zero, or_false]
      using hz
  have hzeq : z = 1 + I * z.im
  Â· rw [mul_comm I, â† re_add_im z, â† h]
    push_cast
    simp only [add_im, one_im, mul_im, ofReal_re, I_im, mul_one, ofReal_im, I_re, mul_zero,
      add_zero, zero_add]
  -- The key step: the vanishing assumption implies that the zeta product below
  -- also vanishes at `z`. We only need the right-hand limit keeping the imaginary part fixed.
  have Hâ‚€ : (fun _ : â„ â†¦ (1 : â„)) =O[ğ“[>] 0]
      (fun x â†¦ Î¶ (1 + x) ^ 3 * Î¶ (1 + x + I * z.im) ^ 4 * Î¶ (1 + x + 2 * I * z.im)) :=
    IsBigO.of_bound' <| eventually_nhdsWithin_of_forall
      fun _ hx â†¦ (norm_one (Î± := â„)).symm â–¸ (norm_zeta_product_ge_one hx hzâ‚€).le
  have H := (riemannZeta_isBigO_near_one_horizontal.pow 3).mul
    ((riemannZeta_isBigO_near_root_horizontal hzâ‚€ (hzeq â–¸ Hz)).pow 4)|>.mul <|
    riemannZeta_isBigO_of_ne_one_horizontal <| mul_ne_zero two_ne_zero hzâ‚€
  have help (x : â„) : ((1 / x) ^ 3 * x ^ 4 * 1 : â„‚) = x
  Â· rcases eq_or_ne x 0 with rfl | h
    Â· simp only [ofReal_zero, zero_pow' 4 (by norm_num), mul_zero, mul_one]
    Â· field_simp [h]
      rfl
  conv at H => enter [3, x]; rw [help]
  conv at H =>
    enter [2, x]; rw [show 1 + x + I * â†‘(2 * z.im) = 1 + x + 2 * I * z.im by simp; ring]
  replace H := (Hâ‚€.trans H).norm_right
  simp only [norm_eq_abs, abs_ofReal] at H
  refine isLittleO_irrefl ?_ <| H.of_abs_right.trans_isLittleO <| isLittleO_id (Set.Ioi 0)
  simp only [ne_eq, one_ne_zero, not_false_eq_true, frequently_true_iff_neBot]
  exact mem_closure_iff_nhdsWithin_neBot.mp <| closure_Ioi (0 : â„) â–¸ Set.left_mem_Ici

end

/-!
### The logarithmic derivative of Î¶ has a simple pole at s = 1 with residue -1

We show that `s â†¦ Î¶' s / Î¶ s + 1 / (s - 1)` (or rather, its negative, which is the function
we need for the Wiener-Ikehara Theorem) is continuous outside the zeros of `Î¶`.
-/

/-- The function obtained by "multiplying away" the pole of `Î¶`. Its (negative) logarithmic
derivative is the function used in the Wiener-Ikehara Theorem to prove the Prime Number
Theorem. -/
noncomputable def Î¶â‚ : â„‚ â†’ â„‚ := Function.update (fun z â†¦ Î¶ z * (z - 1)) 1 1

lemma Î¶â‚_apply_of_ne_one {z : â„‚} (hz : z â‰  1) : Î¶â‚ z = Î¶ z * (z - 1) := by
  simp [Î¶â‚, hz]

lemma Î¶â‚_differentiable : Differentiable â„‚ Î¶â‚ := by
  rw [â† differentiableOn_univ,
    â† differentiableOn_compl_singleton_and_continuousAt_iff (c := 1) Filter.univ_mem, Î¶â‚]
  refine âŸ¨DifferentiableOn.congr (f := fun z â†¦ Î¶ z * (z - 1))
    (fun _ hz â†¦ DifferentiableAt.differentiableWithinAt ?_) fun _ hz â†¦ ?_,
    continuousWithinAt_compl_self.mp ?_âŸ©
  Â· simp only [Set.mem_diff, Set.mem_univ, Set.mem_singleton_iff, true_and] at hz
    exact (differentiableAt_riemannZeta hz).mul <| (differentiableAt_id').sub <|
      differentiableAt_const 1
  Â· simp only [Set.mem_diff, Set.mem_univ, Set.mem_singleton_iff, true_and] at hz
    simp only [ne_eq, hz, not_false_eq_true, Function.update_noteq]
  Â· conv in (_ * _) => rw [mul_comm]
    simp only [continuousWithinAt_compl_self, continuousAt_update_same]
    exact riemannZeta_residue_one

lemma deriv_Î¶â‚_apply_of_ne_one  {z : â„‚} (hz : z â‰  1) :
    deriv Î¶â‚ z = deriv Î¶ z * (z - 1) + Î¶ z := by
  have H : deriv Î¶â‚ z = deriv (fun w â†¦ Î¶ w * (w - 1)) z
  Â· refine Filter.EventuallyEq.deriv_eq <| Filter.eventuallyEq_iff_exists_mem.mpr ?_
    refine âŸ¨{w | w â‰  1}, IsOpen.mem_nhds isOpen_ne hz, fun w hw â†¦ ?_âŸ©
    simp only [Î¶â‚, ne_eq, Set.mem_setOf.mp hw, not_false_eq_true, Function.update_noteq]
  rw [H, deriv_mul (differentiableAt_riemannZeta hz) <| differentiableAt_id'.sub <|
    differentiableAt_const 1, deriv_sub_const, deriv_id'', mul_one]

lemma neg_logDeriv_Î¶â‚_eq {z : â„‚} (hzâ‚ : z â‰  1) (hzâ‚‚ : Î¶ z â‰  0) :
    -deriv Î¶â‚ z / Î¶â‚ z = -deriv Î¶ z / Î¶ z - 1 / (z - 1) := by
  rw [Î¶â‚_apply_of_ne_one hzâ‚, deriv_Î¶â‚_apply_of_ne_one hzâ‚]
  field_simp [sub_ne_zero.mpr hzâ‚]
  ring

lemma continuousOn_neg_logDeriv_Î¶â‚ :
    ContinuousOn (fun z â†¦ -deriv Î¶â‚ z / Î¶â‚ z) {z | z = 1 âˆ¨ Î¶ z â‰  0} := by
  simp_rw [neg_div]
  refine ((Î¶â‚_differentiable.contDiff.continuous_deriv le_rfl).continuousOn.div
    Î¶â‚_differentiable.continuous.continuousOn fun w hw â†¦ ?_).neg
  rcases eq_or_ne w 1 with rfl | hw'
  Â· simp only [Î¶â‚, Function.update_same, ne_eq, one_ne_zero, not_false_eq_true]
  Â· simp only [ne_eq, Set.mem_setOf_eq, hw', false_or] at hw
    simp only [Î¶â‚, ne_eq, hw', not_false_eq_true, Function.update_noteq, _root_.mul_eq_zero, hw,
      false_or]
    exact sub_ne_zero.mpr hw'

/-!
### Derivation of the Prime Number Theorem from the Wiener-Ikehara Theorem
-/

open Filter Nat ArithmeticFunction in
/-- The *Wiener-Ikehara Theorem* implies the *Prime Number Theorem* in the form that
`Ïˆ x âˆ¼ x`, where `Ïˆ x = âˆ‘ n < x, Î› n` and `Î›` is the von Mangoldt function. -/
theorem PNT_vonMangoldt (WIT : WienerIkeharaTheorem) :
    Tendsto (fun N : â„• â†¦ ((Finset.range N).sum Î›) / N) atTop (nhds 1) := by
  have hnv := riemannZeta_ne_zero_of_one_le_re
  refine WIT (F := fun z â†¦ -deriv Î¶â‚ z / Î¶â‚ z) (fun _ â†¦ vonMangoldt_nonneg) (fun s hs â†¦ ?_) ?_
  Â· have hsâ‚ : s â‰  1
    Â· rintro rfl
      simp at hs
    simp only [ne_eq, hsâ‚, not_false_eq_true, LSeries_vonMangoldt_eq_deriv_riemannZeta_div hs,
      ofReal_one]
    exact neg_logDeriv_Î¶â‚_eq hsâ‚ <| hnv hsâ‚ (Set.mem_setOf.mp hs).le
  Â· refine continuousOn_neg_logDeriv_Î¶â‚.mono fun s _ â†¦ ?_
    specialize @hnv s
    simp at *
    tauto
