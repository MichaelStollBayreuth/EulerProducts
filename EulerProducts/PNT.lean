import EulerProducts.Logarithm
import Mathlib.Analysis.SpecialFunctions.Complex.LogBounds
import Mathlib.NumberTheory.DirichletCharacter.Orthogonality
import Mathlib.NumberTheory.LSeries.Linearity
import Mathlib.NumberTheory.LSeries.QuadraticNonvanishing
import Mathlib.RingTheory.RootsOfUnity.AlgebraicallyClosed

section BigO

open Topology Asymptotics Filter

lemma Complex.isBigO_comp_ofReal {f g : â„‚ â†’ â„‚} {x : â„} (h : f =O[ğ“ (x : â„‚)] g) :
    (fun y : â„ â†¦ f y) =O[ğ“ x] (fun y : â„ â†¦ g y) :=
  Asymptotics.IsBigO.comp_tendsto (k := fun y : â„ â†¦ (y : â„‚)) h <|
    Continuous.tendsto Complex.continuous_ofReal x

lemma Complex.isBigO_comp_ofReal_nhds_ne {f g : â„‚ â†’ â„‚} {x : â„} (h : f =O[ğ“[â‰ ] (x : â„‚)] g) :
    (fun y : â„ â†¦ f y) =O[ğ“[â‰ ] x] (fun y : â„ â†¦ g y) :=
  Asymptotics.IsBigO.comp_tendsto (k := fun y : â„ â†¦ (y : â„‚)) h <|
    ((hasDerivAt_id (x : â„‚)).comp_ofReal).tendsto_punctured_nhds one_ne_zero

lemma ContinuousAt.isBigO {f : â„‚ â†’ â„‚} {z : â„‚} (hf : ContinuousAt f z) :
    (fun w â†¦ f (w + z)) =O[ğ“ 0] (fun _ â†¦ (1 : â„‚)) := by
  rw [isBigO_iff']
  replace hf : ContinuousAt (fun w â†¦ f (w + z)) 0 := by
    convert (Homeomorph.comp_continuousAt_iff' (Homeomorph.addLeft (-z)) _ z).mp ?_
    Â· simp
    Â· simp [Function.comp_def, hf]
  simp_rw [Metric.continuousAt_iff', dist_eq_norm_sub, zero_add] at hf
  specialize hf 1 zero_lt_one
  refine âŸ¨â€–f zâ€– + 1, by positivity, ?_âŸ©
  refine Eventually.mp hf <| Eventually.of_forall fun w hw â†¦ le_of_lt ?_
  calc â€–f (w + z)â€–
    _ â‰¤ â€–f zâ€– + â€–f (w + z) - f zâ€– := norm_le_insert' ..
    _ < â€–f zâ€– + 1 := add_lt_add_left hw _
    _ = _ := by simp only [norm_one, mul_one]

lemma DifferentiableAt.isBigO_of_eq_zero {f : â„‚ â†’ â„‚} {z : â„‚} (hf : DifferentiableAt â„‚ f z)
    (hz : f z = 0) : (fun w â†¦ f (w + z)) =O[ğ“ 0] id := by
  rw [â† zero_add z] at hf
  simpa only [zero_add, hz, sub_zero]
    using (hf.hasDerivAt.comp_add_const 0 z).differentiableAt.isBigO_sub

end BigO

section LSeries

-- This should go to `Mathlib.NumberTheory.LSeries.Linearity`

open LSeries

variable {Î¹ : Type*} [DecidableEq Î¹] {s : â„‚} {f : Î¹ â†’ â„• â†’ â„‚} (S : Finset Î¹)

lemma LSeries.term_sum_apply (s : â„‚) (n : â„•) :
    term (âˆ‘ i âˆˆ S, f i) s n  = âˆ‘ i âˆˆ S, term (f i) s n := by
  induction S using Finset.induction_on with
  | empty =>
    simp only [term, Finset.sum_apply, Finset.sum_empty, zero_div, ite_self, Finset.sum_ite_irrel,
      Finset.sum_const_zero]
  | insert hi IH  =>
    simp only [Finset.sum_insert hi, term_add_apply, IH]

lemma LSeries.term_sum (s : â„‚) : term (âˆ‘ i âˆˆ S, f i) s  = âˆ‘ i âˆˆ S, term (f i) s := by
  ext1
  rw [Finset.sum_apply]
  exact term_sum_apply S s _

lemma LSeriesHasSum.sum {a : Î¹ â†’ â„‚} (hf : âˆ€ i âˆˆ S, LSeriesHasSum (f i) s (a i)) :
    LSeriesHasSum (âˆ‘ i âˆˆ S, f i) s (âˆ‘ i âˆˆ S, a i) := by
  simpa only [LSeriesHasSum, term_sum, Finset.sum_fn S fun i â†¦ term (f i) s]
    using hasSum_sum hf

lemma LSeriesSummable.sum (hf : âˆ€ i âˆˆ S, LSeriesSummable (f i) s) :
    LSeriesSummable (âˆ‘ i âˆˆ S, f i) s := by
  simpa only [LSeriesSummable, â† term_sum_apply] using summable_sum hf

@[simp]
lemma LSeries_sum (hf : âˆ€ i âˆˆ S, LSeriesSummable (f i) s) :
    LSeries (âˆ‘ i âˆˆ S, f i) s = âˆ‘ i âˆˆ S, LSeries (f i) s := by
  simpa only [LSeries, term_sum, Finset.sum_apply]
    using tsum_sum hf

variable [Fintype Î¹]

/-- The version of `LSeriesHasSum.sum` for `Fintype.sum`. -/
lemma LSeriesHasSum.sum' {a : Î¹ â†’ â„‚} (hf : âˆ€ i, LSeriesHasSum (f i) s (a i)) :
    LSeriesHasSum (âˆ‘ i : Î¹, f i) s (âˆ‘ i : Î¹, a i) :=
  LSeriesHasSum.sum Finset.univ fun i _ â†¦ hf i

/-- The version of `LSeriesSummable.sum` for `Fintype.sum`. -/
lemma LSeriesSummable.sum' (hf : âˆ€ i, LSeriesSummable (f i) s) :
    LSeriesSummable (âˆ‘ i : Î¹, f i) s :=
  LSeriesSummable.sum Finset.univ fun i _ â†¦ hf i

/-- The version of `LSeries_sum` for `Fintype.sum`. -/
@[simp]
lemma LSeries_sum' (hf : âˆ€ i, LSeriesSummable (f i) s) :
    LSeries (âˆ‘ i : Î¹, f i) s = âˆ‘ i : Î¹, LSeries (f i) s :=
  LSeries_sum Finset.univ fun i _ â†¦ hf i

end LSeries

/-!
### Statement of a version of the Wiener-Ikehara Theorem
-/

open scoped LSeries.notation

open Filter Topology Nat ArithmeticFunction in
/-- A version of the *Wiener-Ikehara Tauberian Theorem*: If `f` is a nonnegative arithmetic
function whose L-series has a simple pole at `s = 1` with residue `A` and otherwise extends
continuously to the closed half-plane `re s â‰¥ 1`, then `âˆ‘ n < N, f n` is asymptotic to `A*N`. -/
def WienerIkeharaTheorem : Prop :=
  âˆ€ {f : â„• â†’ â„} {A : â„} {F : â„‚ â†’ â„‚}, (âˆ€ n, 0 â‰¤ f n) â†’
    Set.EqOn F (fun s â†¦ L â†—f s - A / (s - 1)) {s | 1 < s.re} â†’
    ContinuousOn F {s | 1 â‰¤ s.re} â†’
    Tendsto (fun N : â„• â†¦ ((Finset.range N).sum f) / N) atTop (ğ“ A)

/-!
### The L-function of a Dirichlet character does not vanish on Re(s) = 1
-/

open Complex

-- A helper lemma used in some proofs below
lemma one_lt_re_of_pos {x : â„} (y : â„) (hx : 0 < x) :
    1 < (1 + x : â„‚).re âˆ§ 1 < (1 + x + I * y).re âˆ§ 1 < (1 + x + 2 * I * y).re := by
  simp only [add_re, one_re, ofReal_re, lt_add_iff_pos_right, hx, mul_re, I_re, zero_mul, I_im,
    ofReal_im, mul_zero, sub_self, add_zero, re_ofNat, im_ofNat, mul_one, mul_im, and_self]

/-- We use `Î¶` to denote the Riemann zeta function. -/
local notation (name := rzeta) "Î¶" => riemannZeta

/-- We use `Ï‡â‚` to denote the (trivial) Dirichlet character modulo `1`. -/
local notation (name := Dchar_one') "Ï‡â‚" => (1 : DirichletCharacter â„‚ 1)

section EulerProduct

-- This gets moved to `NumberTheory.LSeries.EulerProduct`

open LSeries Nat EulerProduct

/-- A variant of the Euler product for Dirichlet L-series. -/
theorem DirichletCharacter.LSeries_eulerProduct' {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) {s : â„‚}
    (hs : 1 < s.re) :
    exp (âˆ‘' p : Nat.Primes, -log (1 - Ï‡ p * p ^ (-s))) = L â†—Ï‡ s := by
  rw [LSeries]
  convert exp_sum_primes_log_eq_tsum (f := dirichletSummandHom Ï‡ <| ne_zero_of_one_lt_re hs) <|
    summable_dirichletSummand Ï‡ hs -- where does the `xâœ: â„•` come from??
  ext n
  rcases eq_or_ne n 0 with rfl | hn
  Â· simp only [term_zero, map_zero]
  Â· simp [hn, dirichletSummandHom, div_eq_mul_inv, cpow_neg]

open DirichletCharacter

/-- A variant of the Euler product for the L-series of `Î¶`. -/
theorem ArithmeticFunction.LSeries_zeta_eulerProduct' {s : â„‚} (hs : 1 < s.re) :
    exp (âˆ‘' p : Nat.Primes, -Complex.log (1 - p ^ (-s))) = L 1 s := by
  convert modOne_eq_one (R := â„‚) â–¸ LSeries_eulerProduct' Ï‡â‚ hs using 7
  rw [MulChar.one_apply <| isUnit_of_subsingleton _, one_mul]

/-- A variant of the Euler product for the Riemann zeta function. -/
theorem riemannZeta_eulerProduct'  {s : â„‚} (hs : 1 < s.re) :
    exp (âˆ‘' p : Nat.Primes, -Complex.log (1 - p ^ (-s))) = riemannZeta s :=
  LSeries_one_eq_riemannZeta hs â–¸ ArithmeticFunction.LSeries_zeta_eulerProduct' hs

end EulerProduct


lemma summable_neg_log_one_sub_char_mul_prime_cpow {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) {s : â„‚}
    (hs : 1 < s.re) :
    Summable fun p : Nat.Primes â†¦ -log (1 - Ï‡ p * (p : â„‚) ^ (-s)) := by
  have (p : Nat.Primes) : â€–Ï‡ p * (p : â„‚) ^ (-s)â€– â‰¤ (p : â„) ^ (-s).re := by
    rw [norm_mul, norm_natCast_cpow_of_re_ne_zero _ <| re_neg_ne_zero_of_one_lt_re hs]
    calc â€–Ï‡ pâ€– * (p : â„) ^ (-s).re
      _ â‰¤ 1 * (p : â„) ^ (-s.re) := by gcongr; exact DirichletCharacter.norm_le_one Ï‡ _
      _ = _ := one_mul _
  refine (Nat.Primes.summable_rpow.mpr ?_).of_nonneg_of_le (fun _ â†¦ norm_nonneg _) this
    |>.of_norm.neg_clog_one_sub
  simp only [neg_re, neg_lt_neg_iff, hs]

/-- A technical lemma showing that a certain linear combination of real parts of logarithms
is nonnegative. This is used to show non-vanishing of the Riemann zeta function and of
Dirichlet L-series on the line `re s = 1`. -/
lemma re_log_comb_nonneg {a : â„} (haâ‚€ : 0 â‰¤ a) (haâ‚ : a < 1) {z : â„‚} (hz : â€–zâ€– = 1) :
      0 â‰¤ 3 * (-log (1 - a)).re + 4 * (-log (1 - a * z)).re + (-log (1 - a * z ^ 2)).re := by
  have hacâ‚€ : â€–(a : â„‚)â€– < 1 := by
    simp only [norm_eq_abs, abs_ofReal, _root_.abs_of_nonneg haâ‚€, haâ‚]
  have hacâ‚ : â€–a * zâ€– < 1 := by rwa [norm_mul, hz, mul_one]
  have hacâ‚‚ : â€–a * z ^ 2â€– < 1 := by rwa [norm_mul, norm_pow, hz, one_pow, mul_one]
  have Hâ‚€ := (hasSum_re <| hasSum_taylorSeries_neg_log hacâ‚€).mul_left 3
  have Hâ‚ := (hasSum_re <| hasSum_taylorSeries_neg_log hacâ‚).mul_left 4
  have Hâ‚‚ := hasSum_re <| hasSum_taylorSeries_neg_log hacâ‚‚
  rw [â† ((Hâ‚€.add Hâ‚).add Hâ‚‚).tsum_eq]; clear Hâ‚€ Hâ‚ Hâ‚‚
  refine tsum_nonneg fun n â†¦ ?_
  simp only [mul_pow, â† ofReal_pow, div_natCast_re, ofReal_re, mul_re, ofReal_im, zero_mul,
    sub_zero]
  rcases n.eq_zero_or_pos with rfl | hn
  Â· simp
  field_simp
  refine div_nonneg ?_ n.cast_nonneg
  rw [â† pow_mul, pow_mul', sq, mul_re, â† sq, â† sq, â† sq_abs_sub_sq_re, â† norm_eq_abs, norm_pow, hz]
  calc
    0 â‰¤ 2 * a ^ n * ((z ^ n).re + 1) ^ 2 := by positivity
    _ = _  := by ring

namespace DirichletCharacter

lemma deriv_LFunction_eq_deriv_LSeries {n : â„•} [NeZero n] (Ï‡ : DirichletCharacter â„‚ n) {s : â„‚}
    (hs : 1 < s.re) :
    deriv (LFunction Ï‡) s = deriv (LSeries â†—Ï‡) s :=  by
  refine Filter.EventuallyEq.deriv_eq ?_
  have h : {z | 1 < z.re} âˆˆ nhds s :=
    (isOpen_lt continuous_const continuous_re).mem_nhds hs
  filter_upwards [h] with z hz
  exact LFunction_eq_LSeries Ï‡ hz

/-- The logarithm of an Euler factor of the product `L(Ï‡^0, x)^3 * L(Ï‡, x+I*y)^4 * L(Ï‡^2, x+2*I*y)`
has nonnegative real part when `s = x + I*y` has real part `x > 1`. -/
lemma re_log_comb_nonneg {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) {n : â„•} (hn : 2 â‰¤ n) {x y : â„}
    (hx : 1 < x) :
    0 â‰¤ 3 * (-log (1 - (1 : DirichletCharacter â„‚ N) n * n ^ (-x : â„‚))).re +
          4 * (-log (1 - Ï‡ n * n ^ (-(x + I * y)))).re +
          (-log (1 - (Ï‡ n ^ 2) * n ^ (-(x + 2 * I * y)))).re := by
  by_cases hn' : IsUnit (n : ZMod N)
  Â· have haâ‚€ : 0 â‰¤ (n : â„) ^ (-x) := Real.rpow_nonneg n.cast_nonneg _
    have haâ‚ : (n : â„) ^ (-x) < 1 := by
      simpa only [Real.rpow_lt_one_iff n.cast_nonneg, Nat.cast_eq_zero, Nat.one_lt_cast,
        Left.neg_neg_iff, Nat.cast_lt_one, Left.neg_pos_iff]
        using Or.inr <| Or.inl âŸ¨hn, zero_lt_one.trans hxâŸ©
    have hz : â€–Ï‡ n * (n : â„‚) ^ (-(I * y))â€– = 1 := by
      rw [norm_mul, â† hn'.unit_spec, DirichletCharacter.unit_norm_eq_one Ï‡ hn'.unit, one_mul,
        norm_eq_abs, abs_cpow_of_imp fun h â†¦ False.elim <| by linarith [Nat.cast_eq_zero.mp h, hn]]
      simp
    rw [MulChar.one_apply hn', one_mul]
    convert _root_.re_log_comb_nonneg haâ‚€ haâ‚ hz using 6
    Â· congr 2
      exact_mod_cast (ofReal_cpow n.cast_nonneg (-x)).symm
    Â· congr 2
      rw [neg_add, cpow_add _ _ <| by norm_cast; linarith, â† ofReal_neg,
        ofReal_cpow n.cast_nonneg (-x), ofReal_natCast]
      ring
    Â· rw [neg_add, cpow_add _ _ <| by norm_cast; linarith, â† ofReal_neg,
        ofReal_cpow n.cast_nonneg (-x), ofReal_natCast,
        show -(2 * I * y) = (2 : â„•) * (-I * y) by ring, cpow_nat_mul]
      ring_nf
  Â· simp [MulChar.map_nonunit _ hn']

variable {N : â„•} [NeZero N] {Ï‡ : DirichletCharacter â„‚ N}

open Complex BigOperators Filter Topology Homeomorph Asymptotics

open scoped LSeries.notation

open Nat ArithmeticFunction

/-- For positive `x` and nonzero `y` we have that
$|L(\chi^0, x)^3 \cdot L(\chi, x+iy)^4 \cdot L(\chi^2, x+2iy)| \ge 1$. -/
lemma norm_LSeries_product_ge_one {N : â„•} (Ï‡ : DirichletCharacter â„‚ N) {x : â„} (hx : 0 < x)
    (y : â„) :
    â€–L â†—(1 : DirichletCharacter â„‚ N) (1 + x) ^ 3 * L â†—Ï‡ (1 + x + I * y) ^ 4 *
      L â†—(Ï‡ ^ 2 :) (1 + x + 2 * I * y)â€– â‰¥ 1 := by
  let Ï‡â‚€ := (1 : DirichletCharacter â„‚ N)
  have âŸ¨hâ‚€, hâ‚, hâ‚‚âŸ© := one_lt_re_of_pos y hx
  have hxâ‚ : 1 + (x : â„‚) = (1 + x : â„‚).re := by -- kills three goals of the `convert` below
    simp only [add_re, one_re, ofReal_re, ofReal_add, ofReal_one]
  have hsumâ‚€ :=
    (hasSum_re (summable_neg_log_one_sub_char_mul_prime_cpow Ï‡â‚€ hâ‚€).hasSum).summable.mul_left 3
  have hsumâ‚ :=
    (hasSum_re (summable_neg_log_one_sub_char_mul_prime_cpow Ï‡ hâ‚).hasSum).summable.mul_left 4
  have hsumâ‚‚ :=
    (hasSum_re (summable_neg_log_one_sub_char_mul_prime_cpow (Ï‡ ^ 2) hâ‚‚).hasSum).summable
  rw [â† LSeries_eulerProduct' _ hâ‚€, â† LSeries_eulerProduct' Ï‡ hâ‚,
    â† LSeries_eulerProduct' (Ï‡ ^ 2) hâ‚‚, â† exp_nat_mul, â† exp_nat_mul, â† exp_add, â† exp_add,
    norm_eq_abs, abs_exp]
  simp only [Nat.cast_ofNat, add_re, mul_re, re_ofNat, im_ofNat, zero_mul, sub_zero,
    Real.one_le_exp_iff]
  rw [re_tsum <| summable_neg_log_one_sub_char_mul_prime_cpow _ hâ‚€,
    re_tsum <| summable_neg_log_one_sub_char_mul_prime_cpow _ hâ‚,
    re_tsum <| summable_neg_log_one_sub_char_mul_prime_cpow _ hâ‚‚, â† tsum_mul_left, â† tsum_mul_left,
    â† tsum_add hsumâ‚€ hsumâ‚, â† tsum_add (hsumâ‚€.add hsumâ‚) hsumâ‚‚]
  convert tsum_nonneg fun p : Nat.Primes â†¦ Ï‡.re_log_comb_nonneg p.prop.two_le hâ‚€
  rw [sq, sq, MulChar.mul_apply]

variable {N : â„•} [NeZero N] {Ï‡ : DirichletCharacter â„‚ N}

/-- A variant of `DirichletCharacter.norm_LSeries_product_ge_one` in terms of the L-functions. -/
lemma norm_LFunction_product_ge_one {x : â„} (hx : 0 < x) (y : â„) :
    â€–LFunctionTrivChar N (1 + x) ^ 3 * Ï‡.LFunction (1 + x + I * y) ^ 4 *
      (Ï‡ ^ 2).LFunction (1 + x + 2 * I * y)â€– â‰¥ 1 := by
  convert norm_LSeries_product_ge_one Ï‡ hx y using 3
  Â· congr 2
    Â· refine DirichletCharacter.LFunction_eq_LSeries 1 ?_
      simp only [add_re, one_re, ofReal_re, lt_add_iff_pos_right, hx]
    Â· refine Ï‡.LFunction_eq_LSeries ?_
      simp only [add_re, one_re, ofReal_re, mul_re, I_re, zero_mul, I_im, ofReal_im, mul_zero,
        sub_self, add_zero, lt_add_iff_pos_right, hx]
  Â· refine (Ï‡ ^ 2).LFunction_eq_LSeries ?_
    simp only [add_re, one_re, ofReal_re, mul_re, re_ofNat, I_re, mul_zero, im_ofNat, I_im, mul_one,
      sub_self, zero_mul, mul_im, add_zero, ofReal_im, lt_add_iff_pos_right, hx]

lemma LFunction_triv_char_isBigO_near_one_horizontal :
    (fun x : â„ â†¦ LFunctionTrivChar N (1 + x)) =O[ğ“[>] 0] (fun x â†¦ (1 : â„‚) / x) := by
  have : (fun w : â„‚ â†¦ LFunctionTrivChar N (1 + w)) =O[ğ“[â‰ ] 0] (1 / Â·) := by
    have H : Tendsto (fun w â†¦ w * LFunctionTrivChar N (1 + w)) (ğ“[â‰ ] 0)
               (ğ“ <| âˆ p âˆˆ N.primeFactors, (1 - (p : â„‚)â»Â¹)) := by
      convert Tendsto.comp (f := fun w â†¦ 1 + w) (LFunctionTrivChar_residue_one (N := N)) ?_ using 1
      Â· ext w
        simp only [Function.comp_apply, add_sub_cancel_left]
      Â· refine tendsto_iff_comap.mpr <| map_le_iff_le_comap.mp <| Eq.le ?_
        convert map_punctured_nhds_eq (Homeomorph.addLeft (1 : â„‚)) 0 using 2 <;> simp
    exact ((isBigO_mul_iff_isBigO_div eventually_mem_nhdsWithin).mp <|
      Tendsto.isBigO_one â„‚ H).trans <| isBigO_refl ..
  exact (isBigO_comp_ofReal_nhds_ne this).mono <| nhds_right'_le_nhds_ne 0

lemma LFunction_isBigO_of_ne_one_horizontal {y : â„} (hy : y â‰  0 âˆ¨ Ï‡ â‰  1) :
    (fun x : â„ â†¦ Ï‡.LFunction (1 + x + I * y)) =O[ğ“[>] 0] (fun _ â†¦ (1 : â„‚)) := by
  refine Asymptotics.IsBigO.mono ?_ nhdsWithin_le_nhds
  have hy' : 1 + I * y â‰  1 âˆ¨ Ï‡ â‰  1:= by
    simpa only [ne_eq, add_right_eq_self, _root_.mul_eq_zero, I_ne_zero, ofReal_eq_zero,
      false_or] using hy
  convert isBigO_comp_ofReal
    (Ï‡.differentiableAt_LFunction _ hy').continuousAt.isBigO using 3 with x
  ring

lemma LFunction_isBigO_near_root_horizontal {y : â„} (hy : y â‰  0 âˆ¨ Ï‡ â‰  1)
    (h : Ï‡.LFunction (1 + I * y) = 0) :
    (fun x : â„ â†¦ Ï‡.LFunction (1 + x + I * y)) =O[ğ“[>] 0] fun x : â„ â†¦ (x : â„‚) := by
  have hy' : 1 + I * y â‰  1 âˆ¨ Ï‡ â‰  1:= by simp [hy]
  conv => enter [2, x]; rw [add_comm 1, add_assoc]
  refine (isBigO_comp_ofReal <| DifferentiableAt.isBigO_of_eq_zero ?_ h).mono
    nhdsWithin_le_nhds
  exact Ï‡.differentiableAt_LFunction (1 + I * â†‘y) hy'

/-- The L function of a Dirichlet character `Ï‡` does not vanish at `1 + I*t` if `t â‰  0`
or `Ï‡^2 â‰  1`. -/
theorem LFunction_ne_zero_of_re_eq_one_of_not_quadratic {t : â„} (h : Ï‡ ^ 2 â‰  1 âˆ¨ t â‰  0) :
    Ï‡.LFunction (1 + I * t) â‰  0 := by
  intro Hz
  have Hâ‚€ : (fun _ : â„ â†¦ (1 : â„)) =O[ğ“[>] 0]
      (fun x â†¦ LFunctionTrivChar N (1 + x) ^ 3 * Ï‡.LFunction (1 + x + I * t) ^ 4 *
                   (Ï‡ ^ 2).LFunction (1 + x + 2 * I * t)) :=
    IsBigO.of_bound' <| eventually_nhdsWithin_of_forall
      fun _ hx â†¦ (norm_one (Î± := â„)).symm â–¸ (norm_LFunction_product_ge_one hx t).le
  have hzâ‚ : t â‰  0 âˆ¨ Ï‡ â‰  1 := by
    rcases h with h | h
    Â· refine .inr ?_
      rintro rfl
      simp only [one_pow, ne_eq, not_true_eq_false] at h
    Â· exact .inl h
  have hzâ‚‚ : 2 * t â‰  0 âˆ¨ Ï‡ ^ 2 â‰  1 := by
    rcases h with h | h
    Â· exact .inr h
    Â· exact .inl <| mul_ne_zero two_ne_zero h
  have H := ((LFunction_triv_char_isBigO_near_one_horizontal (N := N)).pow 3).mul
    ((LFunction_isBigO_near_root_horizontal hzâ‚ Hz).pow 4)|>.mul <|
    LFunction_isBigO_of_ne_one_horizontal hzâ‚‚
  have help (x : â„) : ((1 / x) ^ 3 * x ^ 4 * 1 : â„‚) = x := by
    rcases eq_or_ne x 0 with rfl | h
    Â· rw [ofReal_zero, zero_pow (by norm_num), mul_zero, mul_one]
    Â· field_simp [h]
      ring
  conv at H => enter [3, x]; rw [help]
  conv at H =>
    enter [2, x]; rw [show 1 + x + I * â†‘(2 * t) = 1 + x + 2 * I * t by simp; ring]
  replace H := (Hâ‚€.trans H).norm_right
  simp only [norm_eq_abs, abs_ofReal] at H
  refine isLittleO_irrefl ?_ <| H.of_abs_right.trans_isLittleO <|
    isLittleO_id_one.mono nhdsWithin_le_nhds
  simp only [ne_eq, one_ne_zero, not_false_eq_true, frequently_true_iff_neBot]
  exact mem_closure_iff_nhdsWithin_neBot.mp <| closure_Ioi (0 : â„) â–¸ Set.left_mem_Ici

/-- If `Ï‡` is a Dirichlet character, then `L(Ï‡, 1 + I*t)` does not vanish for `t âˆˆ â„`
except when `Ï‡` is trivial and `t = 0` (then `L(Ï‡, s)` has a simple pole at `s = 1`). -/
theorem Lfunction_ne_zero_of_re_eq_one (Ï‡ : DirichletCharacter â„‚ N) (t : â„) (hÏ‡t : Ï‡ â‰  1 âˆ¨ t â‰  0) :
    LFunction Ï‡ (1 + I * t) â‰  0 := by
  by_cases h : Ï‡ ^ 2 = 1 âˆ§ t = 0
  Â· simp only [ne_eq, h.2, not_true_eq_false, or_false] at hÏ‡t
    rw [h.2, ofReal_zero, mul_zero, add_zero]
    exact LFunction_at_one_ne_zero_of_quadratic h.1 hÏ‡t
  Â· exact LFunction_ne_zero_of_re_eq_one_of_not_quadratic <| not_and_or.mp h

/-- If `Ï‡` is a Dirichlet character, then `L(Ï‡, s)` does not vanish for `s.re â‰¥ 1`
except when `Ï‡` is trivial and `s = 1` (then `L(Ï‡, s)` has a simple pole at `s = 1`). -/
theorem Lfunction_ne_zero_of_one_le_re (Ï‡ : DirichletCharacter â„‚ N) â¦ƒs : â„‚â¦„ (hÏ‡s : Ï‡ â‰  1 âˆ¨ s â‰  1)
    (hs : 1 â‰¤ s.re) :
    LFunction Ï‡ s â‰  0 := by
  rcases hs.eq_or_lt with hs | hs
  Â· have hseq : s = 1 + I * s.im := by
      rw [mul_comm I, â† re_add_im s, â† hs]
      push_cast
      simp only [add_im, one_im, mul_im, ofReal_re, I_im, mul_one, ofReal_im, I_re, mul_zero,
        add_zero, zero_add]
    rw [hseq]
    apply Lfunction_ne_zero_of_re_eq_one
    rcases hÏ‡s with h | h
    Â· exact .inl h
    Â· refine .inr ?_
      rw [â† re_add_im s, â† hs, ofReal_one] at h
      simpa only [ne_eq, add_right_eq_self, _root_.mul_eq_zero, ofReal_eq_zero, I_ne_zero,
        or_false] using h
  Â· exact LFunction_eq_LSeries Ï‡ hs â–¸ LSeries_ne_zero_of_one_lt_re Ï‡ hs

end DirichletCharacter

open DirichletCharacter in
/-- The Riemann Zeta Function does not vanish on the closed half-plane `re z â‰¥ 1`. -/
lemma riemannZeta_ne_zero_of_one_le_re â¦ƒz : â„‚â¦„ (hz : z â‰  1) (hz' : 1 â‰¤ z.re) : Î¶ z â‰  0 := by
  rw [â† LFunction_modOne_eq (Ï‡ := 1)]
  exact Lfunction_ne_zero_of_one_le_re (1 : DirichletCharacter â„‚ 1) (.inr hz) hz'

/-!
### The logarithmic derivative of the L-function of a trivial character has a simple pole at s = 1 with residue -1

We show that `s â†¦ L'(Ï‡) s / L(Ï‡) s + 1 / (s - 1)` (or rather, its negative, which is the function
we need for the Wiener-Ikehara Theorem) is continuous outside the zeros of `L(Ï‡)` when `Ï‡`
is a trivial Dirichlet character.
-/

namespace DirichletCharacter

section trivial

variable (n : â„•) [NeZero n]

/-- The function obtained by "multiplying away" the pole of `L(Ï‡)` for a trivial Dirichlet
character `Ï‡`. Its (negative) logarithmic derivative is used in the Wiener-Ikehara Theorem
to prove the Prime Number Theorem version of Dirichlet's Theorem on primes in arithmetic
progressions. -/
noncomputable def LFunction_triv_charâ‚ : â„‚ â†’ â„‚ :=
  Function.update (fun z â†¦ LFunctionTrivChar n z * (z - 1)) 1
    (âˆ p âˆˆ n.primeFactors, (1 - (p : â„‚)â»Â¹))

lemma LFunction_triv_charâ‚_apply_of_ne_one {z : â„‚} (hz : z â‰  1) :
    LFunction_triv_charâ‚ n z = LFunctionTrivChar n z * (z - 1) := by
  simp [LFunction_triv_charâ‚, hz]

lemma LFunction_triv_charâ‚_differentiable : Differentiable â„‚ (LFunction_triv_charâ‚ n) := by
  rw [â† differentiableOn_univ,
    â† differentiableOn_compl_singleton_and_continuousAt_iff (c := 1) Filter.univ_mem,
    LFunction_triv_charâ‚]
  refine âŸ¨DifferentiableOn.congr (f := fun z â†¦ LFunctionTrivChar n z * (z - 1))
    (fun z hz â†¦ DifferentiableAt.differentiableWithinAt ?_) fun _ hz â†¦ ?_,
    continuousWithinAt_compl_self.mp ?_âŸ©
  Â· simp only [Set.mem_diff, Set.mem_univ, Set.mem_singleton_iff, true_and] at hz
    exact (differentiableAt_LFunction _ z (.inl hz)).mul <| (differentiableAt_id').sub <|
      differentiableAt_const _
  Â· simp only [Set.mem_diff, Set.mem_univ, Set.mem_singleton_iff, true_and] at hz
    simp only [ne_eq, hz, not_false_eq_true, Function.update_noteq]
  Â· conv in (_ * _) => rw [mul_comm]
    simp only [continuousWithinAt_compl_self, continuousAt_update_same]
    exact LFunctionTrivChar_residue_one

lemma deriv_LFunction_triv_charâ‚_apply_of_ne_one  {z : â„‚} (hz : z â‰  1) :
    deriv (LFunction_triv_charâ‚ n) z =
      deriv (LFunctionTrivChar n) z * (z - 1) + LFunctionTrivChar n z := by
  have H : deriv (LFunction_triv_charâ‚ n) z =
      deriv (fun w â†¦ LFunctionTrivChar n w * (w - 1)) z := by
    refine Filter.EventuallyEq.deriv_eq <| Filter.eventuallyEq_iff_exists_mem.mpr ?_
    refine âŸ¨{w | w â‰  1}, IsOpen.mem_nhds isOpen_ne hz, fun w hw â†¦ ?_âŸ©
    simp only [LFunction_triv_charâ‚, ne_eq, Set.mem_setOf.mp hw, not_false_eq_true,
      Function.update_noteq]
  rw [H, deriv_mul (differentiableAt_LFunction _ z (.inl hz)) <| differentiableAt_id'.sub <|
    differentiableAt_const 1, deriv_sub_const, deriv_id'', mul_one]

lemma neg_logDeriv_LFunction_triv_charâ‚_eq {z : â„‚} (hzâ‚ : z â‰  1)
    (hzâ‚‚ : LFunctionTrivChar n z â‰  0) :
    -deriv (LFunction_triv_charâ‚ n) z / LFunction_triv_charâ‚ n z =
      -deriv (LFunctionTrivChar n) z / LFunctionTrivChar n z - 1 / (z - 1) := by
  rw [deriv_LFunction_triv_charâ‚_apply_of_ne_one n hzâ‚, LFunction_triv_charâ‚_apply_of_ne_one n hzâ‚]
  field_simp [sub_ne_zero.mpr hzâ‚]
  ring

lemma continuousOn_neg_logDeriv_LFunction_triv_charâ‚ :
    ContinuousOn (fun z â†¦ -deriv (LFunction_triv_charâ‚ n) z / LFunction_triv_charâ‚ n z)
      {z | z = 1 âˆ¨ LFunctionTrivChar n z â‰  0} := by
  simp_rw [neg_div]
  refine (((LFunction_triv_charâ‚_differentiable n).contDiff.continuous_deriv le_rfl).continuousOn.div
    (LFunction_triv_charâ‚_differentiable n).continuous.continuousOn fun w hw â†¦ ?_).neg
  rcases eq_or_ne w 1 with rfl | hw'
  Â· simp only [LFunction_triv_charâ‚, Function.update_same]
    refine Finset.prod_ne_zero_iff.mpr fun p hp â†¦ ?_
    rw [sub_ne_zero, ne_eq, one_eq_inv]
    exact_mod_cast (Nat.prime_of_mem_primeFactors hp).ne_one
  Â· simp only [ne_eq, Set.mem_setOf_eq, hw', false_or] at hw
    simp only [LFunction_triv_charâ‚, ne_eq, hw', not_false_eq_true, Function.update_noteq, _root_.mul_eq_zero, hw,
      false_or]
    exact sub_ne_zero.mpr hw'

lemma eq_one_or_LFunction_triv_char_ne_zero_of_one_le_re :
    {s : â„‚ | 1 â‰¤ s.re} âŠ† {s | s = 1 âˆ¨ LFunction (1 : DirichletCharacter â„‚ n) s â‰  0} := by
  intro s hs
  simp only [Set.mem_setOf_eq, ne_eq] at hs âŠ¢
  have := Lfunction_ne_zero_of_one_le_re (1 : DirichletCharacter â„‚ n) (s := s)
  tauto

end trivial

section nontrivial

variable {n : â„•} [NeZero n] {Ï‡ : DirichletCharacter â„‚ n}

/-- The negative logarithmic derivative of a Dirichlet L-function is continuous away from
the zeros of the L-function. -/
lemma continuousOn_neg_logDeriv_LFunction_nontriv_char (hÏ‡ : Ï‡ â‰  1) :
    ContinuousOn (fun z â†¦ -deriv (LFunction Ï‡) z / LFunction Ï‡ z)
      {z | LFunction Ï‡ z â‰  0} := by
  simp_rw [neg_div]
  have hâ‚ := differentiable_LFunction hÏ‡
  exact ((hâ‚.contDiff.continuous_deriv le_rfl).continuousOn.div
    hâ‚.continuous.continuousOn fun w hw â†¦ hw).neg

lemma LFunction_nontriv_char_ne_zero_of_one_le_re (hÏ‡ : Ï‡ â‰  1) :
    {s : â„‚ | 1 â‰¤ s.re} âŠ† {s | LFunction Ï‡ s â‰  0} := by
  intro s hs
  simp only [Set.mem_setOf_eq, ne_eq] at hs âŠ¢
  have := Lfunction_ne_zero_of_one_le_re Ï‡ (s := s)
  tauto

end nontrivial

end DirichletCharacter

section zeta

/-!
### The logarithmic derivative of Î¶ has a simple pole at s = 1 with residue -1

We show that `s â†¦ Î¶' s / Î¶ s + 1 / (s - 1)` (or rather, its negative, which is the function
we need for the Wiener-Ikehara Theorem) is continuous outside the zeros of `Î¶`.
-/

/-- The function obtained by "multiplying away" the pole of `Î¶`. Its (negative) logarithmic
derivative is the function used in the Wiener-Ikehara Theorem to prove the Prime Number
Theorem. -/
noncomputable def Î¶â‚ : â„‚ â†’ â„‚ := Function.update (fun z â†¦ Î¶ z * (z - 1)) 1 1

open DirichletCharacter

lemma riemannZeta_eq_LFunction_triv_char_one : Î¶ = LFunctionTrivChar 1 :=
  LFunction_modOne_eq.symm

lemma Î¶â‚_eq_LFunction_triv_charâ‚_one : Î¶â‚ = LFunction_triv_charâ‚ 1 := by
  ext1
  simp only [Î¶â‚, LFunction_triv_charâ‚, LFunction_modOne_eq, Nat.primeFactors_one,
    Finset.prod_empty]

lemma neg_logDeriv_Î¶â‚_eq {z : â„‚} (hzâ‚ : z â‰  1) (hzâ‚‚ : Î¶ z â‰  0) :
    -deriv Î¶â‚ z / Î¶â‚ z = -deriv Î¶ z / Î¶ z - 1 / (z - 1) := by
  simp only [Î¶â‚_eq_LFunction_triv_charâ‚_one, riemannZeta_eq_LFunction_triv_char_one] at hzâ‚‚ âŠ¢
  exact neg_logDeriv_LFunction_triv_charâ‚_eq 1 hzâ‚ hzâ‚‚

lemma continuousOn_neg_logDeriv_Î¶â‚ :
    ContinuousOn (fun z â†¦ -deriv Î¶â‚ z / Î¶â‚ z) {z | z = 1 âˆ¨ Î¶ z â‰  0} := by
  simp only [Î¶â‚_eq_LFunction_triv_charâ‚_one, riemannZeta_eq_LFunction_triv_char_one]
  exact continuousOn_neg_logDeriv_LFunction_triv_charâ‚ 1

end zeta

/-!
### Proof of Lemma 9

We prove Lemma 9 of
[Section 2 in the PNT+ Project](https://alexkontorovich.github.io/PrimeNumberTheoremAnd/web/sect0002.html).
-/

section arith_prog

open scoped ArithmeticFunction.vonMangoldt
open DirichletCharacter

variable {q : â„•} [NeZero q] {a : ZMod q}

/-- Lemma 9 of Section 2 of PNT+: The L-series of the von Mangoldt function restricted to the
prime residue class `a` mod `q` as a linear combination of logarithmic derivatives of
L functions of the Dirichlet characters mod `q`. -/
lemma WeakPNT_character (ha : IsUnit a) {s : â„‚} (hs : 1 < s.re) :
     LSeries ({n : â„• | (n : ZMod q) = a}.indicator â†—Î›) s =
      -(q.totient : â„‚)â»Â¹ * âˆ‘ Ï‡ : DirichletCharacter â„‚ q, Ï‡ aâ»Â¹ *
        (deriv (LFunction Ï‡) s / LFunction Ï‡ s) := by
  simp only [deriv_LFunction_eq_deriv_LSeries _ hs, LFunction_eq_LSeries _ hs, neg_mul, â† mul_neg, â†
    Finset.sum_neg_distrib, â† neg_div, â† LSeries_twist_vonMangoldt_eq _ hs]
  rw [eq_inv_mul_iff_mul_eqâ‚€ <| mod_cast (Nat.totient_pos.mpr q.pos_of_neZero).ne']
  simp only [â† LSeries_smul]
  classical
  rw [â† LSeries_sum' <| fun Ï‡ â†¦ (LSeriesSummable_twist_vonMangoldt Ï‡ hs).smul _]
  refine LSeries_congr s fun {n} _ â†¦ ?_
  simp only [Pi.smul_apply, smul_eq_mul, Finset.sum_apply, Pi.mul_apply, Set.indicator_apply]
  conv_lhs => rw [â† one_mul (Î› n : â„‚), â† zero_mul (Î› n : â„‚), â† ite_mul]
  simp only [â† mul_assoc, â† Finset.sum_mul, mul_ite, mul_one, mul_zero, Set.mem_setOf_eq]
  congrm (?_ * (Î› n : â„‚))
  simpa only [Nat.cast_ite, Nat.cast_zero, eq_comm (a := a)]
    using (sum_char_inv_mul_char_eq â„‚ ha n).symm

variable (q a) in
open Classical in
/-- The function `F` used in the Wiener-Ikehara Theorem to prove Dirichlet's Theorem. -/
noncomputable
def weakDirichlet_auxFun (s : â„‚) : â„‚ :=
  (q.totient : â„‚)â»Â¹ * (-deriv (LFunction_triv_charâ‚ q) s / LFunction_triv_charâ‚ q s -
    âˆ‘ Ï‡ âˆˆ ({1}á¶œ : Finset (DirichletCharacter â„‚ q)), Ï‡ aâ»Â¹ * deriv (LFunction Ï‡) s / LFunction Ï‡ s)

lemma weakDirichlet_auxFun_prop (ha : IsUnit a) :
    Set.EqOn (weakDirichlet_auxFun q a)
      (fun s â†¦ LSeries ({n : â„• | (n : ZMod q) = a}.indicator â†—Î›) s - (q.totient : â„‚)â»Â¹ / (s - 1))
      {s | 1 < s.re} := by
  classical
  intro s hs
  simp only [Set.mem_setOf_eq] at hs
  simp only [WeakPNT_character ha hs]
  rw [weakDirichlet_auxFun, neg_div, â† neg_add', mul_neg, â† neg_mul,
    div_eq_mul_one_div (q.totient : â„‚)â»Â¹, sub_eq_add_neg, â† neg_mul, â† mul_add]
  congrm (_ * ?_)
  -- this should be easier, but `IsUnit.inv ha` does not work here
  have ha' : IsUnit aâ»Â¹ := isUnit_of_dvd_one âŸ¨a, (ZMod.inv_mul_of_unit a ha).symmâŸ©
  rw [Fintype.sum_eq_add_sum_compl 1, MulChar.one_apply ha', one_mul, add_right_comm]
  simp only [mul_div_assoc]
  congrm (?_ + _)
  have hsâ‚ : s â‰  1 := by
    rintro rfl
    simp only [one_re, lt_self_iff_false] at hs
  rw [deriv_LFunction_triv_charâ‚_apply_of_ne_one _ hsâ‚, LFunction_triv_charâ‚_apply_of_ne_one _ hsâ‚]
  simp only [LFunctionTrivChar]
  rw [add_div, mul_div_mul_right _ _ (sub_ne_zero_of_ne hsâ‚)]
  conv_lhs => enter [2, 1]; rw [â† mul_one (LFunction ..)]
  rw [mul_div_mul_left _ _ <| Lfunction_ne_zero_of_one_le_re 1 (.inr hsâ‚) hs.le]

/-- (A version of) Proposition 2 of Section 2 of PNT+: the L-series of the von Mangoldt function
restricted to the prime residue class `a` mod `q` is continuous on `s.re â‰¥ 1` except
for a single pole at `s = 1` with residue `(q.totient)â»Â¹`.-/
lemma continuousOn_weakDirichlet_auxFun :
    ContinuousOn (weakDirichlet_auxFun q a) {s | 1 â‰¤ s.re} := by
  rw [show weakDirichlet_auxFun q a = fun s â†¦ _ from rfl]
  simp only [weakDirichlet_auxFun, sub_eq_add_neg]
  refine continuousOn_const.mul <| ContinuousOn.add ?_ ?_
  Â· exact ContinuousOn.mono (continuousOn_neg_logDeriv_LFunction_triv_charâ‚ q)
      (eq_one_or_LFunction_triv_char_ne_zero_of_one_le_re q)
  Â· simp only [â† Finset.sum_neg_distrib, mul_div_assoc, â† mul_neg, â† neg_div]
    refine continuousOn_finset_sum _ fun Ï‡ hÏ‡ â†¦ continuousOn_const.mul ?_
    replace hÏ‡ : Ï‡ â‰  1 := by simpa only [ne_eq, Finset.mem_compl, Finset.mem_singleton] using hÏ‡
    exact ContinuousOn.mono (continuousOn_neg_logDeriv_LFunction_nontriv_char hÏ‡)
      (LFunction_nontriv_char_ne_zero_of_one_le_re hÏ‡)

end arith_prog

/-!
### Derivation of the Prime Number Theorem and Dirichlet's Theorem from the Wiener-Ikehara Theorem
-/

open Filter ArithmeticFunction Topology

/- /-- The *Wiener-Ikehara Theorem* implies the *Prime Number Theorem* in the form that
`Ïˆ x âˆ¼ x`, where `Ïˆ x = âˆ‘ n < x, Î› n` and `Î›` is the von Mangoldt function. -/
theorem PNT_vonMangoldt' (WIT : WienerIkeharaTheorem) :
    Tendsto (fun N : â„• â†¦ ((Finset.range N).sum Î›) / N) atTop (ğ“ 1) := by
  have hnv := riemannZeta_ne_zero_of_one_le_re
  refine WIT (F := fun z â†¦ -deriv Î¶â‚ z / Î¶â‚ z) (fun _ â†¦ vonMangoldt_nonneg) (fun s hs â†¦ ?_) ?_
  Â· have hsâ‚ : s â‰  1 := by
      rintro rfl
      simp at hs
    simp only [ne_eq, hsâ‚, not_false_eq_true, LSeries_vonMangoldt_eq_deriv_riemannZeta_div hs,
      ofReal_one]
    exact neg_logDeriv_Î¶â‚_eq hsâ‚ <| hnv hsâ‚ (Set.mem_setOf.mp hs).le
  Â· refine continuousOn_neg_logDeriv_Î¶â‚.mono fun s _ â†¦ ?_
    specialize @hnv s
    simp at *
    tauto -/

/--  The *Wiener-Ikehara Theorem* implies *Dirichlet's Theorem* in the form that
`Ïˆ x âˆ¼ q.totientâ»Â¹ * x`, where `Ïˆ x = âˆ‘ n < x âˆ§ n â‰¡ a mod q, Î› n`
and `Î›` is the von Mangoldt function.

This is Theorem 2 in Section 2 of PNT+ (but using the `WIT` stub defined here). -/
theorem Dirichlet_vonMangoldt (WIT : WienerIkeharaTheorem) {q : â„•} [NeZero q] {a : ZMod q}
    (ha : IsUnit a) :
    Tendsto (fun N : â„• â†¦ (((Finset.range N).filter (fun n : â„• â†¦ (n : ZMod q) = a)).sum Î›) / N)
      atTop (ğ“ <| (q.totient : â„)â»Â¹) := by
  classical
  have H N : ((Finset.range N).filter (fun n : â„• â†¦ (n : ZMod q) = a)).sum Î› =
      (Finset.range N).sum ({n : â„• | (n : ZMod q) = a}.indicator Î›) :=
    (Finset.sum_indicator_eq_sum_filter _ _ (fun _ â†¦ {n : â„• | n = a}) _).symm
  simp only [H]
  refine WIT (F := weakDirichlet_auxFun q a) (fun n â†¦ ?_) ?_ ?_
  Â· exact Set.indicator_apply_nonneg fun _ â†¦ vonMangoldt_nonneg
  Â· convert weakDirichlet_auxFun_prop ha with s n
    Â· by_cases hn : n = a
      Â· simp only [Set.mem_setOf_eq, hn, Set.indicator_of_mem]
      Â· simp only [Set.mem_setOf_eq, hn, not_false_eq_true, Set.indicator_of_not_mem, ofReal_zero]
    Â· rw [ofReal_inv, ofReal_natCast]
  Â· exact continuousOn_weakDirichlet_auxFun

/-- The *Wiener-Ikehara Theorem* implies the *Prime Number Theorem* in the form that
`Ïˆ x âˆ¼ x`, where `Ïˆ x = âˆ‘ n < x, Î› n` and `Î›` is the von Mangoldt function. -/
theorem PNT_vonMangoldt (WIT : WienerIkeharaTheorem) :
    Tendsto (fun N : â„• â†¦ ((Finset.range N).sum Î›) / N) atTop (ğ“ 1) := by
  convert Dirichlet_vonMangoldt WIT (q := 1) (a := 1) isUnit_one with n
  Â· exact (Finset.filter_true_of_mem fun _ _ â†¦ Subsingleton.eq_one _).symm
  Â· simp only [Nat.totient_one, Nat.cast_one, inv_one]
