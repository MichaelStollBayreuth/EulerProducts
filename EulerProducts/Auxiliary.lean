import Mathlib.Analysis.Complex.TaylorSeries
import Mathlib.Data.Real.StarOrdered
import Mathlib.NumberTheory.ArithmeticFunction
import Mathlib.NumberTheory.LSeries.Deriv

/-!
### Auxiliary lemmas
-/

-- not really needed here

namespace Complex

lemma summable_re {Î± : Type _} {f : Î± â†’ â„‚} (h : Summable f) : Summable fun x â†¦ (f x).re :=
  (Complex.hasSum_re h.hasSum).summable

lemma summable_im {Î± : Type _} {f : Î± â†’ â„‚} (h : Summable f) : Summable fun x â†¦ (f x).im :=
  (Complex.hasSum_im h.hasSum).summable

-- #find_home summable_re -- [Mathlib.Analysis.Complex.Basic]

open scoped ComplexOrder

lemma inv_natCast_pow_ofReal_pos {n : â„•} (hn : n â‰  0) (x : â„) : 0 < ((n : â„‚) ^ (x : â„‚))â»Â¹ := by
  refine RCLike.inv_pos_of_pos ?_
  rw [show (n : â„‚) ^ (x : â„‚) = (n : â„) ^ (x : â„‚) from rfl, â† ofReal_cpow n.cast_nonneg']
  positivity

end Complex

namespace ArithmeticFunction

open Complex

open scoped ComplexOrder

/-- If all values of a `â„‚`-valued arithmetic function are nonnegative reals and `x` is a
real number in the domain of absolute convergence, then the `n`th iterated derivative
of the associated L-series is nonnegative real when `n` is even and nonpositive real
when `n` is odd. -/
lemma iteratedDeriv_LSeries_alternating (a : ArithmeticFunction â„‚)
    (hn : âˆ€ n, 0 â‰¤ a n) {x : â„} (h : LSeries.abscissaOfAbsConv (a Â·) < x) (n : â„•) :
    0 â‰¤ (-1) ^ n * iteratedDeriv n (LSeries (a Â·)) x := by
  rw [LSeries_iteratedDeriv _ h, LSeries, â† mul_assoc, â† pow_add, Even.neg_one_pow âŸ¨n, rflâŸ©,
    one_mul]
  refine tsum_nonneg fun k â†¦ ?_
  rw [LSeries.term_def]
  split
  Â· exact le_rfl
  Â· refine mul_nonneg ?_ <| (inv_natCast_pow_ofReal_pos (by assumption) x).le
    induction n with
    | zero => simp only [Function.iterate_zero, id_eq]; exact hn k
    | succ n IH =>
        rw [Function.iterate_succ_apply']
        refine mul_nonneg ?_ IH
        simp only [â† natCast_log, zero_le_real, Real.log_natCast_nonneg]

end ArithmeticFunction


section Topology

open Filter

namespace Asymptotics



end Asymptotics


open Topology Asymptotics

lemma DifferentiableAt.isBigO_of_eq_zero {f : â„‚ â†’ â„‚} {z : â„‚} (hf : DifferentiableAt â„‚ f z)
    (hz : f z = 0) : (fun w â†¦ f (w + z)) =O[ğ“ 0] id := by
  rw [â† zero_add z] at hf
  simpa only [zero_add, hz, sub_zero]
    using (hf.hasDerivAt.comp_add_const 0 z).differentiableAt.isBigO_sub

lemma ContinuousAt.isBigO {f : â„‚ â†’ â„‚} {z : â„‚} (hf : ContinuousAt f z) :
    (fun w â†¦ f (w + z)) =O[ğ“ 0] (fun _ â†¦ (1 : â„‚)) := by
  rw [isBigO_iff']
  replace hf : ContinuousAt (fun w â†¦ f (w + z)) 0 := by
    convert (Homeomorph.comp_continuousAt_iff' (Homeomorph.addLeft (-z)) _ z).mp ?_
    Â· simp
    Â· simp [Function.comp_def, hf]
  simp_rw [Metric.continuousAt_iff', dist_eq_norm_sub, zero_add] at hf
  specialize hf 1 zero_lt_one
  refine âŸ¨â€–f zâ€– + 1, by positivity, ?_âŸ©
  refine Eventually.mp hf <| Eventually.of_forall fun w hw â†¦ le_of_lt ?_
  calc â€–f (w + z)â€–
    _ â‰¤ â€–f zâ€– + â€–f (w + z) - f zâ€– := norm_le_insert' ..
    _ < â€–f zâ€– + 1 := add_lt_add_left hw _
    _ = _ := by simp only [norm_one, mul_one]

lemma Complex.isBigO_comp_ofReal {f g : â„‚ â†’ â„‚} {x : â„} (h : f =O[ğ“ (x : â„‚)] g) :
    (fun y : â„ â†¦ f y) =O[ğ“ x] (fun y : â„ â†¦ g y) :=
  Asymptotics.IsBigO.comp_tendsto (k := fun y : â„ â†¦ (y : â„‚)) h <|
    Continuous.tendsto Complex.continuous_ofReal x

lemma Complex.isBigO_comp_ofReal_nhds_ne {f g : â„‚ â†’ â„‚} {x : â„} (h : f =O[ğ“[â‰ ] (x : â„‚)] g) :
    (fun y : â„ â†¦ f y) =O[ğ“[â‰ ] x] (fun y : â„ â†¦ g y) :=
  Asymptotics.IsBigO.comp_tendsto (k := fun y : â„ â†¦ (y : â„‚)) h <|
    ((hasDerivAt_id (x : â„‚)).comp_ofReal).tendsto_punctured_nhds one_ne_zero

end Topology

namespace Complex
-- see https://leanprover.zulipchat.com/#narrow/stream/217875-Is-there-code-for-X.3F/topic/Differentiability.20of.20the.20natural.20map.20.E2.84.9D.20.E2.86.92.20.E2.84.82/near/418095234

lemma hasDerivAt_ofReal (x : â„) : HasDerivAt ofReal 1 x :=
  HasDerivAt.ofReal_comp <| hasDerivAt_id x

lemma deriv_ofReal (x : â„) : deriv ofReal x = 1 :=
  (hasDerivAt_ofReal x).deriv

lemma differentiableAt_ofReal (x : â„) : DifferentiableAt â„ ofReal x :=
  (hasDerivAt_ofReal x).differentiableAt

lemma differentiable_ofReal : Differentiable â„ ofReal :=
  ofRealCLM.differentiable

-- #find_home hasDerivAt_ofReal -- [Mathlib.Analysis.SpecialFunctions.NonIntegrable]
-- Mathlib.Analysis.Complex.RealDeriv ?

end Complex

lemma DifferentiableAt.comp_ofReal {e : â„‚ â†’ â„‚} {z : â„} (hf : DifferentiableAt â„‚ e z) :
    DifferentiableAt â„ (fun x : â„ â†¦ e x) z :=
  hf.hasDerivAt.comp_ofReal.differentiableAt

lemma deriv.comp_ofReal {e : â„‚ â†’ â„‚} {z : â„} (hf : DifferentiableAt â„‚ e z) :
    deriv (fun x : â„ â†¦ e x) z = deriv e z :=
  hf.hasDerivAt.comp_ofReal.deriv

lemma Differentiable.comp_ofReal {e : â„‚ â†’ â„‚} (h : Differentiable â„‚ e) :
    Differentiable â„ (fun x : â„ â†¦ e x) :=
  fun _ â†¦ h.differentiableAt.comp_ofReal

lemma DifferentiableAt.ofReal_comp {z : â„} {f : â„ â†’ â„} (hf : DifferentiableAt â„ f z) :
    DifferentiableAt â„ (fun (y : â„) â†¦ (f y : â„‚)) z :=
  hf.hasDerivAt.ofReal_comp.differentiableAt

lemma Differentiable.ofReal_comp {f : â„ â†’ â„} (hf : Differentiable â„ f) :
    Differentiable â„ (fun (y : â„) â†¦ (f y : â„‚)) :=
  fun _ â†¦ hf.differentiableAt.ofReal_comp

open Complex ContinuousLinearMap in
lemma HasDerivAt.of_hasDerivAt_ofReal_comp {z : â„} {f : â„ â†’ â„} {u : â„‚}
    (hf : HasDerivAt (fun y â†¦ (f y : â„‚)) u z) :
    âˆƒ u' : â„, u = u' âˆ§ HasDerivAt f u' z := by
  lift u to â„
  Â· have H := (imCLM.hasFDerivAt.comp z hf.hasFDerivAt).hasDerivAt.deriv
    simp only [Function.comp_def, imCLM_apply, ofReal_im, deriv_const] at H
    rwa [eq_comm, comp_apply, imCLM_apply, smulRight_apply, one_apply, one_smul] at H
  refine âŸ¨u, rfl, ?_âŸ©
  convert (reCLM.hasFDerivAt.comp z hf.hasFDerivAt).hasDerivAt
  rw [comp_apply, smulRight_apply, one_apply, one_smul, reCLM_apply, ofReal_re]

lemma DifferentiableAt.ofReal_comp_iff {z : â„} {f : â„ â†’ â„} :
    DifferentiableAt â„ (fun (y : â„) â†¦ (f y : â„‚)) z â†” DifferentiableAt â„ f z := by
  refine âŸ¨fun H â†¦ ?_, ofReal_compâŸ©
  obtain âŸ¨u, _, huâ‚‚âŸ© := H.hasDerivAt.of_hasDerivAt_ofReal_comp
  exact HasDerivAt.differentiableAt huâ‚‚

lemma Differentiable.ofReal_comp_iff {f : â„ â†’ â„} :
    Differentiable â„ (fun (y : â„) â†¦ (f y : â„‚)) â†” Differentiable â„ f :=
  forall_congr' fun _ â†¦ DifferentiableAt.ofReal_comp_iff

lemma deriv.ofReal_comp {z : â„} {f : â„ â†’ â„} :
    deriv (fun (y : â„) â†¦ (f y : â„‚)) z = deriv f z := by
  by_cases hf : DifferentiableAt â„ f z
  Â· exact hf.hasDerivAt.ofReal_comp.deriv
  Â· have hf' := mt DifferentiableAt.ofReal_comp_iff.mp hf
    rw [deriv_zero_of_not_differentiableAt hf, deriv_zero_of_not_differentiableAt hf',
      Complex.ofReal_zero]

section iteratedDeriv

variable {ğ•œ F} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup F] [NormedSpace ğ•œ F]

-- the lemmas in this section should go to Mathlib.Analysis.Calculus.Deriv.Shift
lemma iteratedDeriv_comp_const_add (n : â„•) (f : ğ•œ â†’ F) (s : ğ•œ) :
    iteratedDeriv n (fun z â†¦ f (s + z)) = fun t â†¦ iteratedDeriv n f (s + t) := by
  induction n with
  | zero => simp only [iteratedDeriv_zero]
  | succ n IH =>
      simp only [iteratedDeriv_succ, IH]
      ext1 z
      exact deriv_comp_const_add (iteratedDeriv n f) s z

lemma iteratedDeriv_comp_add_const (n : â„•) (f : ğ•œ â†’ F) (s : ğ•œ) :
    iteratedDeriv n (fun z â†¦ f (z + s)) = fun t â†¦ iteratedDeriv n f (t + s) := by
  induction n with
  | zero => simp only [iteratedDeriv_zero]
  | succ n IH =>
      simp only [iteratedDeriv_succ, IH]
      ext1 z
      exact deriv_comp_add_const (iteratedDeriv n f) s z

lemma iteratedDeriv_eq_on_open (n : â„•) {f g : ğ•œ â†’ F} {s : Set ğ•œ} (hs : IsOpen s) (x : s)
    (hfg : Set.EqOn f g s) : iteratedDeriv n f x = iteratedDeriv n g x := by
  induction' n with n IH generalizing f g
  Â· simpa only [iteratedDeriv_zero] using hfg x.2
  Â· simp only [iteratedDeriv_succ']
    exact IH fun y hy â†¦ Filter.EventuallyEq.deriv_eq <|
      Filter.eventuallyEq_iff_exists_mem.mpr âŸ¨s, IsOpen.mem_nhds hs hy, hfgâŸ©

end iteratedDeriv

namespace Complex

open Nat

/-- A function that is complex differentiable on the closed ball of radius `r` around `c`,
where `c` is real, and all whose iterated derivatives at `c` are real can be give by a real
differentiable function on the real open interval from `c-r` to `c+r`. -/
lemma realValued_of_iteratedDeriv_real_on_ball {f : â„‚ â†’ â„‚} â¦ƒr : â„â¦„ {c : â„}
    (hf : DifferentiableOn â„‚ f (Metric.ball (c : â„‚) r)) â¦ƒD : â„• â†’ â„â¦„
    (hd : âˆ€ n, iteratedDeriv n f c = D n) :
    âˆƒ F : â„ â†’ â„, DifferentiableOn â„ F (Set.Ioo (c - r) (c + r)) âˆ§
      Set.EqOn (f âˆ˜ ofReal) (ofReal âˆ˜ F) (Set.Ioo (c - r) (c + r)) := by
  have Hz : âˆ€ x âˆˆ Set.Ioo (c - r) (c + r), (x : â„‚) âˆˆ Metric.ball (c : â„‚) r := by
    intro x hx
    refine Metric.mem_ball.mpr ?_
    rw [dist_eq, â† ofReal_sub, abs_ofReal, abs_sub_lt_iff, sub_lt_iff_lt_add', sub_lt_comm]
    exact and_comm.mpr hx
  have H â¦ƒz : â„‚â¦„ (hz : z âˆˆ Metric.ball (c : â„‚) r) := taylorSeries_eq_on_ball' hz hf
  refine âŸ¨fun x â†¦ âˆ‘' (n : â„•), (â†‘n !)â»Â¹ * (D n) * (x - c) ^ n, fun x hx â†¦ ?_, fun x hx â†¦ ?_âŸ©
  Â· have Hx := Hz _ hx
    refine DifferentiableAt.differentiableWithinAt ?_
    replace hf := ((hf x Hx).congr (fun _ hz â†¦ H hz) (H Hx)).differentiableAt
      (Metric.isOpen_ball.mem_nhds Hx) |>.comp_ofReal
    simp_rw [hd, â† ofReal_sub, â† ofReal_natCast, â† ofReal_inv, â† ofReal_pow, â† ofReal_mul,
      â† ofReal_tsum] at hf
    exact DifferentiableAt.ofReal_comp_iff.mp hf
  Â· simp only [Function.comp_apply, â† H (Hz _ hx), hd, ofReal_tsum]
    push_cast
    rfl

/-- A function that is complex differentiable on the complex plane and all whose iterated
derivatives at a real point `c` are real can be given by a real differentiable function
on the real line. -/
lemma realValued_of_iteratedDeriv_real {f : â„‚ â†’ â„‚} (hf : Differentiable â„‚ f) {c : â„} {D : â„• â†’ â„}
    (hd : âˆ€ n, iteratedDeriv n f c = D n) :
    âˆƒ F : â„ â†’ â„, Differentiable â„ F âˆ§ (f âˆ˜ ofReal) = (ofReal âˆ˜ F) := by
  have H (z : â„‚) := taylorSeries_eq_of_entire' c z hf
  simp_rw [hd] at H
  refine âŸ¨fun x â†¦ âˆ‘' (n : â„•), (â†‘n !)â»Â¹ * (D n) * (x - c) ^ n, ?_, ?_âŸ©
  Â· have := hf.comp_ofReal
    simp_rw [â† H, â† ofReal_sub, â† ofReal_natCast, â† ofReal_inv, â† ofReal_pow, â† ofReal_mul,
      â† ofReal_tsum] at this
    exact Differentiable.ofReal_comp_iff.mp this
  Â· ext x
    simp only [Function.comp_apply, â† H, ofReal_tsum, ofReal_mul, ofReal_inv, ofReal_natCast,
      ofReal_pow, ofReal_sub]


open scoped ComplexOrder

/-- An entire function whose iterated derivatives at zero are all nonnegative real has nonnegative
real values for nonnegative real arguments. -/
theorem nonneg_of_iteratedDeriv_nonneg {f : â„‚ â†’ â„‚} (hf : Differentiable â„‚ f)
    (h : âˆ€ n, 0 â‰¤ iteratedDeriv n f 0) â¦ƒz : â„‚â¦„ (hz : 0 â‰¤ z) : 0 â‰¤ f z := by
  have H := taylorSeries_eq_of_entire' 0 z hf
  have hz' := eq_re_of_ofReal_le hz
  rw [hz'] at hz H âŠ¢
  obtain âŸ¨D, hDâŸ© : âˆƒ D : â„• â†’ â„, âˆ€ n, 0 â‰¤ D n âˆ§ iteratedDeriv n f 0 = D n := by
    refine âŸ¨fun n â†¦ (iteratedDeriv n f 0).re, fun n â†¦ âŸ¨?_, ?_âŸ©âŸ©
    Â· have := eq_re_of_ofReal_le (h n) â–¸ h n
      norm_cast at this
    Â· rw [eq_re_of_ofReal_le (h n)]
  simp_rw [â† H, hD, â† ofReal_natCast, sub_zero, â† ofReal_pow, â† ofReal_inv, â† ofReal_mul,
    â† ofReal_tsum]
  norm_cast
  refine tsum_nonneg fun n â†¦ ?_
  norm_cast at hz
  have := (hD n).1
  positivity

/-- An entire function whose iterated derivatives at zero are all nonnegative real is
monotonic on the nonnegative real axis. -/
theorem monotoneOn_of_iteratedDeriv_nonneg {f : â„‚ â†’ â„‚} (hf : Differentiable â„‚ f)
    (h : âˆ€ n, 0 â‰¤ iteratedDeriv n f 0) : MonotoneOn (f âˆ˜ ofReal) (Set.Ici (0 : â„)) := by
  let D : â„• â†’ â„ := fun n â†¦ (iteratedDeriv n f 0).re
  have hD (n : â„•) : iteratedDeriv n f 0 = D n := by
    refine Complex.ext rfl ?_
    simp only [ofReal_im]
    exact (le_def.mp (h n)).2.symm
  obtain âŸ¨F, hFd, hFâŸ© := realValued_of_iteratedDeriv_real hf hD
  rw [hF]
  refine monotone_ofReal.comp_monotoneOn <| monotoneOn_of_deriv_nonneg (convex_Ici 0)
    hFd.continuous.continuousOn hFd.differentiableOn fun x hx â†¦ ?_
  have hD' (n : â„•) : 0 â‰¤ iteratedDeriv n (deriv f) 0 := by
    rw [â† iteratedDeriv_succ']
    exact h (n + 1)
  have hf' := (contDiff_succ_iff_deriv.mp <| hf.contDiff (n := 2)).2.differentiable rfl.le
  have hx : (0 : â„‚) â‰¤ x := by
    norm_cast
    simp only [Set.nonempty_Iio, interior_Ici', Set.mem_Ioi] at hx
    exact hx.le
  have H := nonneg_of_iteratedDeriv_nonneg hf' hD' hx
  rw [â† deriv.comp_ofReal hf.differentiableAt] at H
  change 0 â‰¤ deriv (f âˆ˜ ofReal) x at H
  erw [hF, deriv.ofReal_comp] at H
  norm_cast at H

/-- An entire function whose iterated derivatives at zero are all nonnegative real (except
possibly the value itself) has values of the form `f 0 + nonneg. real` along the nonnegative
real axis. -/
theorem at_zero_le_of_iteratedDeriv_nonneg {f : â„‚ â†’ â„‚} (hf : Differentiable â„‚ f)
    (h : âˆ€ n â‰  0, 0 â‰¤ iteratedDeriv n f 0) {z : â„‚} (hz : 0 â‰¤ z) : f 0 â‰¤ f z := by
  have h' (n : â„•) : 0 â‰¤ iteratedDeriv n (f Â· - f 0) 0 := by
    cases n with
    | zero => simp only [iteratedDeriv_zero, sub_self, le_refl]
    | succ n =>
      specialize h n.succ <| succ_ne_zero n
      rw [iteratedDeriv_succ'] at h âŠ¢
      convert h using 2
      ext w
      exact deriv_sub_const (f 0)
  exact sub_nonneg.mp <| nonneg_of_iteratedDeriv_nonneg (hf.sub_const (f 0)) h' hz

/-- An entire function whose iterated derivatives at `s`` are all nonnegative real (except
possibly the value itself) has values of the form `f s + nonneg. real` along the set `s + â„â‰¥0`. -/
theorem apply_le_of_iteratedDeriv_nonneg {f : â„‚ â†’ â„‚} {s : â„‚} (hf : Differentiable â„‚ f)
    (h : âˆ€ n â‰  0, 0 â‰¤ iteratedDeriv n f s) {z : â„‚} (hz : s â‰¤ z) : f s â‰¤ f z := by
  let g := fun z â†¦ f (s + z)
  convert at_zero_le_of_iteratedDeriv_nonneg (f := g) ?_ (fun n hn â†¦ ?_) ?_ using 1
  Â· simp only [add_zero, g]
  Â· rw [show z = s + (-s + z) by ring]
  Â· exact Differentiable.comp hf <| Differentiable.const_add differentiable_id s
  Â· simpa only [iteratedDeriv_comp_const_add, add_zero, g] using h n hn
  Â· exact le_neg_add_iff_le.mpr hz

/-- An entire function whose iterated derivatives at zero are all real with alternating signs
(except possibly the value itself) has values of the form `f 0 + nonneg. real` along the nonpositive
real axis. -/
theorem at_zero_le_of_iteratedDeriv_alternating {f : â„‚ â†’ â„‚} (hf : Differentiable â„‚ f)
    (h : âˆ€ n â‰  0, 0 â‰¤ (-1) ^ n * iteratedDeriv n f 0) {z : â„‚} (hz : z â‰¤ 0) : f 0 â‰¤ f z := by
  let F : â„‚ â†’ â„‚ := fun z â†¦ f (-z)
  convert at_zero_le_of_iteratedDeriv_nonneg (f := F) (hf.comp <| differentiable_neg)
    (fun n hn â†¦ ?_) (neg_nonneg.mpr hz) using 1
  Â· simp only [F, neg_zero]
  Â· simp only [F, neg_neg]
  Â· simpa only [F, iteratedDeriv_comp_neg, neg_zero] using h n hn

/-- An entire function whose iterated derivatives at `s` are all real with alternating signs
(except possibly the value itself) has values of the form `f s + nonneg. real` along the
set `s - â„â‰¥0`. -/
theorem apply_le_of_iteratedDeriv_alternating {f : â„‚ â†’ â„‚} {s : â„‚} (hf : Differentiable â„‚ f)
    (h : âˆ€ n â‰  0, 0 â‰¤ (-1) ^ n * iteratedDeriv n f s) {z : â„‚} (hz : z â‰¤ s) : f s â‰¤ f z := by
  let g := fun z â†¦ f (s + z)
  convert at_zero_le_of_iteratedDeriv_alternating (f := g) ?_ (fun n hn â†¦ ?_) ?_ using 1
  Â· simp only [add_zero, g]
  Â· rw [show z = s + (-s + z) by ring]
  Â· exact Differentiable.comp hf <| Differentiable.const_add differentiable_id s
  Â· simpa only [iteratedDeriv_comp_const_add, add_zero, g] using h n hn
  Â· exact neg_add_nonpos_iff.mpr hz

end Complex
