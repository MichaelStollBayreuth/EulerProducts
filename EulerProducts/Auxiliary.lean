import Mathlib.Analysis.PSeries
import Mathlib.Topology.CompletelyRegular
import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.NumberTheory.LegendreSymbol.MulCharacter
import Mathlib.RingTheory.DedekindDomain.Basic
import Mathlib.Topology.EMetricSpace.Paracompact
import Mathlib.Topology.MetricSpace.Polish
import Mathlib.Analysis.Calculus.Deriv.Shift
import Mathlib.Analysis.Calculus.IteratedDeriv.Defs

/-!
### Auxiliary lemmas
-/

namespace Nat

lemma Prime.one_le {p : â„•} (hp : p.Prime) : 1 â‰¤ p := hp.one_lt.le

lemma pow_eq_one_iff {m n : â„•} : m ^ n = 1 â†” m = 1 âˆ¨ n = 0 := by
  refine âŸ¨fun H â†¦ ?_, fun H â†¦ ?_âŸ©
  Â· rcases eq_or_ne n 0 with h | h
    Â· exact Or.inr h
    Â· refine Or.inl ?_
      rcases m.eq_zero_or_pos with rfl | hm
      Â· simp [h] at H
      by_contra! hm'
      exact (H â–¸ (one_lt_pow_iff h).mpr <| lt_of_le_of_ne hm hm'.symm).false
  Â· rcases H with rfl | rfl <;> simp

lemma pow_injective_on_primes {p q m n : â„•} (hp : p.Prime) (hq : q.Prime)
    (h : p ^ (m + 1) = q ^ (n + 1)) : p = q âˆ§ m = n := by
  have H := dvd_antisymm (Prime.dvd_of_dvd_pow hp <| h â–¸ dvd_pow_self p (succ_ne_zero m))
    (Prime.dvd_of_dvd_pow hq <| h.symm â–¸ dvd_pow_self q (succ_ne_zero n))
  exact âŸ¨H, succ_inj'.mp <| Nat.pow_right_injective hq.two_le (H â–¸ h)âŸ©

end Nat

namespace Fin

lemma snoc_zero {Î± : Type*} (p : Fin 0 â†’ Î±) (x : Î±) :
    Fin.snoc p x = fun _ â†¦ x := by
  ext y
  have : Subsingleton (Fin (0 + 1)) := Fin.subsingleton_one
  simp only [Subsingleton.elim y (Fin.last 0), snoc_last]

end Fin


namespace Finset

lemma piecewise_same {Î± : Type*} {Î´ : Î± â†’ Sort*} (s : Finset Î±)
    (f : (i : Î±) â†’ Î´ i) [(j : Î±) â†’ Decidable (j âˆˆ s)] :
    s.piecewise f f = f := by
  ext i
  by_cases h : i âˆˆ s <;> simp [h]

end Finset


namespace ZMod

open Nat

lemma eq_one_of_isUnit_natCast {n : â„•} (h : IsUnit (n : ZMod 0)) : n = 1 := by
  have := Int.isUnit_iff.mp h
  norm_cast at this
  exact this.resolve_right not_false

lemma isUnit_iff_coprime (m n : â„•) : IsUnit (m : ZMod n) â†” m.Coprime n := by
  refine âŸ¨fun H â†¦ ?_, fun H â†¦ (unitOfCoprime m H).isUnitâŸ©
  have H' := val_coe_unit_coprime H.unit
  rw [IsUnit.unit_spec, val_nat_cast m, coprime_iff_gcd_eq_one] at H'
  rw [coprime_iff_gcd_eq_one, Nat.gcd_comm, â† H']
  exact gcd_rec n m

lemma not_isUnit_of_mem_primeFactors {n p : â„•} (h : p âˆˆ n.primeFactors) :
    Â¬ IsUnit (p : ZMod n) := by
  rw [isUnit_iff_coprime]
  exact (Prime.dvd_iff_not_coprime <| prime_of_mem_primeFactors h).mp <| dvd_of_mem_primeFactors h

lemma isUnit_prime_of_not_dvd {n p : â„•} (hp : p.Prime) (h : Â¬ p âˆ£ n) : IsUnit (p : ZMod n) := by
  rw [isUnit_iff_coprime]
  exact (Nat.Prime.coprime_iff_not_dvd hp).mpr h

end ZMod

namespace Real

lemma log_le_rpow {x Îµ : â„} (hx : 0 â‰¤ x) (hÎµ : 0 < Îµ) :
    log x â‰¤ Îµâ»Â¹ * x ^ Îµ := by
  rcases hx.eq_or_lt with rfl | h
  Â· rw [log_zero, zero_rpow hÎµ.ne', mul_zero]
  suffices : Îµ * log x â‰¤ x ^ Îµ
  Â· apply_fun (Îµâ»Â¹ * Â·) at this using monotone_mul_left_of_nonneg (inv_pos.mpr hÎµ).le
    simp only at this
    rwa [â† mul_assoc, inv_mul_cancel hÎµ.ne', one_mul] at this
  exact (log_rpow h Îµ).symm.trans_le <| (log_le_sub_one_of_pos <| rpow_pos_of_pos h Îµ).trans
    (sub_one_lt _).le

lemma log_ofNat_le_rpow (n : â„•) {Îµ : â„} (hÎµ : 0 < Îµ) :
    log n â‰¤ Îµâ»Â¹ * n ^ Îµ :=
  log_le_rpow n.cast_nonneg hÎµ

lemma not_summable_indicator_one_div_nat_cast {m : â„•} (hm : m â‰  0) (k : ZMod m) :
    Â¬ Summable (Set.indicator {n : â„• | (n : ZMod m) = k} fun n : â„• â†¦ (1 / n : â„)) := by
  have : NeZero m := { out := hm }
  suffices : Â¬ Summable fun n : â„• â†¦ (1 / (m * n + k.val) : â„)
  Â· set f : â„• â†’ â„ := Set.indicator {n : â„• | (n : ZMod m) = k} fun n : â„• â†¦ (1 / n : â„)
    contrapose! this
    let g : â„• â†’ â„• := fun n â†¦ m * n + k.val
    have hg : Function.Injective g
    Â· intro m n hmn
      simpa [g, hm] using hmn
    have hg' : âˆ€ n âˆ‰ Set.range g, f n = 0
    Â· intro n hn
      contrapose! hn
      convert Set.mem_of_indicator_ne_zero hn
      ext n
      simp only [Set.mem_range, Set.mem_setOf_eq, ZMod.nat_coe_zmod_eq_iff]
      conv => enter [1, 1, y]; rw [add_comm, eq_comm]
    convert (Function.Injective.summable_iff hg hg').mpr this with n
    simp [g]
  by_contra! h
  refine not_summable_one_div_nat_cast <| (summable_nat_add_iff 2).mp <|
    (summable_mul_left_iff (one_div_ne_zero <| Nat.cast_ne_zero.mpr hm)).mp <|
    Summable.of_nonneg_of_le (fun n â†¦ by positivity) (fun n â†¦ ?_) <| (summable_nat_add_iff 1).mpr h
  field_simp
  rw [â† ZMod.nat_cast_val k]
  gcongr
  norm_cast
  linarith only [ZMod.val_le k]

end Real


namespace Complex

lemma one_add_I_mul_ne_one {y : â„} (hy : y â‰  0) : 1 + I * y â‰  1 := by
  simp only [ne_eq, add_right_eq_self, mul_eq_zero, I_ne_zero, ofReal_eq_zero, hy, or_self,
    not_false_eq_true]

@[simp, norm_cast]
lemma ofNat_log {n : â„•} : Real.log n = log n := ofReal_nat_cast n â–¸ ofReal_log n.cast_nonneg

@[simp, norm_cast]
lemma ofNat_arg {n : â„•} : arg n = 0 :=
  ofReal_nat_cast n â–¸ arg_ofReal_of_nonneg n.cast_nonneg

lemma norm_log_ofNat_le_rpow (n : â„•) {Îµ : â„} (hÎµ : 0 < Îµ) :
    â€–log nâ€– â‰¤ Îµâ»Â¹ * n ^ Îµ := by
  rcases n.eq_zero_or_pos with rfl | h
  Â· rw [Nat.cast_zero, Nat.cast_zero, log_zero, norm_zero, Real.zero_rpow hÎµ.ne', mul_zero]
  rw [norm_eq_abs, â† ofNat_log, abs_ofReal,
    _root_.abs_of_nonneg <| Real.log_nonneg <| by exact_mod_cast Nat.one_le_of_lt h.lt]
  exact Real.log_ofNat_le_rpow n hÎµ

lemma mul_cpow_ofNat (m n : â„•) (s : â„‚) : (m * n : â„‚) ^ s = m ^ s * n ^ s :=
  ofReal_nat_cast m â–¸ ofReal_nat_cast n â–¸ mul_cpow_ofReal_nonneg m.cast_nonneg n.cast_nonneg s

lemma ofNat_cpow_mul (n m : â„•) (z : â„‚) : (n : â„‚) ^ (m * z) = ((n : â„‚) ^ m) ^ z := by
  rw [cpow_mul]
  Â· rw [cpow_nat_cast]
  all_goals
    rw [â† ofNat_log]
    norm_cast
    linarith [Real.pi_pos]

lemma norm_ofNat_cpow_of_re_ne_zero (n : â„•) {s : â„‚} (hs : s.re â‰  0) :
    â€–(n : â„‚) ^ sâ€– = (n : â„) ^ (s.re) := by
  rw [norm_eq_abs, â† ofReal_nat_cast, abs_cpow_eq_rpow_re_of_nonneg n.cast_nonneg hs]

lemma norm_ofNat_cpow_of_pos {n : â„•} (hn : 0 < n) (s : â„‚) :
    â€–(n : â„‚) ^ sâ€– = (n : â„) ^ (s.re) := by
  rw [norm_eq_abs, â† ofReal_nat_cast, abs_cpow_eq_rpow_re_of_pos (Nat.cast_pos.mpr hn) _]

lemma norm_ofNat_cpow_pos_of_pos {n : â„•} (hn : 0 < n) (s : â„‚) : 0 < â€–(n : â„‚) ^ sâ€– :=
  (norm_ofNat_cpow_of_pos hn _).symm â–¸ Real.rpow_pos_of_pos (Nat.cast_pos.mpr hn) _

lemma norm_prime_cpow_le_one_half (p : Nat.Primes) {s : â„‚} (hs : 1 < s.re) :
    â€–(p : â„‚) ^ (-s)â€– â‰¤ 1 / 2 := by
  rw [norm_ofNat_cpow_of_re_ne_zero p <| by rw [neg_re]; linarith only [hs]]
  refine (Real.rpow_le_rpow_of_nonpos zero_lt_two (Nat.cast_le.mpr p.prop.two_le) <|
    by rw [neg_re]; linarith only [hs]).trans ?_
  rw [one_div, â† Real.rpow_neg_one]
  exact Real.rpow_le_rpow_of_exponent_le one_le_two <| (neg_lt_neg hs).le

lemma one_sub_prime_cpow_ne_zero {p : â„•} (hp : p.Prime) {s : â„‚} (hs : 1 < s.re) :
    1 - (p : â„‚) ^ (-s) â‰  0 := by
  refine sub_ne_zero_of_ne fun H â†¦ ?_
  have := norm_prime_cpow_le_one_half âŸ¨p, hpâŸ© hs
  simp only at this
  rw [â† H, norm_one] at this
  norm_num at this

lemma norm_ofNat_cpow_le_norm_ofNat_cpow_of_pos {n : â„•} (hn : 0 < n) {w z : â„‚} (h : w.re â‰¤ z.re) :
    â€–(n : â„‚) ^ wâ€– â‰¤ â€–(n : â„‚) ^ zâ€– := by
  simp_rw [norm_ofNat_cpow_of_pos hn]
  exact Real.rpow_le_rpow_of_exponent_le (by exact_mod_cast hn) h

lemma indicator_ofReal {f : â„• â†’ â„} {s : Set â„•} :
    (fun n â†¦ ((Set.indicator s f n : â„) : â„‚)) = Set.indicator s (fun n â†¦ (f n : â„‚)) := by
  ext n
  by_cases h : n âˆˆ s
  Â· simp only [h, Set.indicator_of_mem]
  Â· simp only [h, not_false_eq_true, Set.indicator_of_not_mem, ofReal_zero]

lemma summable_re {Î± : Type u_1} {f : Î± â†’ â„‚} (h : Summable f) :
    Summable fun x â†¦ (f x).re :=
  HasSum.summable <| Complex.hasSum_re h.hasSum

lemma summable_im {Î± : Type u_1} {f : Î± â†’ â„‚} (h : Summable f) :
    Summable fun x â†¦ (f x).im :=
  HasSum.summable <| Complex.hasSum_im h.hasSum

end Complex


namespace Equiv.Set

lemma prod_symm_apply {Î± Î² : Type*} (s : Set Î±) (t : Set Î²) (x : s Ã— t) :
    (Set.prod s t).symm x = (x.1.val, x.2.val) := rfl

/-- The canonical equivalence between `{a} Ã—Ë¢ t` and `t`, considered as types. -/
def prod_singleton_left {Î± Î² : Type*} (a : Î±) (t : Set Î²) : â†‘({a} Ã—Ë¢ t) â‰ƒ â†‘t where
  toFun := fun x â†¦ âŸ¨x.val.snd, (Set.mem_prod.mp x.prop).2âŸ©
  invFun := fun b â†¦ âŸ¨(a, b.val), Set.mem_prod.mpr âŸ¨Set.mem_singleton a, Subtype.mem bâŸ©âŸ©
  left_inv := by simp [Function.LeftInverse]
  right_inv := by simp [Function.RightInverse, Function.LeftInverse]

/-- The canonical equivalence between `s Ã—Ë¢ {b}` and `s`, considered as types. -/
def prod_singleton_right {Î± Î² : Type*} (s : Set Î±) (b : Î²) : â†‘(s Ã—Ë¢ {b}) â‰ƒ â†‘s where
  toFun := fun x â†¦ âŸ¨x.val.fst, (Set.mem_prod.mp x.prop).1âŸ©
  invFun := fun a â†¦ âŸ¨(a.val, b), Set.mem_prod.mpr âŸ¨Subtype.mem a, Set.mem_singleton bâŸ©âŸ©
  left_inv := by simp [Function.LeftInverse]
  right_inv := by simp [Function.RightInverse, Function.LeftInverse]

end Equiv.Set


lemma HasSum.tsum_fiberwise {Î² Î³ : Type*} {f : Î² â†’ â„‚} (g : Î² â†’ Î³) {a : â„‚} (hf : HasSum f a) :
    HasSum (fun c : Î³ â†¦ âˆ‘' b : g â»Â¹' {c}, f b) a :=
  HasSum.sigma ((Equiv.hasSum_iff <| Equiv.sigmaFiberEquiv g).mpr hf) <|
    fun _ â†¦ (Summable.hasSum_iff <| Summable.subtype hf.summable _).mpr rfl

lemma tsum_setProd_eq_tsum_prod {Î± Î² : Type*} (s : Set Î±) (t : Set Î²) (f : Î± Ã— Î² â†’ â„‚) :
    (âˆ‘' x : s Ã—Ë¢ t, f x) = âˆ‘' x : s Ã— t, f ((Equiv.Set.prod s t).symm x) :=
  ((Equiv.Set.prod s t).symm.tsum_eq <| (s Ã—Ë¢ t).restrict f).symm

lemma tsum_setProd_singleton_left {Î± Î² : Type*} (a : Î±) (t : Set Î²) (f : Î± Ã— Î² â†’ â„‚) :
    (âˆ‘' x : {a} Ã—Ë¢ t, f x) = âˆ‘' b : t, f (a, b) :=
  (Equiv.Set.prod_singleton_left a t |>.symm.tsum_eq <| ({a} Ã—Ë¢ t).restrict f).symm

lemma tsum_setProd_singleton_right {Î± Î² : Type*} (s : Set Î±) (b : Î²) (f : Î± Ã— Î² â†’ â„‚) :
    (âˆ‘' x : s Ã—Ë¢ {b}, f x) = âˆ‘' a : s, f (a, b) :=
  (Equiv.Set.prod_singleton_right s b |>.symm.tsum_eq <| (s Ã—Ë¢ {b}).restrict f).symm


namespace MulChar

@[coe]
def toMonoidWithZeroHom  {R R' : Type*} [CommMonoidWithZero R] [CommMonoidWithZero R']
    [Nontrivial R] (Ï‡ : MulChar R R') : R â†’*â‚€ R' where
      toFun := Ï‡.toFun
      map_zero' := Ï‡.map_zero
      map_one' := Ï‡.map_one'
      map_mul' := Ï‡.map_mul'

lemma one_apply {R : Type*} [CommMonoid R] (R' : Type*) [CommMonoidWithZero R'] {x : R}
    (hx : IsUnit x) : (1 : MulChar R R') x = 1 := by
  rw [show (1 : MulChar R R') = trivial R R' from rfl]
  simp only [trivial_apply, hx, ite_true]

end MulChar

section Topology

namespace Asymptotics

open Filter in
lemma isBigO_mul_iff_isBigO_div {Î± F : Type*} [NormedField F] {l : Filter Î±} {f g h : Î± â†’ F}
    (hf : âˆ€á¶  x in l, f x â‰  0) :
    (fun x â†¦ f x * g x) =O[l] h â†” g =O[l] (fun x â†¦ h x / f x) := by
  rw [isBigO_iff', isBigO_iff']
  refine âŸ¨fun âŸ¨c, hc, HâŸ© â†¦ âŸ¨c, hc, ?_âŸ©, fun âŸ¨c, hc, HâŸ© â†¦ âŸ¨c, hc, ?_âŸ©âŸ© <;>
  { refine H.congr <| Eventually.mp hf <| eventually_of_forall fun x hx â†¦ ?_
    rw [norm_mul, norm_div, â† mul_div_assoc, mul_comm]
    have hx' : â€–f xâ€– > 0 := norm_pos_iff.mpr hx
    rw [le_div_iff hx', mul_comm] }

open Topology Filter in
lemma isLittleO_id (s : Set â„) : (id : â„ â†’ â„) =o[nhdsWithin 0 s] (fun _ â†¦ (1 : â„)) :=
  ((isLittleO_one_iff â„).mpr tendsto_id).mono nhdsWithin_le_nhds

end Asymptotics


open Topology Asymptotics in
lemma DifferentiableAt.isBigO_of_eq_zero {f : â„‚ â†’ â„‚} {z : â„‚} (hf : DifferentiableAt â„‚ f z)
    (hz : f z = 0) : (fun w â†¦ f (w + z)) =O[ğ“ 0] id := by
  rw [â† zero_add z] at hf
  have := (hf.hasDerivAt.comp_add_const 0 z).differentiableAt.isBigO_sub
  simp only [zero_add, hz, sub_zero] at this
  exact this.trans <| isBigO_refl ..

open Topology Asymptotics Filter in
lemma ContinuousAt.isBigO {f : â„‚ â†’ â„‚} {z : â„‚} (hf : ContinuousAt f z) :
    (fun w â†¦ f (w + z)) =O[ğ“ 0] (fun _ â†¦ (1 : â„‚)) := by
  rw [isBigO_iff']
  replace hf : ContinuousAt (fun w â†¦ f (w + z)) 0
  Â· convert (Homeomorph.comp_continuousAt_iff' (Homeomorph.addLeft (-z)) _ z).mp ?_
    Â· simp
    Â· simp [Function.comp_def, hf]
  simp_rw [Metric.continuousAt_iff', dist_eq_norm_sub, zero_add] at hf
  specialize hf 1 zero_lt_one
  refine âŸ¨â€–f zâ€– + 1, by positivity, ?_âŸ©
  refine Eventually.mp hf <| eventually_of_forall fun w hw â†¦ le_of_lt ?_
  calc â€–f (w + z)â€–
    _ â‰¤ â€–f zâ€– + â€–f (w + z) - f zâ€– := norm_le_insert' ..
    _ < â€–f zâ€– + 1 := add_lt_add_left hw _
    _ = _ := by simp only [norm_one, mul_one]

open Topology in
lemma Complex.isBigO_comp_ofReal {f g : â„‚ â†’ â„‚} {x : â„} (h : f =O[ğ“ (x : â„‚)] g) :
    (fun y : â„ â†¦ f y) =O[ğ“ x] (fun y : â„ â†¦ g y) :=
  Asymptotics.IsBigO.comp_tendsto (k := fun y : â„ â†¦ (y : â„‚)) h <|
    Continuous.tendsto Complex.continuous_ofReal x

open Topology in
lemma Complex.isBigO_comp_ofReal_nhds_ne {f g : â„‚ â†’ â„‚} {x : â„} (h : f =O[ğ“[â‰ ] (x : â„‚)] g) :
    (fun y : â„ â†¦ f y) =O[ğ“[â‰ ] x] (fun y : â„ â†¦ g y) :=
  Asymptotics.IsBigO.comp_tendsto (k := fun y : â„ â†¦ (y : â„‚)) h <|
    ((hasDerivAt_id (x : â„‚)).comp_ofReal).tendsto_punctured_nhds one_ne_zero

end Topology


namespace FormalMultilinearSeries

universe u v

variable {ğ•œ : Type*} {E : Type u} {F : Type v} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  (p : FormalMultilinearSeries ğ•œ E F)

/-- This series appears in `HasFPowerSeriesOnBall.fderiv` -/
noncomputable
def derivSeries : FormalMultilinearSeries ğ•œ E (E â†’L[ğ•œ] F) :=
  (continuousMultilinearCurryFin1 ğ•œ E F : (E[Ã—1]â†’L[ğ•œ] F) â†’L[ğ•œ] E â†’L[ğ•œ] F)
    |>.compFormalMultilinearSeries (p.changeOriginSeries 1)

open Fintype ContinuousLinearMap in
theorem derivSeries_apply_diag (n : â„•) (x : E) :
    derivSeries p n (fun _ â†¦ x) x = (n + 1) â€¢ p (n + 1) fun _ â†¦ x := by
  simp only [derivSeries, strongUniformity_topology_eq, compFormalMultilinearSeries_apply,
    changeOriginSeries, compContinuousMultilinearMap_coe, ContinuousLinearEquiv.coe_coe,
    LinearIsometryEquiv.coe_coe, Function.comp_apply, ContinuousMultilinearMap.sum_apply, map_sum,
    coe_sum', Finset.sum_apply, continuousMultilinearCurryFin1_apply, Matrix.zero_empty]
  convert Finset.sum_const _
  Â· rw [Fin.snoc_zero, changeOriginSeriesTerm_apply, Finset.piecewise_same, add_comm]
  Â· erw [â† card, card_subtype, â† Finset.powersetCard_eq_filter, Finset.card_powersetCard, â† card,
      card_fin, eq_comm, add_comm, Nat.choose_succ_self_right]

end FormalMultilinearSeries

namespace HasFPowerSeriesOnBall

universe u v

open FormalMultilinearSeries ENNReal Nat

variable {ğ•œ : Type*} {E : Type u} {F : Type v} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {p : FormalMultilinearSeries ğ•œ E F} {f : E â†’ F} {x : E} {r : â„â‰¥0âˆ}
  (h : HasFPowerSeriesOnBall f p x r) (y : E)
-- assumption h could be replaced by HasFPowerSeriesAt

theorem iteratedFDeriv_zero_apply_diag :
    iteratedFDeriv ğ•œ 0 f x (fun _ â†¦ y) = p 0 (fun _ â†¦ y) := by
  convert (h.hasSum <| EMetric.mem_ball_self h.r_pos).tsum_eq.symm
  Â· rw [iteratedFDeriv_zero_apply, add_zero]
  Â· rw [tsum_eq_single 0 <| fun n hn â†¦ by haveI := NeZero.mk hn; exact (p n).map_zero]
    exact congr(p 0 $(Subsingleton.elim _ _))

private theorem factorial_smul' : âˆ€ {F : Type max u v} [NormedAddCommGroup F]
    [NormedSpace ğ•œ F] [CompleteSpace F] {p : FormalMultilinearSeries ğ•œ E F}
    {f : E â†’ F}, HasFPowerSeriesOnBall f p x r â†’
    n ! â€¢ p n (fun _ â†¦ y) = iteratedFDeriv ğ•œ n f x (fun _ â†¦ y) := by
  induction' n with n ih <;> intro F _ _ _ p f h
  Â· rw [factorial_zero, one_smul, h.iteratedFDeriv_zero_apply_diag]
  Â· rw [factorial_succ, mul_comm, mul_smul, â† derivSeries_apply_diag,
      â† ContinuousLinearMap.smul_apply, derivSeries, ih h.fderiv,
      iteratedFDeriv_succ_apply_right]
    rfl

variable [CompleteSpace F]

theorem factorial_smul (n : â„•) :
    n ! â€¢ p n (fun _ â†¦ y) = iteratedFDeriv ğ•œ n f x (fun _ â†¦ y) := by
  cases n
  Â· rw [factorial_zero, one_smul, h.iteratedFDeriv_zero_apply_diag]
  Â· erw [factorial_succ, mul_comm, mul_smul, â† derivSeries_apply_diag,
      â† ContinuousLinearMap.smul_apply, factorial_smul'.{_,u,v} _ h.fderiv,
      iteratedFDeriv_succ_apply_right]
    rfl

theorem hasSum_iteratedFDeriv [CharZero ğ•œ] {y : E} (hy : y âˆˆ EMetric.ball 0 r) :
    HasSum (fun n â†¦ (1 / n ! : ğ•œ) â€¢ iteratedFDeriv ğ•œ n f x fun _ â†¦ y) (f (x + y)) := by
  convert h.hasSum hy with n
  rw [â† h.factorial_smul y n, smul_comm, â† smul_assoc, nsmul_eq_mul,
    mul_one_div_cancel <| cast_ne_zero.mpr n.factorial_ne_zero, one_smul]

/- We can't quite show
  `HasFPowerSeriesOnBall f (fun n â†¦ (1 / n !) â€¢ iteratedFDeriv ğ•œ n f x) x r`
  because `r_le` requires bounding the norm of a multilinear map using values on
  the diagonal, so some polarization identity would be required. -/

end HasFPowerSeriesOnBall


namespace deriv

variable {ğ•œ F : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup F] [NormedSpace ğ•œ F]

open ContinuousLinearMap in
lemma comp_neg (f : ğ•œ â†’ F) (a : ğ•œ) : deriv (fun x â†¦ f (-x)) a = -deriv f (-a) := by
  by_cases h : DifferentiableAt ğ•œ f (-a)
  Â· simp_rw [â† fderiv_deriv]
    change (fderiv ğ•œ (f âˆ˜ fun x â†¦ -x) a) 1 = _
    rw [fderiv.comp _ h differentiable_neg.differentiableAt, show @Neg.neg ğ•œ _ = (- Â·) from rfl,
      coe_comp', Function.comp_apply, fderiv_neg, fderiv_id', neg_apply, coe_id', id_eq, map_neg]
  Â· have H : Â¬ DifferentiableAt ğ•œ (fun x â†¦ f (-x)) a
    Â· contrapose! h
      rw [â† neg_neg a] at h
      convert h.comp (-a) differentiable_neg.differentiableAt
      ext
      simp only [Function.comp_apply, neg_neg]
    rw [deriv_zero_of_not_differentiableAt h, deriv_zero_of_not_differentiableAt H, neg_zero]

/-- A variant of `deriv_const_smul` without differentiability assumption when the scalar
multiplication is by field elements. -/
lemma const_smul {f : ğ•œ â†’ F} {x : ğ•œ} {R : Type*} [Field R] [Module R F] [SMulCommClass ğ•œ R F]
    [ContinuousConstSMul R F] (c : R) :
    deriv (fun y â†¦ c â€¢ f y) x = c â€¢ deriv f x := by
  by_cases hf : DifferentiableAt ğ•œ f x
  Â· exact deriv_const_smul c hf
  Â· rcases eq_or_ne c 0 with rfl | hc
    Â· simp only [zero_smul, deriv_const']
    Â· have H : Â¬DifferentiableAt ğ•œ (fun y â†¦ c â€¢ f y) x
      Â· contrapose! hf
        change DifferentiableAt ğ•œ (fun y â†¦ f y) x
        conv => enter [2, y]; rw [â† inv_smul_smulâ‚€ hc (f y)]
        exact DifferentiableAt.const_smul hf câ»Â¹
      rw [deriv_zero_of_not_differentiableAt hf, deriv_zero_of_not_differentiableAt H, smul_zero]

end deriv


namespace iteratedDeriv

variable {ğ•œ F : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup F] [NormedSpace ğ•œ F]

lemma neg (n : â„•) (f : ğ•œ â†’ F) (a : ğ•œ) :
    iteratedDeriv n (fun x â†¦ -(f x)) a = -(iteratedDeriv n f a) := by
  induction' n with n ih generalizing a
  Â· simp only [Nat.zero_eq, iteratedDeriv_zero]
  Â· have ih' : iteratedDeriv n (fun x â†¦ -f x) = fun x â†¦ -iteratedDeriv n f x := funext ih
    rw [iteratedDeriv_succ, iteratedDeriv_succ, ih', deriv.neg]

lemma comp_neg (n : â„•) (f : ğ•œ â†’ F) (a : ğ•œ) :
    iteratedDeriv n (fun x â†¦ f (-x)) a = (-1 : ğ•œ) ^ n â€¢ iteratedDeriv n f (-a) := by
  induction' n with n ih generalizing a
  Â· simp only [Nat.zero_eq, iteratedDeriv_zero, pow_zero, one_smul]
  Â· have ih' : iteratedDeriv n (fun x â†¦ f (-x)) = fun x â†¦ (-1 : ğ•œ) ^ n â€¢ iteratedDeriv n f (-x) :=
      funext ih
    rw [iteratedDeriv_succ, iteratedDeriv_succ, ih', pow_succ, neg_mul, one_mul,
      deriv.comp_neg (f := fun x â†¦ (-1 : ğ•œ) ^ n â€¢ iteratedDeriv n f x), deriv.const_smul, neg_smul]

end iteratedDeriv


namespace Complex
-- see https://leanprover.zulipchat.com/#narrow/stream/217875-Is-there-code-for-X.3F/topic/Differentiability.20of.20the.20natural.20map.20.E2.84.9D.20.E2.86.92.20.E2.84.82/near/418095234

lemma hasDerivAt_ofReal (x : â„) : HasDerivAt ofReal' 1 x :=
  HasDerivAt.ofReal_comp <| hasDerivAt_id x

lemma differentiableAt_ofReal (x : â„) : DifferentiableAt â„ ofReal' x :=
  (hasDerivAt_ofReal x).differentiableAt

lemma differentiable_ofReal : Differentiable â„ ofReal' :=
  Complex.ofRealClm.differentiable
  -- fun x â†¦ âŸ¨_, (hasDerivAt_ofReal x).hasFDerivAtâŸ©

end Complex

lemma DifferentiableAt.comp_ofReal {e : â„‚ â†’ â„‚} {z : â„} (hf : DifferentiableAt â„‚ e â†‘z) :
    DifferentiableAt â„ (fun x : â„ â†¦ e x) z :=
  hf.hasDerivAt.comp_ofReal.differentiableAt

lemma Differentiable.comp_ofReal {e : â„‚ â†’ â„‚} (h : Differentiable â„‚ e) :
    Differentiable â„ (fun x : â„ â†¦ e x) :=
  fun _ â†¦ h.differentiableAt.comp_ofReal

lemma DifferentiableAt.ofReal_comp {z : â„} {f : â„ â†’ â„} (hf : DifferentiableAt â„ f z) :
    DifferentiableAt â„ (fun (y : â„) => (f y : â„‚)) z :=
  hf.hasDerivAt.ofReal_comp.differentiableAt

lemma Differentiable.ofReal_comp {f : â„ â†’ â„} (hf : Differentiable â„ f) :
    Differentiable â„ (fun (y : â„) => (f y : â„‚)) :=
  fun _ â†¦ hf.differentiableAt.ofReal_comp

namespace Complex

section OrderInstance

open scoped ComplexOrder

instance : OrderClosedTopology â„‚ where
  isClosed_le' := by
    simp_rw [le_def, Set.setOf_and]
    refine IsClosed.inter (isClosed_le ?_ ?_) (isClosed_eq ?_ ?_) <;> continuity

end OrderInstance

open BigOperators Nat

variable {E : Type u} [NormedAddCommGroup E] [NormedSpace â„‚ E] [CompleteSpace E]

/-- A function that is complex differentiable on the closed ball of radius `r` around `c`
is given by evaluating its Taylor series at `c` on the open ball of radius `r` around `c`. -/
lemma hasSum_taylorSeries_on_ball {f : â„‚ â†’ E} {r : NNReal} (hr : 0 < r)
    (hf : DifferentiableOn â„‚ f (Metric.closedBall c r)) {z : â„‚} (hz : z âˆˆ Metric.ball c r) :
    HasSum (fun n : â„• â†¦ (1 / n ! : â„‚) â€¢ (z - c) ^ n â€¢ iteratedDeriv n f c) (f z) := by
  have hz' : z - c âˆˆ EMetric.ball 0 r
  Â· rw [Metric.emetric_ball_nnreal]
    exact mem_ball_zero_iff.mpr hz
  have H := (hf.hasFPowerSeriesOnBall hr).hasSum_iteratedFDeriv hz'
  simp only [add_sub_cancel'_right] at H
  convert H using 4 with n
  simpa only [iteratedDeriv_eq_iteratedFDeriv, smul_eq_mul, mul_one, Finset.prod_const,
    Finset.card_fin]
    using ((iteratedFDeriv â„‚ n f c).map_smul_univ (fun _ â†¦ z - c) (fun _ â†¦ 1)).symm

/-- A function that is complex differentiable on the closed ball of radius `r` around `c`
is given by evaluating its Taylor series at `c` on the open ball of radius `r` around `c`. -/
lemma taylorSeries_eq_on_ball {f : â„‚ â†’ E} {r : NNReal} (hr : 0 < r)
    (hf : DifferentiableOn â„‚ f (Metric.closedBall c r)) {z : â„‚} (hz : z âˆˆ Metric.ball c r) :
    âˆ‘' n : â„•, (1 / n ! : â„‚) â€¢ (z - c) ^ n â€¢ iteratedDeriv n f c = f z :=
  (hasSum_taylorSeries_on_ball hr hf hz).tsum_eq

/-- A function that is complex differentiable on the closed ball of radius `r` around `c`
is given by evaluating its Taylor series at `c` on the open ball of radius `r` around `c`. -/
lemma taylorSeries_eq_on_ball' {f : â„‚ â†’ â„‚} {r : NNReal} (hr : 0 < r)
    (hf : DifferentiableOn â„‚ f (Metric.closedBall c r)) {z : â„‚} (hz : z âˆˆ Metric.ball c r) :
    âˆ‘' n : â„•, (1 / n ! : â„‚) * iteratedDeriv n f c * (z - c) ^ n = f z := by
  convert taylorSeries_eq_on_ball hr hf hz using 3 with n
  rw [mul_right_comm, smul_eq_mul, smul_eq_mul, mul_assoc]

/-- A function that is complex differentiable on the complex plane is given by evaluating
its Taylor series at any point `c`. -/
lemma hasSum_taylorSeries_of_entire {f : â„‚ â†’ E} (hf : Differentiable â„‚ f) (c z : â„‚) :
    HasSum (fun n : â„• â†¦ (1 / n ! : â„‚) â€¢ (z - c) ^ n â€¢ iteratedDeriv n f c) (f z) := by
  have hR := lt_add_of_pos_of_le zero_lt_one <| zero_le (âŸ¨â€–z - câ€–, norm_nonneg _âŸ© : NNReal)
  refine hasSum_taylorSeries_on_ball hR hf.differentiableOn ?_
  rw [mem_ball_iff_norm, NNReal.coe_add, NNReal.coe_one, NNReal.coe_mk, lt_add_iff_pos_left]
  exact zero_lt_one

/-- A function that is complex differentiable on the complex plane is given by evaluating
its Taylor series at any point `c`. -/
lemma taylorSeries_eq_of_entire {f : â„‚ â†’ E} (hf : Differentiable â„‚ f) (c z : â„‚) :
    âˆ‘' n : â„•, (1 / n ! : â„‚) â€¢ (z - c) ^ n â€¢ iteratedDeriv n f c = f z :=
  (hasSum_taylorSeries_of_entire hf c z).tsum_eq

/-- A function that is complex differentiable on the complex plane is given by evaluating
its Taylor series at any point `c`. -/
lemma taylorSeries_eq_of_entire' {f : â„‚ â†’ â„‚} (hf : Differentiable â„‚ f) (c z : â„‚) :
    âˆ‘' n : â„•, (1 / n ! : â„‚) * iteratedDeriv n f c * (z - c) ^ n = f z := by
  convert taylorSeries_eq_of_entire hf c z using 3 with n
  rw [mul_right_comm, smul_eq_mul, smul_eq_mul, mul_assoc]

/-- A function that is complex differentiable on the closed ball of radius `r` around `c`,
where `c` is real, and all whose iterated derivatives at `c` are real can be give by a real
differentiable function on the real open interval from `c-r` to `c+r`. -/
lemma realValued_of_iteratedDeriv_real {f : â„‚ â†’ â„‚} {r : NNReal} {c : â„} (hr : 0 < r)
    (hf : DifferentiableOn â„‚ f (Metric.closedBall c r)) {D : â„• â†’ â„}
    (hd : âˆ€ n, iteratedDeriv n f c = D n) :
    âˆƒ F : â„ â†’ â„, DifferentiableOn â„ F (Set.Ioo (c - r) (c + r)) âˆ§
      Set.EqOn (f âˆ˜ ofReal') (ofReal' âˆ˜ F) (Set.Ioo (c - r) (c + r)) := by
  sorry

/-- A function that is complex differentiable on the complex plane and all whose iterated
derivatives at a real point `c` are real can be given by a real differentiable function
on the real line. -/
lemma realValued_of_iteratedDeriv_real' {f : â„‚ â†’ â„‚} (hf : Differentiable â„‚ f) {c : â„} {D : â„• â†’ â„}
    (hd : âˆ€ n, iteratedDeriv n f c = D n) :
    âˆƒ F : â„ â†’ â„, Differentiable â„ F âˆ§ (f âˆ˜ ofReal') = (ofReal' âˆ˜ F) := by
  have H := taylorSeries_eq_of_entire' hf c
  simp_rw [hd] at H
  refine âŸ¨fun x â†¦ âˆ‘' (n : â„•), 1 / â†‘n ! * (D n) * (x - c) ^ n, ?_, ?_âŸ©
  Â· have := hf.comp_ofReal
    sorry
  Â· ext x
    simp only [Function.comp_apply, ofReal_eq_coe, â† H, ofReal_tsum]
    push_cast
    rfl

open scoped ComplexOrder

lemma monotone_ofReal : Monotone ofReal := by
  intro x y hxy
  simp only [ofReal_eq_coe, real_le_real, hxy]

/-- An entire function whose iterated derivatives at zero are all nonnegative real has nonnegative
real values for nonnegative real arguments. -/
theorem nonneg_of_iteratedDeriv_nonneg {f : â„‚ â†’ â„‚} (hf : Differentiable â„‚ f)
    (h : âˆ€ n, 0 â‰¤ iteratedDeriv n f 0) {z : â„‚} (hz : 0 â‰¤ z) : 0 â‰¤ f z := by
  have H := taylorSeries_eq_of_entire' hf 0 z
  have hz' := eq_re_of_ofReal_le hz
  rw [hz'] at hz H âŠ¢
  obtain âŸ¨D, hDâŸ© : âˆƒ D : â„• â†’ â„, âˆ€ n, 0 â‰¤ D n âˆ§ iteratedDeriv n f 0 = D n
  Â· refine âŸ¨fun n â†¦ (iteratedDeriv n f 0).re, fun n â†¦ âŸ¨?_, ?_âŸ©âŸ©
    Â· have := eq_re_of_ofReal_le (h n) â–¸ h n
      norm_cast at this
    Â· rw [eq_re_of_ofReal_le (h n)]
  simp_rw [â† H, hD, â† ofReal_nat_cast, sub_zero, â† ofReal_pow, â† ofReal_one, â† ofReal_div,
    â† ofReal_mul, â† ofReal_tsum]
  norm_cast
  refine tsum_nonneg fun n â†¦ ?_
  norm_cast at hz
  have := (hD n).1
  positivity

/-- An entire function whose iterated derivatives at zero are all nonnegative real is
monotonic on the nonnegative real axis. -/
theorem monotoneOn_of_iteratedDeriv_nonneg  {f : â„‚ â†’ â„‚} (hf : Differentiable â„‚ f)
    (h : âˆ€ n, 0 â‰¤ iteratedDeriv n f 0) : MonotoneOn (f âˆ˜ ofReal) (Set.Ici (0 : â„)) := by
  obtain âŸ¨F, hFâŸ© : âˆƒ F : â„ â†’ â„, f âˆ˜ ofReal = ofReal âˆ˜ F
  Â· sorry
  rw [hF]
  refine Monotone.comp_monotoneOn monotone_ofReal <| Convex.monotoneOn_of_deriv_nonneg ?_ ?_ ?_ ?_
  Â· exact convex_Ici 0
  Â· refine Continuous.continuousOn ?_
    sorry
  Â· refine Differentiable.differentiableOn ?_
    sorry

  sorry

/-- An entire function whose iterated derivatives at zero are all nonnegative real (except
possibly the value itself) has values of the form `f 0 + nonneg. real` along the nonnegative
real axis. -/
theorem at_zero_le_of_iteratedDeriv_nonneg {f : â„‚ â†’ â„‚} (hf : Differentiable â„‚ f)
    (h : âˆ€ n â‰  0, 0 â‰¤ iteratedDeriv n f 0) {z : â„‚} (hz : 0 â‰¤ z) : f 0 â‰¤ f z := by
  have h' (n : â„•) : 0 â‰¤ iteratedDeriv n (f Â· - f 0) 0
  Â· cases n with
  | zero => simp only [iteratedDeriv_zero, sub_self, le_refl]
  | succ n =>
      specialize h n.succ <| succ_ne_zero n
      rw [iteratedDeriv_succ'] at h âŠ¢
      convert h using 2
      ext w
      exact deriv_sub_const (f 0)
  exact sub_nonneg.mp <| nonneg_of_iteratedDeriv_nonneg (hf.sub_const (f 0)) h' hz

/-- An entire function whose iterated derivatives at zero are all real with alternating signs
(except possibly the value itself) has values of the form `f 0 + nonneg. real` along the nonpositive
real axis. -/
theorem at_zero_le_of_iteratedDeriv_alternating {f : â„‚ â†’ â„‚} (hf : Differentiable â„‚ f)
    (h : âˆ€ n â‰  0, 0 â‰¤ (-1) ^ n * iteratedDeriv n f 0) {z : â„‚} (hz : z â‰¤ 0) : f 0 â‰¤ f z := by
  let F : â„‚ â†’ â„‚ := fun z â†¦ f (-z)
  convert at_zero_le_of_iteratedDeriv_nonneg (f := F) (hf.comp <| differentiable_neg)
    (fun n hn â†¦ ?_) (neg_nonneg.mpr hz) using 1
  Â· simp only [neg_zero]
  Â· simp only [neg_neg]
  Â· simpa only [iteratedDeriv.comp_neg, neg_zero] using h n hn

end Complex
