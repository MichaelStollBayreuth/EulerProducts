import Mathlib

/-- Translation in the domain does not change the derivative. -/
lemma HasDerivAt.comp_const_add {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] (a x : ğ•œ) {ğ•œ' : Type*}
    [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] {h : ğ•œ â†’ ğ•œ'} {h' : ğ•œ'}
    (hh : HasDerivAt h h' (a + x)) :
    HasDerivAt (fun x â†¦ h (a + x)) h' x  := by
  simpa [Function.comp_def] using HasDerivAt.scomp (ğ•œ := ğ•œ) x hh <| hasDerivAt_id' x |>.const_add a

/-- Translation in the domain does not change the derivative. -/
lemma HasDerivAt.comp_add_const {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] (x a : ğ•œ) {ğ•œ' : Type*}
    [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] {h : ğ•œ â†’ ğ•œ'} {h' : ğ•œ'}
    (hh : HasDerivAt h h' (x + a)) :
    HasDerivAt (fun x â†¦ h (x + a)) h' x  := by
  simpa [Function.comp_def] using HasDerivAt.scomp (ğ•œ := ğ•œ) x hh <| hasDerivAt_id' x |>.add_const a


namespace intervalIntegral

/-- A variant of the Fundamental theorem of calculus-2 involving integrating over the
unit interval. -/
lemma integral_unitInterval_eq_sub {C E : Type*} [NontriviallyNormedField C]
    [NormedAlgebra â„ C] [NormedAddCommGroup E] [NormedSpace â„ E] [NormedSpace C E]
    [CompleteSpace E] [IsScalarTower â„ C E] {f f' : C â†’ E} {zâ‚€ zâ‚ : C}
    (hcont : ContinuousOn (fun t : â„ â†¦ f' (zâ‚€ + t â€¢ zâ‚)) (Set.Icc 0 1))
    (hderiv : âˆ€ t âˆˆ Set.Icc (0 : â„) 1, HasDerivAt f (f' (zâ‚€ + t â€¢ zâ‚)) (zâ‚€ + t â€¢ zâ‚)) :
    zâ‚ â€¢ âˆ« t in (0 : â„)..1, f' (zâ‚€ + t â€¢ zâ‚) = f (zâ‚€ + zâ‚) - f zâ‚€ := by
  let Î³ (t : â„) : C := zâ‚€ + t â€¢ zâ‚
  have hint : IntervalIntegrable (zâ‚ â€¢ (f' âˆ˜ Î³)) MeasureTheory.volume 0 1 :=
    (ContinuousOn.const_smul hcont zâ‚).intervalIntegrable_of_Icc zero_le_one
  have hderiv' : âˆ€ t âˆˆ Set.uIcc (0 : â„) 1, HasDerivAt (f âˆ˜ Î³) (zâ‚ â€¢ (f' âˆ˜ Î³) t) t
  Â· intro t ht
    refine (hderiv t <| (Set.uIcc_of_le (Î± := â„) zero_le_one).symm â–¸ ht).scomp t ?_
    have : HasDerivAt (fun t : â„ â†¦ t â€¢ zâ‚) zâ‚ t
    Â· convert (hasDerivAt_id t).smul_const (F := C) _ using 1
      simp only [one_smul]
    exact this.const_add zâ‚€
  convert (integral_eq_sub_of_hasDerivAt hderiv' hint) using 1
  Â· simp_rw [â† integral_smul, Function.comp_apply]
  Â· simp only [Function.comp_apply, one_smul, zero_smul, add_zero]

end intervalIntegral


namespace Complex

/-!
### Define the "slit plane" `â„‚ âˆ– â„â‰¤0` and provide some API
-/

/-- The *slit plane* is the complex plane with the closed negative real axis removed. -/
def slitPlane : Set â„‚ := {z | 0 < z.re âˆ¨ z.im â‰  0}

lemma mem_slitPlane_iff {z : â„‚} : z âˆˆ slitPlane â†” 0 < z.re âˆ¨ z.im â‰  0 := Iff.rfl

/-- An alternative description of the slit plane as consisting of nonzero complex numbers
whose argument is not Ï€. -/
lemma mem_slitPlane_iff' {z : â„‚} : z âˆˆ slitPlane â†” z.arg â‰  Real.pi âˆ§ z â‰  0 := by
  simp only [mem_slitPlane_iff, ne_eq, arg_eq_pi_iff, not_and]
  refine âŸ¨fun H â†¦ âŸ¨fun h â†¦ H.resolve_left fun h' â†¦ lt_irrefl 0 <| h'.trans h, fun h â†¦ ?_âŸ©,
          fun H â†¦ ?_âŸ©
  Â· simp only [h, zero_re, lt_self_iff_false, zero_im, not_true_eq_false, or_self] at H
  Â· by_contra' h
    simp only [h.2, not_true_eq_false] at H
    have hâ‚ : z = 0 â†” z.re = 0 âˆ§ z.im = 0 := ext_iff
    have hâ‚‚ : z.re â‰¤ 0 â†” z.re = 0 âˆ¨ z.re < 0 := le_iff_eq_or_lt
    tauto

lemma slitPlane_ne_zero {z : â„‚} (hz : z âˆˆ slitPlane) : z â‰  0 :=
  (mem_slitPlane_iff'.mp hz).2

lemma slitPlane_arg_ne_pi {z : â„‚} (hz : z âˆˆ slitPlane) : z.arg â‰  Real.pi :=
  (mem_slitPlane_iff'.mp hz).1

/-- The slit plane is star-shaped with respect to the point `1`. -/
lemma slitPlane_star_shaped {z : â„‚} (hz : 1 + z âˆˆ slitPlane) {t : â„} (ht : t âˆˆ Set.Icc 0 1) :
    1 + t * z âˆˆ slitPlane := by
  rw [Set.mem_Icc] at ht
  simp only [slitPlane, Set.mem_setOf_eq, add_re, one_re, add_im, one_im, zero_add, mul_re,
    ofReal_re, ofReal_im, zero_mul, sub_zero, mul_im, add_zero, mul_eq_zero] at hz âŠ¢
  by_contra' H
  simp only [mul_eq_zero] at H hz
  have htâ‚€ : t â‰  0
  Â· rintro rfl
    simp only [zero_mul, add_zero, true_or, and_true] at H
    norm_num at H
  simp only [htâ‚€, false_or] at H
  replace hz := hz.neg_resolve_right H.2
  replace H := H.1
  have H' := mul_pos (ht.1.eq_or_lt.resolve_left htâ‚€.symm) hz
  nlinarith

/-- The slit plane contains the open unit ball of radius `1` around `1`. -/
lemma mem_slitPlane_of_norm_lt_one {z : â„‚} (hz : â€–zâ€– < 1) : 1 + z âˆˆ slitPlane := by
  simp only [slitPlane, Set.mem_setOf_eq, add_re, one_re, add_im, one_im, zero_add]
  simp only [norm_eq_abs] at hz
  by_contra' H
  linarith only [H.1, neg_lt_of_abs_lt <| (abs_re_le_abs z).trans_lt hz]

/-!
### Some missing differentiability statements on the complex log
-/

lemma hasDerivAt_log {z : â„‚} (hz : z âˆˆ slitPlane) : HasDerivAt log zâ»Â¹ z :=
  HasStrictDerivAt.hasDerivAt <| hasStrictDerivAt_log hz

lemma differentiableAt_log {z : â„‚} (hz : z âˆˆ slitPlane) : DifferentiableAt â„‚ log z :=
  (hasDerivAt_log hz).differentiableAt

/-!
### Integral representation of the complex log
-/

lemma continousOn_one_add_mul_inv {z : â„‚} (hz : 1 + z âˆˆ slitPlane) :
    ContinuousOn (fun t : â„ â†¦ (1 + t * z)â»Â¹) (Set.Icc 0 1) :=
  ContinuousOn.invâ‚€ (Continuous.continuousOn (by continuity))
    (fun t ht â†¦ slitPlane_ne_zero <| slitPlane_star_shaped hz ht)

open intervalIntegral in
/-- Represent `log (1 + z)` as an integral over the unit interval -/
lemma log_eq_integral {z : â„‚} (hz : 1 + z âˆˆ slitPlane) :
    log (1 + z) = z * âˆ« (t : â„) in (0 : â„)..1, (1 + t * z)â»Â¹ := by
  convert (integral_unitInterval_eq_sub (continousOn_one_add_mul_inv hz)
    (fun _ ht â†¦ hasDerivAt_log <| slitPlane_star_shaped hz ht)).symm using 1
  simp only [log_one, sub_zero]

/-- Represent `log (1 - z)â»Â¹` as an integral over the unit interval -/
lemma log_inv_eq_integral {z : â„‚} (hz : 1 - z âˆˆ slitPlane) :
    log (1 - z)â»Â¹ = z * âˆ« (t : â„) in (0 : â„)..1, (1 - t * z)â»Â¹ := by
  rw [sub_eq_add_neg 1 z] at hz âŠ¢
  rw [log_inv _ <| slitPlane_arg_ne_pi hz, neg_eq_iff_eq_neg, â† neg_mul]
  convert log_eq_integral hz using 5
  rw [sub_eq_add_neg, mul_neg]

/-!
### The Taylor polynomials of the logarithm
-/

open BigOperators

local macro_rules | `($x ^ $y)   => `(HPow.hPow $x $y)

/-- The `n`th Taylor polynomial of `log` at `1`, as a function `â„‚ â†’ â„‚` -/
noncomputable
def logTaylor (n : â„•) : â„‚ â†’ â„‚ := fun z â†¦ âˆ‘ j in Finset.range n, (-1) ^ (j + 1) * z ^ j / j

lemma logTaylor_zero : logTaylor 0 = fun _ â†¦ 0 := by
  funext
  simp only [logTaylor, Finset.range_zero, Nat.odd_iff_not_even, Int.cast_pow, Int.cast_neg,
    Int.cast_one, Finset.sum_empty]

lemma logTaylor_succ (n : â„•) :
    logTaylor (n + 1) = logTaylor n + (fun z : â„‚ â†¦ (-1) ^ (n + 1) * z ^ n / n) := by
  funext
  simpa only [logTaylor] using Finset.sum_range_succ ..

lemma logTaylor_at_zero (n : â„•) : logTaylor n 0 = 0 := by
  induction n with
  | zero => simp [logTaylor_zero]
  | succ n ih => simpa [logTaylor_succ, ih] using (Nat.eq_zero_or_pos n).symm

lemma hasDerivAt_logTaylor (n : â„•) (z : â„‚) :
    HasDerivAt (logTaylor (n + 1)) (âˆ‘ j in Finset.range n, (-1) ^ j * z ^ j) z := by
  induction n with
  | zero => simp [logTaylor_succ, logTaylor_zero, Pi.add_def, hasDerivAt_const]
  | succ n ih =>
    rw [logTaylor_succ]
    simp only [cpow_nat_cast, Nat.cast_add, Nat.cast_one, Nat.odd_iff_not_even,
      Finset.sum_range_succ, (show (-1) ^ (n + 1 + 1) = (-1) ^ n by ring)]
    refine HasDerivAt.add ih ?_
    simp only [Nat.odd_iff_not_even, Int.cast_pow, Int.cast_neg, Int.cast_one, mul_div_assoc]
    have : HasDerivAt (fun x : â„‚ â†¦ (x ^ (n + 1) / (n + 1))) (z ^ n) z
    Â· simp_rw [div_eq_mul_inv]
      convert HasDerivAt.mul_const (hasDerivAt_pow (n + 1) z) (((n : â„‚) + 1)â»Â¹) using 1
      field_simp [Nat.cast_add_one_ne_zero n]
      ring
    exact HasDerivAt.const_mul _ this

/-!
### Bounds for the difference between log and its Taylor polynomials
-/

lemma hasDerivAt_log_sub_logTaylor (n : â„•) {z : â„‚} (hz : 1 + z âˆˆ slitPlane) :
    HasDerivAt (fun z : â„‚ â†¦ log (1 + z) - logTaylor (n + 1) z) ((-z) ^ n * (1 + z)â»Â¹) z := by
  convert HasDerivAt.sub ((hasDerivAt_log hz).comp_const_add 1 z) (hasDerivAt_logTaylor n z) using 1
  push_cast
  have hz' : -z â‰  1
  Â· intro H
    rw [neg_eq_iff_eq_neg] at H
    simp only [H, add_right_neg] at hz
    exact slitPlane_ne_zero hz rfl
  simp_rw [â† mul_pow, neg_one_mul, geom_sum_eq hz', â† neg_add', div_neg, add_comm z]
  field_simp [add_comm z 1 â–¸ slitPlane_ne_zero hz]

/-- Bound `â€–(1 + t * z)â»Â¹â€–` for `0 â‰¤ t â‰¤ 1` and `â€–zâ€– < 1` -/
lemma norm_one_add_mul_inv_le {t : â„} (ht : t âˆˆ Set.Icc 0 1) {z : â„‚} (hz : â€–zâ€– < 1) :
    â€–(1 + t * z)â»Â¹â€– â‰¤ (1 - â€–zâ€–)â»Â¹ := by
  rw [Set.mem_Icc] at ht
  rw [norm_inv, norm_eq_abs]
  refine inv_le_inv_of_le (by linarith) ?_
  calc 1 - â€–zâ€–
    _ â‰¤ 1 - t * â€–zâ€– := by
      nlinarith [norm_nonneg z]
    _ = 1 - â€–t * zâ€– := by
      rw [norm_mul, norm_eq_abs (t :â„‚), abs_of_nonneg ht.1]
    _ â‰¤ â€–1 + t * zâ€– := by
      rw [â† norm_neg (t * z), â† sub_neg_eq_add]
      convert norm_sub_norm_le 1 (-(t * z))
      exact norm_one.symm

lemma integrable_pow_mul_norm_one_add_mul_inv (n : â„•) {z : â„‚} (hz : â€–zâ€– < 1) :
  IntervalIntegrable (fun t : â„ â†¦ t ^ n * â€–(1 + t * z)â»Â¹â€–) MeasureTheory.volume 0 1 := by
  have := continousOn_one_add_mul_inv <| mem_slitPlane_of_norm_lt_one hz
  rw [â† Set.uIcc_of_le zero_le_one] at this
  exact ContinuousOn.intervalIntegrable <|
    Continuous.continuousOn (by continuity) |>.mul <| ContinuousOn.norm this

open intervalIntegral in
/-- The difference of `log (1+z)` and its `(n+1)`st Taylor polynomial can be bounded in
terms of `â€–zâ€–`. -/
lemma log_sub_logTaylor_norm_le (n : â„•) {z : â„‚} (hz : â€–zâ€– < 1) :
    â€–log (1 + z) - logTaylor (n + 1) zâ€– â‰¤ â€–zâ€– ^ (n + 1) * (1 - â€–zâ€–)â»Â¹ / (n + 1) := by
  have help : IntervalIntegrable (fun t : â„ â†¦ t ^ n * (1 - â€–zâ€–)â»Â¹) MeasureTheory.volume 0 1 :=
    IntervalIntegrable.mul_const (Continuous.intervalIntegrable (by continuity) 0 1) (1 - â€–zâ€–)â»Â¹
  let f (z : â„‚) : â„‚ := log (1 + z) - logTaylor (n + 1) z
  let f' (z : â„‚) : â„‚ := (-z) ^ n * (1 + z)â»Â¹
  have hderiv : âˆ€ t âˆˆ Set.Icc (0 : â„) 1, HasDerivAt f (f' (0 + t * z)) (0 + t * z)
  Â· intro t ht
    rw [zero_add]
    exact hasDerivAt_log_sub_logTaylor n <|
      slitPlane_star_shaped (mem_slitPlane_of_norm_lt_one hz) ht
  have hcont : ContinuousOn (fun t : â„ â†¦ f' (0 + t * z)) (Set.Icc 0 1)
  Â· simp only [zero_add, zero_le_one, not_true_eq_false]
    exact (Continuous.continuousOn (by continuity)).mul <|
      continousOn_one_add_mul_inv <| mem_slitPlane_of_norm_lt_one hz
  have H : f z = z * âˆ« t in (0 : â„)..1, (-(t * z)) ^ n * (1 + t * z)â»Â¹
  Â· convert (integral_unitInterval_eq_sub hcont hderiv).symm using 1
    Â· simp only [zero_add, add_zero, log_one, logTaylor_at_zero, sub_self, sub_zero]
    Â· simp only [add_zero, log_one, logTaylor_at_zero, sub_self, real_smul, zero_add, smul_eq_mul]
  simp only [H, norm_mul]
  simp_rw [neg_pow (_ * z) n, mul_assoc, intervalIntegral.integral_const_mul, mul_pow,
    mul_comm _ (z ^ n), mul_assoc, intervalIntegral.integral_const_mul, norm_mul, norm_pow,
    norm_neg, norm_one, one_pow, one_mul, â† mul_assoc, â† pow_succ, mul_div_assoc]
  refine mul_le_mul_of_nonneg_left ?_ (pow_nonneg (norm_nonneg z) (n + 1))
  calc â€–âˆ« t in (0 : â„)..1, (t : â„‚) ^ n * (1 + t * z)â»Â¹â€–
    _ â‰¤ âˆ« t in (0 : â„)..1, â€–(t : â„‚) ^ n * (1 + t * z)â»Â¹â€– :=
        intervalIntegral.norm_integral_le_integral_norm zero_le_one
    _ = âˆ« t in (0 : â„)..1, t ^ n * â€–(1 + t * z)â»Â¹â€– := by
        refine intervalIntegral.integral_congr <| fun t ht â†¦ ?_
        rw [Set.uIcc_of_le zero_le_one, Set.mem_Icc] at ht
        simp_rw [norm_mul, norm_pow, norm_eq_abs, abs_of_nonneg ht.1]
    _ â‰¤ âˆ« t in (0 : â„)..1, t ^ n * (1 - â€–zâ€–)â»Â¹ :=
        intervalIntegral.integral_mono_on zero_le_one
          (integrable_pow_mul_norm_one_add_mul_inv n hz) help <|
          fun t ht â†¦ mul_le_mul_of_nonneg_left (norm_one_add_mul_inv_le ht hz)
                       (pow_nonneg ((Set.mem_Icc.mp ht).1) _)
    _ = (1 - â€–zâ€–)â»Â¹ / (n + 1) := by
        rw [intervalIntegral.integral_mul_const, mul_comm, integral_pow]
        field_simp

lemma norm_log_sub_self_le {z : â„‚} (hz : â€–zâ€– < 1) :
    â€–log (1 + z) - zâ€– â‰¤ â€–zâ€– ^ 2 * (1 - â€–zâ€–)â»Â¹ / 2 := by
  convert log_sub_logTaylor_norm_le 1 hz using 2
  Â· simp [logTaylor_succ, logTaylor_zero, sub_eq_add_neg]
  Â· norm_num

lemma log_inv_add_logTaylor_neg_norm_le (n : â„•) {z : â„‚} (hz : â€–zâ€– < 1) :
    â€–log (1 - z)â»Â¹ + logTaylor (n + 1) (-z)â€– â‰¤ â€–zâ€– ^ (n + 1) * (1 - â€–zâ€–)â»Â¹ / (n + 1) := by
  rw [sub_eq_add_neg,
    log_inv _ <| slitPlane_arg_ne_pi <| mem_slitPlane_of_norm_lt_one <| (norm_neg z).symm â–¸ hz,
    â† sub_neg_eq_add, â† neg_sub', norm_neg]
  convert log_sub_logTaylor_norm_le n <| (norm_neg z).symm â–¸ hz using 4 <;> rw [norm_neg]

lemma norm_log_inv_sub_self_le {z : â„‚} (hz : â€–zâ€– < 1) :
    â€–log (1 - z)â»Â¹ - zâ€– â‰¤ â€–zâ€– ^ 2 * (1 - â€–zâ€–)â»Â¹ / 2 := by
  convert log_inv_add_logTaylor_neg_norm_le 1 hz using 2
  Â· simp [logTaylor_succ, logTaylor_zero, sub_eq_add_neg]
  Â· norm_num

end Complex
